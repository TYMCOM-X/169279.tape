	:TARGET PROFILE DATABASE ASSEMBLY MACROS.

	:THE CREATION OF A TARGET PROFILE DATABASE PROCEEDS IN 4 STAGES.
	:STAGE 1 - START WITH THE TPSTRT MACRO, WHICH DEFINES THE NUMBER
	:	OF SUBNETS IN THE NETWORK.  SUBNETS WILL BE NUMBERED
	:	FROM 1 TO THE PARAMETER VALUE.  0 FOR A SUBNET NUMBER
	:	IMPLIES THE MAIN NETWORK.
	:STAGE 2 - TARGET PROFILES ARE DEFINED AND LABLED USING
	:	USER SUPPLIED LABLES.  THESE TARGET PROFILES ARE
	:	GENERATED USING THE DEFTP AND THE ADDTP MACRO.  THE USER
	:	SUPPLIED NAMES WILL BE USED TO LINK THE LOGIN SUPPLIED
	:	ADDRESS (DNIC/TNIC COMBINATION) WITH THE TARGET PROFILE.
	:STAGE 3 - LINKAGES BETWEEN THE DNIC/TNIC ADDRESS AND THE TARGET
	:	PROFILES IS ESTABLISHED.  SINCE THIS LINKAGE IS CONTINGENT
	:	ON THE ORIGINATION SUBNET NUMBER, ONE FIRST USES THE
	:	SNET MACRO TO DEFINE THE SUBNET FOR WHICH THE FOLLOWING
	:	DEFINITIONS APPLY.  THEN ONE USES THE DNIC MACRO TO ESTABLISH
	:	THE LINKAGE BETWEEN A PRIMARY UNQUALIFIED ADDRESS (DNIC
	:	WITH NO TNIC), AND ONE USES THE TNIC MACRO TO ESTABLISH THE
	:	LINKAGE FOR QUALIFICATIONS OF THE DNIC BY A TNIC.
	:	BOTH THE DNIC AND TNIC MACROS SERVE TO ESTABLISH FORMATTED
	:	LOOKUP TABLES.
	:STAGE 4 - ONE ISSUES THE TPEND MACRO TO ACTUALLY CREATE THE
	:	DATA AREA.



	:START MACRO, DEFINES NUMBER OF SUBNETS IN NETWORK AND START
	:ASSEMBLY LOCATION (DEFAULT IS 0)

	:NUMBER IS MAXIMUM SUBNET NUMBER.  ZERO IMPLIES THAT ONLY THE MAIN
	:NETWORK IS INVOLVED.

TPSTRT	MACRO(N.2,N.3) [
	IF	N.2 0
SNET.S	EQ	N.2
	ELSE
SNET.S	EQ	0
	EI

	IF	N.3 0
DDSTRT	EQ	N.3
	ELSE
DDSTRT	EQ	0
	EI

	ORG	DDSTRT

S.DXP	EQ	2		:INITIAL DNIC TABLE INDEX
S.TXP	EQ	2		:INITIAL TNIC TABLE INDEX
TP.DXP	EQ	0		:INITIAL PROFILE DEFINITION INDEX
TP.N	EQ	0		:START PROFILE INDEX NUMBER
FLG.T	EQ	0		:SHOW NO UNTERMINATED SECONDARY SEARCH TABLE
]


	:DEFINE A TARGET PROFILE USING NAME N.1, WHERE FIRST
	:LINE OF TARGET PROFILE CONTAINS HOSTS Y.1 THRU Y.C.
	:HOST LIST MAY BE TERMINATED EARLY.  THE NAME ASSIGNED TO
	:A TARGET PROFILE MUST NOT CONTAIN A PERIOD, MAY
	:NOT CONTAIN MORE THAN SIX LETTERS OR NUMBERS AND
	:IT MUST START WITH A LETTER.


DEFTP	MACRO(N.1,Y.1,Y.2,Y.3,Y.4,Y.5,Y.6,Y.7,Y.8,Y.9,Y.A,Y.B,Y.C) [
X.1	EQ	Y.1 0
X.2	EQ	Y.1 1
	IF	X.2-X.1
	REMARK %NULL TARGET LIST FOR INITIAL PROFILE LIST FOR NAME N.1 %
	ELSE
	IF	\N.1
	REMARK %ERROR - NAME N.1 ALREADY IN USE
	ELSE

TP.N	EQ	TP.N+1	:GO TO NEXT PROFILE INDEX
TP.DXP	EQ	TP.DXP+2	:TERMINATE PREVIOUS PROFILE
N.1	EQ	TP.N		:ASSIGN PROFILE NUMBER TP PROFILE NAME
TP.SXP	EQ	TP.DXP		:MARK SIZE POSITION FOR THIS LINE
P.|TP.N| EQ	TP.DXP		:OFFSET TO POSITION OF TARGET PROFILE
TP.DXP	EQ	TP.DXP+2

T.|TP.DXP| EQ $A Y.1 0
TP.DXP	EQ	TP.DXP+2

	IF	Y.2 0
T.|TP.DXP| EQ $A Y.2
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.3 0
T.|TP.DXP| EQ $A Y.3
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.4 0
T.|TP.DXP| EQ $A Y.4
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.5 0
T.|TP.DXP| EQ $A Y.5
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.6 0
T.|TP.DXP| EQ $A Y.6
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.7 0
T.|TP.DXP| EQ $A Y.7
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.8 0
T.|TP.DXP| EQ $A Y.8
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.9 0
T.|TP.DXP| EQ $A Y.9
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.A 0
T.|TP.DXP| EQ $A Y.A
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.B 0
T.|TP.DXP| EQ $A Y.B
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.C 0
T.|TP.DXP| EQ $A Y.C
TP.DXP	EQ	TP.DXP+2
	EI

T.|TP.DXP| EQ	0		:INSURE TO TERMINATE THIS LINE IF NO MORE
T.|TP.SXP| EQ	TP.DXP-TP.SXP	:SETUP SIZE OF THIS PROFILE LINE
	EI
	EI
]


	:ADD ANOTHER HOST TO PREVIOUSLY DEFINED TARGET PROFILE
ADDTP	MACRO(Y.1,Y.2,Y.3,Y.4,Y.5,Y.6,Y.7,Y.8,Y.9,Y.A,Y.B,Y.C) [
X.1	EQ	Y.1 0
X.2	EQ	Y.1 1
	IF	X.2-X.1
	REMARK %NULL TARGET LIST FOR ADDITIONAL PROFILE LIST FOR NAME N.1 %
	ELSE

TP.SXP	EQ	TP.DXP		:MARK SIZE INDEX FOR THIS LINE
TP.DXP	EQ	TP.DXP+2


T.|TP.DXP| EQ $A Y.1
TP.DXP	EQ	TP.DXP+2

	IF	Y.2 0
T.|TP.DXP| EQ $A Y.2
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.3 0
T.|TP.DXP| EQ $A Y.3
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.4 0
T.|TP.DXP| EQ $A Y.4
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.5 0
T.|TP.DXP| EQ $A Y.5
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.6 0
T.|TP.DXP| EQ $A Y.6
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.7 0
T.|TP.DXP| EQ $A Y.7
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.8 0
T.|TP.DXP| EQ $A Y.8
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.9 0
T.|TP.DXP| EQ $A Y.9
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.A 0
T.|TP.DXP| EQ $A Y.A
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.B 0
T.|TP.DXP| EQ $A Y.B
TP.DXP	EQ	TP.DXP+2
	EI

	IF	Y.C 0
T.|TP.DXP| EQ $A Y.C
TP.DXP	EQ	TP.DXP+2
	EI

T.|TP.DXP| EQ	0	:INSURE TERMINATOR DEFINED
T.|TP.SXP| EQ	TP.DXP-TP.SXP	:MAKE SIZE ENTRY FOR THIS LINE
	EI
]

	:DEFINE SUBNET NUMBER FOR SUBSEQUENT LINK DEFINITIONS
SNET	MACRO(S.NUM) [
S.N	EQ	$A S.NUM
	IF	S.N-SNET.S
	REMARK %SUBNET NUMBER TOO LARGE - 
	NUMBER $A S.N
	REMARK %
	EI
	IF	S.DXP-2
D.|S.DXP| EQ	0		:TERMINATE PRIOR PRIMARY SEARCH TABLE
S.DXP	EQ	S.DXP+2		:START NEW PRIMARY SEARCH TABLE HERE
	EI
S|S.N|.X EQ	S.DXP		:START OF PRIMARY SEARCH TABLE FOR THIS SUBNET
]


	:DEFINE THE NEXT VALID DNIC FOR CURRENT SUBNET.  THIS IS
	:PRIMARY NAME IN THE "MMMMTNNNN" STYLE NAME.
	:IF THERE IS NO TARGET PROFILE, THEN T.PNA SHOULD BE NULL
DNIC	MACRO(DN.1,T.PNA) [

	IF	FLG.T
E.|S.TXP| EQ	0		:TERMINATE OUTSTANDING SECONDARY SEARCH TABLE
S.TXP	EQ	S.TXP+2
FLG.T	EQ	0
	EI
FLG.D	EQ	1		:SET FLAG TO SHOW STARTING NEW PRIMARY NAME

D.|S.DXP| EQ	DN.1		:INSERT DNIC IN SEARCH TABLE
S.DXP	EQ	S.DXP+2

	IF	T.PNA 0
D.|S.DXP| EQ	P.|T.PNA| 	:INSERT TARGET PROFILE INDEX
	ELSE
D.|S.DXP|	EQ	0
	EI
S.DXP	EQ	S.DXP+2

D.|S.DXP| EQ	0		:INITIALIZE SECONDARY INDEX TO 0
D.TPTR	EQ	S.DXP		:POINT TO SECONDARY INDEX INDEX
S.DXP	EQ	S.DXP+2
]



	:DEFINE TNIC QUALIFIERS FOR CURRENT DNIC
	:THIS MACRO SETS UP LINKAGES FOR DNIC/TNIC
	:COMBINATIONS.  THERE MUST BE A TARGET PROFILE NAME.
TNIC	MACRO(TN.1,T.PNA) [
	IF	1-T.PNA 0
	REMARK %TARGET PROFILE NAME MUST BE SUPPLIED FOR TNIC MACRO
	ELSE
	IF	FLG.D
D.|D.TPTR| EQ	S.TXP		:SETUP PTR TO SECONDARY SEARCH TABLE
FLG.D	EQ	0
FLG.T	EQ	1		:SHOW THERE IS AN UNTERMINATED TNIC TBL
	EI

E.|S.TXP| EQ	TN.1		:SETUP TNIC NUMBER
S.TXP	EQ	S.TXP+2
E.|S.TXP| EQ	P.|T.PNA|	:SETUP PTR INDEX FOR TARGET PROFILE
S.TXP	EQ	S.TXP+2
	EI
]



	:TPEND -
	:FINISH MACRO TO CREATE ACTUAL TARGET PROFILE DATA AREA
TPEND	MACRO [
P2.A	EQ	4+(4*SNET.S)
	GL	DDSTRT,DDSIZE
DDSTRT	EQ	.
D.|S.DXP| EQ	0		:TERMINATE PRIMARY SEARCH TABLE AREA
E.|S.TXP| EQ	0		:TERMINATE SECONDARY SEARCH TABLE AREA
P3.A	EQ	P2.A+S.DXP	:OFFSET TO BEGINNING OF SECONDARY SEARCH TABLES
P4.A	EQ	P3.A+S.TXP	:OFFSET TO BEGINNING OF PROFILE DEFINTIONS

	ORG	DDSTRT
	:DEFINE OFFSETS TO PRIMARY SEARCH TABLES BY SUBNET
TEQ	EQ	0
	RE
	IF	\S|TEQ|.X
	WC	P2.A-2+S|TEQ|.X :DEINE OFFSET
	ELSE
	WC	0		:NOTHING FOR THIS SUBNET||
	EI
TEQ	EQ	TEQ+1
TEQ1	EQ	SNET.S+1-TEQ
	ER	TEQ1

	:INSERT PRIMARY SEARCH TABLES
TEQ	EQ	2		:START INDEX
	RE
	HC	D.|TEQ|
	IF	D.|TEQ|
TEQ	EQ	TEQ+2
	IF	D.|TEQ|
	HC	P4.A-2+D.|TEQ| :SETUP THIS ENTRY
	ELSE
	HC	0		:NO PRIMARY DEFINITION
	EI
TEQ	EQ	TEQ+2
	IF	D.|TEQ|
	HC	P3.A-2+D.|TEQ|
	ELSE
	HC	0
	EI
TEQ	EQ	TEQ+2
	ELSE
TEQ	EQ	TEQ+2
	EI
TEQ1	EQ	S.DXP+2-TEQ
	ER	TEQ1


	:INSERT SECONDARY SEARCH TABLES
TEQ	EQ	2		:START INDEX
	RE
	HC	E.|TEQ|
	IF	E.|TEQ|
TEQ	EQ	TEQ+2
	IF	E.|TEQ|
	HC	P4.A-2+E.|TEQ| :SETUP THIS ENTRY
	ELSE
	HC	0		:NO SECONDARY DEFINITION
	EI
TEQ	EQ	TEQ+2
	ELSE
TEQ	EQ	TEQ+2
	EI
TEQ1	EQ	S.TXP+2-TEQ
	ER	TEQ1


	:INSERT TARGET PROFILES PROPER
TEQ	EQ	2
	RE
	HC	T.|TEQ|
TEQ	EQ	TEQ+2
TEQ1	EQ	TP.DXP+2-TEQ
	ER	TEQ1

	:DISPLAY SIZE OF AREA
TPSIZE	EQ	.
	REMARK %SIZE OF TARGET PROFILE LOOKUP AND DEFINITION AREA IS 
	NUMBER TPSIZE
	REMARK  HEX BYTES
TPSEC	EQ	(TPSIZE+0FF)/100
	REMARK %SUPERVISOR AREA ALLOCATION SHOULD BE AT LEAST 
	NUMBER TPSEC
	REMARK  PAGES%
DDSIZE	EQ	100*TPSEC
	REMARK %%TARGET PROFILE FILE ASSEMBLED STARTING AT HEX 
	NUMBER DDSTRT
	REMARK %LOAD FILE TO CORE, AND SAVE TO DISK SAVE AREA
	REMARK %
]

    