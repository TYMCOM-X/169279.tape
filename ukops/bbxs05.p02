	RA	0

:**********************************************************************
:**********************************************************************
:	BBXS 5.02 PATCH FILE
:**********************************************************************
:**********************************************************************

	REMARK	%
	REMARK	%*********************************************************
	REMARK	%*                                                       *
	REMARK	%*  If the PATCH1 area overflows, it will be necessary   *
	REMARK	%*  to include the following statement in the Tymfile:   *
	REMARK	%*                                                       *
	REMARK	%*       OPTION(P1SIZE,2048)                             *
	REMARK	%*                                                       *
	REMARK	%*  If the Patch History area overflows, it will be      *
	REMARK  %*  necessary to include the following statement in      *
	REMARK  %*  the Tymfile:                                         *
	REMARK  %*                                                       *
	REMARK  %*       OPTION(PHSIZE,640)                              *
	REMARK  %*                                                       *
	REMARK	%*********************************************************
	REMARK	%

:**********************************************************************
:
:	This patch is merely a test of the PATCH.LIB routine.  All it
:	does is provide a monitor for each line that keeps track of
:	how many times BSCPTP was called.  It does this in a halfword
:	table indexed by line number called CALCNT.
:
:	This patch serves no purpose but to validate the PATCH.LIB
:	routine.

	PATCH(850110,1021,E/BUELL,BSCPTP+48,,6)
	J	BSCMON,,		:JUMP TO THE PATCH

	CONPATCH(PA1PTR,,0C)
BSCMON	HS	0
	LO	BSCEXC
	ST	R4,PRUNL,R2		:REPLACE PATCHED INSTRUCTION
	FO	BSCEXC
	LIS	R4,1			:BUMP CALL COUNTER
	LO	FCB
	AHM	R4,CALCNT,RL2
	FO	FCB
	JR	R5			:RETURN FROM BSCPTP

	CONPATCH(PA0PTR,,(NBILIN*2))
	GL	CALCNT
CALCNT	HS	0			:DEFINE AND ZERO THE COUNTERS
	RE	NBILIN
	HC	0
	ER

	ENDPATCH(Count calls to BSCPTP for each line)


	IF	NMLV
:**********************************************************************
:
:	This patch fixes a bug in HASP that causes the interface to
:	not send a Request-To-Initiate (RTI) to the local device if
:	the stream being requested is suspended.  This is not right
:	because the RTI is not transmitted on the stream in question,
:	it is asking premission to open the stream.

	LO	BSCDEF
	LO	BSCEXC
	LO	BSCPTP

	PATCH(850827,1831,NTD.E/BUELL,MXDAT5+10,,6)
	J	RTIFX1,,

	CONPATCH(PA1PTR,,31)
RTIFX1	HS	0
	TBT	R4,RCVFCS,RL2		:IS THE STREAM SUSPENDED
	JN	MXDAT5+18,,		:NO

	LB	R5,BBUFER+RCBPS,R3,	:GET THE FIRST RCB
	LB	R5,REVTAB,R5,

	CHI	R5,RCBREQ		:IS IT REQUEST TO INITIATE
	JE	MXDAT5+18,,		:YES

	CHI	R5,RCBPER		:IS IT PERMIT TO INITIATE
	JE	MXDAT5+18,,		:YES

	J	MXDAT4,,

	ENDPATCH(Fix backpressured RTI problem)

	FO	BSCEXC
	FO	BSCDEF
	FO	BSCPTP

	EI	(NMLV)


	IF	VMSW
:**********************************************************************
:
:	This patch fixes a bug in IX300 that caused IRSs to be sent
:	to a virtual host as data.

	LO	IX100
	LO	FCB
	LO	IDS
	LO	LINE
	LO	EBCCHR

	PATCH(850923,1023,NTD.E/BUELL,BT3002,SDF302)
	IF	\NOCSC
	IF	NOCSC
	J	IRSFX1,,
	ELSE
	JR	RLINK
	EI	(NOCSC)
	ELSE
	JR	RLINK
	EI	(\NOCSC)

	IF	\NOCSC
	IF	NOCSC
	CONPATCH(PA1PTR,,15)
IRSFX1	LHL	R0,F.BIT,RAFCB,		:ARE WE IN TRANSPARENT MODE
	TBT	R0,TRFLG
	JNR	RLINK			:YES
	LA	RAOS,IX304,,		:CHECK FOR COMPONENT SELECT CHARACTER
	JR	RLINK
	EI	(NOCSC)
	EI	(\NOCSC)

	CONPATCH(SDF302,,8)
	NOP
	NOP

	CONPATCH(SDF302+28,,6)
	J	IRSFX2,,

	CONPATCH(PA1PTR,,1D)
IRSFX2	CLHI	RDATA,IRS		:IS IT AN IRS
	JE	EOR302,,		:YES
	CLHI	RDATA,IGS		:IS IT AN IGS
	JN	IGSNO,,			:NO
	LA	RAOS,IX303,,		:..ELSE GET BLANK COUNT
	JR	RLINK			:       ..

	IF	\NOCSC
	IF	NOCSC

	CONPATCH(EOR304,,6)
	J	IRSFX3,,

	CONPATCH(PA1PTR,,1D)
IRSFX3	LA	RAOS,IX302,,		:SWITCH BACK TO DATA STATE
	CLHI	RDATA,IRS		:IS IT AN IRS
	JE	EOR302,,		:YES
	LB	R0,F.DIAL,RAFCB,	:IF 2780
	J	EOR304+6,,

	CONPATCH(CSCNOA,,4)
	NOP

	EI	(NOCSC)
	EI	(\NOCSC)

	ENDPATCH(Fix IRS sent as data for virtual mode)

	FO	IX100
	FO	FCB
	FO	IDS
	FO	LINE
	FO	EBCCHR

	EI	(VMSW)


	IF	VMSW
:**********************************************************************
:
:	This patch fixes a problem in IR300 for Virtual mode.  This
:	bug caused records in frames sent to a 3780 device to be
:	separated by ITBs instead of IRSs.
:
:	Patch updated on 14-July-86 to include a check for transparency
:	mode.  E. Buell

	LO	IR100
	LO	FCB
	LO	IDS
	LO	LINE

	PATCH(860714,0924,NTD.E/BUELL,IRPUT+20,,6)
	J	IRSFX5,,

	CONPATCH(PA1PTR,,20)
IRSFX5	LIS	R0,XPRSW		:IS IT TRANSPARENT MODE
	TBT	R0,F.IRFL,RAFCB,
	JN	IRPUT9,,		:YES
	LHL	R4,F.AII,RAFCB,		:IF 3780
	LB	R0,F.DIAL,R4,		:..
	J	IRPUT+26,,

	ENDPATCH(Separate 3780 records with IRS)

	FO	IR100
	FO	FCB
	FO	IDS
	FO	LINE

	EI	(VMSW)


	IF	NMLV
:**********************************************************************
:
:	This patch fixes PVCs for HASP.

	LO	HSEC
	LO	IMS50
	LO	LINE
	LO	FCB

	PATCH(092585,1707,NTD.E/BUELL,PVC00M,,0C)
	NOP	,,,
	NOP
	NOPR

	CONPATCH(IMSPN3+34,,6)
	LB	R0,F.LINE,RAFCB,	:SET THE COLON LAST FLAG

:	THIS GOES AFTER THE DEFINITION OF XHMSG IN HSEC
	CONPATCH(PA0PTR,,4)
LSAV1	WS	1			:RELATIVE HOST ARGUMENT FOR HSTHPA

	CONPATCH(HSC18+16,,6)
	J	PVCFX1,,

	CONPATCH(PA1PTR,,15)
PVCFX1	LH	R5,L.RHOST,RL2,		:PASS THE RELATIVE HOST NUMBER TO HSTHPA
	JAL	R3,HSTHPA,,		:REPORT HOST PORT AVAILABILITY
	LA	RALS,LDOWN,,
	JR	RLINK

	CONPATCH(ANSSHT+12,,6)
	J	PVCFX2,,

	CONPATCH(PA1PTR,,13)
PVCFX2	ST	R5,LRET5,,		:SAVE RETURN LINKATGE
	ST	R1,LSAV1,,		:SAVE RELATIVE HOST NUMBER
	J	ANSSHT+18,,

	CONPATCH(ANSSH1,,6)
	J	PVCFX3,,

	CONPATCH(PA1PTR,,19)
PVCFX3	L	R5,LSAV1,,		:RESTORE THE RELATIVE HOST NUMBER
	JAL	R3,HSTHPA,,		:TELL THE SUP THE NUMBER OF PORTS AVAILABLE
	L	R15,LSAVF,,
	J	ANSSH1+8,,

	CONPATCH(HSTHPA+4,,6)
	LR	R5,R5
	NOP

	ENDPATCH(Fix PVCs for HASP)

	FO	HSEC
	FO	IMS50
	FO	LINE
	FO	FCB

	EI	(NMLV)


:**********************************************************************
:
:	This patch restores some code that was mistakenly deleted from
:	from MAIN, LINE and HSEC.  This deletion took the form of
:	eliminating the variable HSTPRT which was used to control
:	the Host Port Availability (HPA) messages for hostr port rotors.
:	Note:
:	CONPATCH(MHPRDY+0A,,6) was corrected to CONPATCH(MHPRDY+0C,,6)
:	(03/05/86 Bodo Krueger)

	LO	LINE
	LO	HSEC

	PATCH(851124,1118,NTD.E/BUELL,PA0PTR,,2*MAXHST)
:
:	(NOTE:  ADD THE FOLLOWING DATA DEFINITION TO THE MODULE LINE,
:	        AFTER THE DEFINITION OF THE VARIABLE HSTSTA.)
:
:	PORTS AVAILABLE FOR THIS HOST
HSTPRT	HS	MAXHST

	IF	MAXHST
	CONPATCH(DNHST1+42,,6)
	J	RTRFX1,,

	CONPATCH(PA1PTR,,13)
RTRFX1	STH	R0,HSTPRT,R5,R5		:INITIALIZE TO NO PORTS AVAILABLE
	JAL	R4,PUTCH,,
	J	DNHST1+48,,
	EI	(MAXHST)

	CONPATCH(HPRDY+8,,6)
	J	RTRFX2,,

	CONPATCH(PA1PTR,,0F)
RTRFX2	LIS	R0,1
	AHM	R0,HSTPRT,R1,R1		:INCREASE THE NUMBER OF PORTS AVAILABLE
	J	ANSSHT,,

	CONPATCH(MHPRDY+0C,,6)
	J	RTRFX3,,

	CONPATCH(PA1PTR,,13)
RTRFX3	LB	R0,L.MOVC,R2,		:GET THE CONFIGURED NUMBER OF OVCS
	AHM	R0,HSTPRT,R1,R1		:INCREASE THE NUMBER OF PORTS AVAILABLE
	J	ANSSHT,,

	CONPATCH(HPNRD0,,6)
	J	RTRFX4,,

	CONPATCH(PA1PTR,,13)
RTRFX4	LCS	R0,1
	AHM	R0,HSTPRT,R1,R1		:DECREMENT THE NUMBER OF PORTS AVAILABLE
	JGR	R5			:JUST RETURN IF SOME PORTS STILL READY
	LIS	R0,0			:HOST ANSWERED BUT NO PORTS AVAILABLE
	J	ANSSHT,,

	ENDPATCH(Fix host port rotors)

	FO	LINE
	FO	HSEC



:**********************************************************************
:
:	This patch fixes a bug in the /$NETCMD LOGON logic that caused
:	it to work for line 0 only.

	LO	FCB
	LO	R100

	PATCH(851124,1350,NTD.E/BUELL,CMLOG9-0A,,6)
	J	LOGFX1,,

	CONPATCH(PA1PTR,,15)
LOGFX1	LB	RL2,F.LINE,RAFCB,	:GET THE LINE NUMBER TIMES 2
	SLLS	RL2,1
	JAL	R5,BLDSTR,,		:GO QUEUE DATA STRINGS
	J	CMLOG9-4,,

	FO	R100

	IF	NMLV
	LO	R500

	CONPATCH(CMLOG9-0A,,6)
	J	LOGFX2,,

	CONPATCH(PA1PTR,,15)
LOGFX2	LB	RL2,F.LINE,RAFCB,	:GET THE LINE NUMBER TIMES 2
	SLLS	RL2,1
	JAL	R5,BLDSTR,,		:GO QUEUE DATA STRINGS
	J	CMLOG9-4,,

	FO	R500
	EI	(NMLV)

	ENDPATCH(Fix /$NETCMD LOGON to work for all lines)

	FO	FCB

:************************************************************************
:	This patch adds a line option to ignore a line bid until a
:	permanent virtual circuit is built (2780/3780 only).

	LO	BSCPTP
	LO	BSCDEF
	LO	LINE

	PATCH(851219,1731,NTD.B/KRUEGER,PA1PTR,,2*((NBILIN-1)/10+1))
	DEFBTA(BSCWVC,\BWVC|Q|,BWVC|Q|)

	CONPATCH(ETPVC-0A,,6)
	J	PA1PTR,,

	CONPATCH(PA1PTR,,1C)
	CL	R0,L.AS,RL2,RL2
	JE	ETPVC,,			:JUMP IF PVC IS BUILT
	TBT	RL,BSCWVC		:IGNORE LINE BID UNTIL PVC BUILT?
	JN	IDLE,,			:YES--GO TO IDLE STATE
	J	INTRDY,,		:NO

	ENDPATCH(Ignore line bid before PVC built)

	FO	BSCPTP
	FO	BSCDEF
	FO	LINE


	IF	NMLV

:**********************************************************************
:	This patch fixes the flow control problem encountered when a
:	console command is entered from the terminal while a print stream 
:	is being backpressured from the terminal.
:	If the dataflow is from the host to the terminal (e.g. print
:	stream), the host interface should be backpressuring individual
:	streams rather than using wait-a-bit.
:
:	Note:	This patch has been tested with Release 5.01 by USGS
:		(Esc #86179). De-escalated on 12/20/85.

	LO	FCB
	LO	X500
	LO	D500

	PATCH(860103,1101,NTD.B/KRUEGER,ATTSTR+12,,0C)

	JE	ATST1,,			:JUMP IF TERMINAL LINE
	J	ATST0,,			:JUMP IF HOST LINE

	CONPATCH(PA1PTR,,3C)

:	FOR HOST INTERFACE, THE OUTBOUND DIRECTION (DATA FLOW FROM
:	TERMINAL TO HOST) MUST USE WAIT-A-BIT FLOW CONTROL.
:	THE INBOUND DIRECTION (DATA FLOW FROM HOST TO TERMINAL) MUST
:	USE INDIVIDUAL STREAM SUSPENSION FLOW CONTROL.

ATST0	HS	0
	AHI	R3,10			:USE BIASED FCBTBL FOR WAIT-A-BIT
	STB	R3,F.STNO,RAFCB,	:STORE ADJ STREAM NO. INTO OUTBOUND FCB
	LHL	R5,F.AII,RAFCB,		:GET INBOUND FCB INDIRECTION PTR
	SHI	R3,10			:USE UNBIASED FCBTBL FOR STREAM SUSP.
	STB	R3,F.STNO,R5,		:STORE ADJ STREAM NO. INTO INBOUND FCB
	J	ATTST1+12,,

:	FOR TERMINAL INTERFACE, THE OUTBOUND DIRECTION (DATA FLOW FROM HOST
:	TO TERMINAL) MUST USE INDIVIDUAL STREAM SUSPENSION FLOW CONTROL.
:	THE INBOUND DIRECTION (DATA FLOW FROM TERMINAL TO HOST) MUST USE
:	WAIT-A-BIT FLOW CONTROL.

ATST1					:USE UNBIASED FCBTBL FOR STREAM SUSP.
	STB	R3,F.STNO,RAFCB,	:STORE ADJ STREAM NO. INTO OUTBOUND FCB
	LHL	R5,F.AII,RAFCB,		:GET INBOUND FCB INDIRECTION PTR
	AHI	R3,10			:USE BIASED FCBTBL FOR WAIT-A-BIT
	STB	R3,F.STNO,R5,		:STORE ADJ STREAM NO. INTO INBOUND FCB
	J	ATTST1+12,,

	CONPATCH(ATTF2+1C,,0C)

	JE	ATF3,,			:JUMP IF TERMINAL LINE
	J	ATF2.5,,		:JUMP IF HOST LINE

	CONPATCH(PA1PTR,,3C)

:	FOR HOST INTERFACE, THE INBOUND DIRECTION (DATA FLOW FROM HOST TO 
:	TERMINAL) MUST USE INDIVIDUAL STREAM SUSPENSION FLOW CONTROL.
:	THE OUTBOUND DIRECTION (DATA FLOW FROM TERMINAL TO HOST) MUST USE
:	WAIT-A-BIT FLOW CONTROL.

ATF2.5	HS	0			:USE UNBIASED FCBTBL FOR STREAM SUSP.
	STB	R4,F.STNO,R3,		:MARK INBOUND FCB HAS STREAM ATTACHED
	LHL	R3,F.AOI,R3,		:GET OUTBOUND INDIRECTION PTR
	AHI	R4,10			:USE BIASED FCBTBL FOR WAIT-A-BIT
	STB	R4,F.STNO,R3,		:MARK OUTBOUND FCB HAS STREAM ATTACHED
	J	4,R5,,			:SKIP RETURN

:	FOR TERMINAL INTERFACE, THE INBOUND DIRECTION (DATA FLOW FROM TERMINAL
:	TO HOST) MUST USE WAIT-A-BIT FLOW CONTROL.
:	THE OUTBOUND DIRECTION (DATA FLOW FROM HOST TO TERMINAL) MUST USE 
:	INDIVIDUAL STREAM SUSPENSION FLOW CONTROL.

ATF3
	AHI	R4,10			:USE BIASED FCBTBL FOR WAIT-A-BIT
	STB	R4,F.STNO,R3,		:MARK INBOUND FCB HAS STREAM ATTACHED
	LHL	R3,F.AOI,R3,		:GET OUTBOUND INDIRECTIONAL PTR
	SHI	R4,10			:USE UNBIASED FCBTBL FOR STREAM SUSP.
	STB	R4,F.STNO,R3,		:MARK OUTBOUND FCB HAS STREAM ATTACHED
	J	4,R5,,			:SKIP RETURN

	ENDPATCH(Fix MLV Flow Control Problem)

	FO	FCB
	FO	X500
	FO	D500

	EI	(NMLV)

	IF	NMLV
:*****************************************************************************
:
:	This patch fixes a problem that was reported by DOL (Esc #86169).
:	In IMS50 at receiving a hang character (IMSHNG) register 3 was 
:	destroyed by the SLOR routine. Changed R3 to R6.
:
	LO	IMS50
	LO	FCB
	LO	LINE

	PATCH(860103,1430,NTD.B/KRUEGER,IMSHNG+8,,6)
	LHL	R6,OFCB+2,R1,R1

	CONPATCH(IMSHG1,,6)
	LHL	R1,F.PORT,R6,

	CONPATCH(IMSHG2,,6)
	LHL	R6,F.LNK,R6,

	ENDPATCH(Fix hang char crash)

	FO	IMS50
	FO	FCB
	FO	LINE

	EI	(NMLV)


	IF	NMLV
:************************************************************************
:
:	This patch fixes a bug in HASP CRC generation for non-transparent
:	frames.  The bug caused the STX in the SOH/STX sequence to NOT
:	be included in the CRC.  The bug was probably introduced during
:	a merging of the source for MLVPTP.

	LO	BSCPTP
	LO	BSCCRC
	LO	BSCDEF

	PATCH(860204,1901,NTD.E/BUELL,CRCSTX+8,,14)
	CRCACC(RL2,R10,R12,R14)

	ENDPATCH(Fix HASP non-transparent frame CRC problem)

	FO	BSCPTP
	FO	BSCCRC
	FO	BSCDEF

	EI	(NMLV)



	IF	NMLV
:************************************************************************
:
:	This patch eliminates the HASP interface to crash on an unrecoverable
:	error, such as protocol violation by the transmitter.
:	HASP will now zap the circuit.

	LO	BSCPTP
	LO	BSCDEF

	PATCH(860221,1702,B/KRUEGER,SHINIT-10,,6)
MERROR	J	CRSFX1,,		:JUMP TO PATCH, ALSO DEFINE MERROR

	CONPATCH(PA1PTR,,18)
CRSFX1	L	R0,FASTC,,		:MAKE BACKGROUND ZAP THE CIRCUIT
	SI	R0,TMPER*RATE		:...WITHOUT TIME DELAY
	ST	R0,BSCETM,RL2,RL2
	J	MIDLE,,

	CONPATCH(SHINIT,MSDISC)

	SUBTTL	** MULTILEAVING **  STATE TABLES - HOST INTERFACE  **  MLVPTP

:       RESPONSE WHILE LINE IN INIT STATE : SHINIT
:       -------------------------------------------
:       THE HOST INTERFACE HAS SENT  SOH,ENQ TO THE HOST AND IS WAITING
:       A POSITIVE ACKNOWLEDGMENT ACK0 FROM HOST. IF HOST NOT RESPODING
:       OR HOST RESPODING BUT NOT APPROPRIATE (I.E. OTHER THAN
:       ACK0), AFTER 3 SEC., THE HOST INTERFACE WILL SEND SOH,ENQ
:       AGAIN.  IF NOT RECEIVE ACK0 WITHIN  XXX  SEC,  THEN  DISCONNECT.

SHINIT	HC	MERROR-PSEG		:GOOD DATA ETX BLOCK
	HC	HINIT2-PSEG		:GOOD DATA ETB BLOCK(F12/10/21/83/CHS)
	HC	MERROR-PSEG		:ENQ
	HC	MERROR-PSEG		:EOT
	HC	HINIT1-PSEG		:NAK
	HC	MIDISC-PSEG		:DLE,EOT (DISCONNECT SIGNAL)
	HC	MERROR-PSEG		:ACK1
	HC	MERROR-PSEG		:WACK
	HC	HBDACK-PSEG		:ACK0
	HC	MERROR-PSEG		:RVI
	HC	MERROR-PSEG		:TTD (STX,ENQ)
	HC	MERROR-PSEG		:MULTI-LEAVING BID (SOH,ENQ)
	HC	MERROR-PSEG		:DATA ABORT (ENQ,EOT,NAK IN TEXT)
	HC	RCVRTY-PSEG		:LOST SYNCHRONIZATION
	HC	HINIT1-PSEG		:OVERRUN (EXCEED RCV RESOURCE LIMIT)
	HC	HINIT1-PSEG		:NO INITIAL BUFFERLET AVAILABLE
	HC	HINIT1-PSEG		:TIMEOUT BEFORE MSG RECEIVED
	HC	HINIT1-PSEG		:CRC ERROR IN DATA MESSAGE
	HC	MERROR-PSEG		:LOST DATA (BUFFERLET UNAVAILABLE)
	HC	RCVRTY-PSEG		:UNIDENTIFIABLE MSG
	HC	MERROR-PSEG		:FORCED COMPLETION BY BACKGROUND

	IF	NMLI

:	WAITING FOR MLI HOST TO RESPOND WITH "PREADY" BLOCK : SHMLIR
:	------------------------------------------------------------

SHMLIR	HC	MERROR-PSEG		:GOOD DATA ETX BLOCK
	HC	HMLICO-PSEG		:GOOD DATA ETB BLOCK
	HC	MERROR-PSEG		:ENQ
	HC	MERROR-PSEG		:EOT
	HC	SNRNAK-PSEG		:NAK
	HC	MIDISC-PSEG		:(DLE,EOT) DISCONNECT
	HC	MERROR-PSEG		:ACK1 
	HC	MERROR-PSEG		:WACK
	HC	BIDAGN-PSEG		:ACK0 - RESPOND WITH ACK0
	HC	MERROR-PSEG		:RVI
	HC	MERROR-PSEG		:TTD (STX,ENQ)
	HC	MERROR-PSEG		:MULTILEAVING BID 
	HC	MERROR-PSEG		:DATA ABORT
	HC	RCVRTY-PSEG		:LOST SYNCHRONIZATION
	HC	HINIT1-PSEG		:OVERRUN
	HC	HINIT1-PSEG		:NO INITIAL BUFFERLET AVAILABLE
	HC	HINIT1-PSEG		:TIMEOUT BEFORE MESSAGE RECEIVED
	HC	HINIT1-PSEG		:CRC ERROR IN DATA MESSAGE
	HC	MERROR-PSEG		:LOST DATA (BUFFERLET UNAVAILABLE)
	HC	RCVRTY-PSEG		:UNIDENTIFIABLE MSG
	HC	MERROR-PSEG		:FORCED COMPLETION BY BACKGROUND		
	
	EI	(NMLI)

	SUBTTL	** MULTILEAVING **  STATE TABLES - TERMINAL INTERFACE  **  MLVPTP

:       WAITING FOR TERMINAL TO SEND SOH,ENQ OR DLE,ENQ (MLI): STINIT
:       ---------------------------------------------------
:       THE TERMINAL INTERFACE IS WAITING THE TERMIAL TO BID.

STINIT	HC	MERROR-PSEG		:GOOD DATA ETX BLOCK
	HC	TINIT1-PSEG		:GOOD DATA ETB BLOCK(F12/10/21/83/CHS)
	HC	RBSCBD-PSEG		:ENQ (BSC 2780/3780 LINE BID)
	HC	MERROR-PSEG		:EOT
	HC	TINIT1-PSEG		:NAK
	HC	MIDISC-PSEG		:DLE,EOT (DISCONNECT SIGNAL)
	HC	MERROR-PSEG		:ACK1
	HC	MERROR-PSEG		:WACK
	HC	TINIT1-PSEG		:ACK0
	HC	MERROR-PSEG		:RVI
	HC	MERROR-PSEG		:TTD (STX,ENQ)
	HC	TRMLBD-PSEG		:MULTI-LEAVING BID (SOH,ENQ)
	HC	MERROR-PSEG		:DATA ABORT (ENQ,EOT,NAK IN TEXT)
	HC	RCVRTY-PSEG		:LOST SYNCHRONIZATION
	HC	TINIT1-PSEG		:OVERRUN (EXCEED RCV RESOURCE LIMIT)
	HC	TINIT1-PSEG		:NO INITIAL BUFFERLET AVAILABLE
	HC	TINIT1-PSEG		:TIMEOUT BEFORE MSG RECEIVED
	HC	MERROR-PSEG		:CRC ERROR IN DATA MESSAGE
	HC	MERROR-PSEG		:LOST DATA (BUFFERLET UNAVAILABLE)
	HC	RCVRTY-PSEG		:UNIDENTIFIABLE MSG
	HC	MERROR-PSEG		:FORCED COMPLETION BY BACKGROUND


:	WAITING FOR THE SYSTEM SIGN ON CARD : STWSG
:	-----------------------------------------------
:	THE TERMINAL INTERFACE ACKNOWLEGE THE LINE BID WITH A
:	AFFIRMATIVE ACK0, AND WAITING FOR THE RECEIVING OF A SYSTEM
:	SIGN ON CARD IMAGE.
:
:	FOR MLI (APRIL-84) WAITING FOR "PSIGNON" RECORD
:	-----------------------------------------------
STWSG	HC	MERROR-PSEG		:GOOD DATA ETX BLOCK
	HC	TRSGON-PSEG		:GOOD DATA ETB BLOCK
	HC	MERROR-PSEG		:ENQ
	HC	MERROR-PSEG		:EOT
	HC	MERROR-PSEG		:NAK
	HC	MIDISC-PSEG		:DLE,EOT (DISCONNECT SIGNAL)
	HC	MERROR-PSEG		:ACK1
	HC	MERROR-PSEG		:WACK
	HC	TRACK0-PSEG		:ACK0
	HC	MERROR-PSEG		:RVI
	HC	MERROR-PSEG		:TTD (STX,ENQ)
	HC	TRMLBD-PSEG		:MULTI-LEAVING BID (SOH,ENQ)
	HC	MERROR-PSEG		:DATA ABORT (ENQ,EOT,NAK IN TEXT)
	HC	RCVRTY-PSEG		:LOST SYNCHRONIZATION
	HC	MERROR-PSEG		:OVERRUN (EXCEED RCV RESOURCE LIMIT)
	HC	MERROR-PSEG		:NO INITIAL BUFFERLET AVAILABLE
	HC	TRTMOT-PSEG		:TIMEOUT BEFORE MSG RECEIVED
	HC	TRCRCR-PSEG		:CRC ERROR IN DATA MESSAGE
	HCTY-PSEG		:LOST DATA (BUFFERLET UNAVAILABLE)
	HC	RCVRTY-PSEG		:UNIDENTIFIABLE MSG
	HC	MERROR-PSEG		:FORCED COMPLETION BY BACKGROUND




:	WAITING FOR THE REQUEST TO INITIATE BLOCK : STRTIN
:	------------------------------------------------------
:       THE TERMINAL INTERFACE IS WAITING TO RECEIVE THE FIRST RECORD -
:	A 'REQUEST TO INITIATE' RECORD, TO GRANT PERMISION TO SEND
:	NETCMD RECORD.
:
:	FOR MLI (APRIL-84) TERMINAL INTRF. WAITING FOR "PCOMPLETE" RECORD.
:	------------------------------------------------------------------

STRTIN	HC	MERROR-PSEG		:GOOD DATA ETX BLOCK
	HC	TIRTIN-PSEG		:GOOD DATA ETB BLOCK
	HC	MERROR-PSEG		:ENQ
	HC	MERROR-PSEG		:EOT
	HC	TIRNAK-PSEG		:NAK
	HC	MIDISC-PSEG		:DLE,EOT (DISCONNECT SIGNAL)
	HC	MERROR-PSEG		:ACK1
	HC	MERROR-PSEG		:WACK
	HC	TIRACK-PSEG		:ACK0
	HC	MERROR-PSEG		:RVI
	HC	MERROR-PSEG		:TTD (STX,ENQ)
	HC	TIRST-PSEG		:MULTI-LEAVING BID (SOH,ENQ)
	HC	MERROR-PSEG		:DATA ABORT (ENQ,EOT,NAK IN TEXT)
	HC	RCVRTY-PSEG		:LOST SYNCHRONIZATION
	HC	TIOVRN-PSEG		:OVERRUN (EXCEED RCV RESOURCE LIMIT)
	HC	TINBUF-PSEG		:NO INITIAL BUFFERLET AVAILABLE
	HC	TITMOT-PSEG		:TIMEOUT BEFORE MSG RECEIVED
	HC	TICRCR-PSEG		:CRC ERROR IN DATA MESSAGE
	HC	TILTDT-PSEG		:LOST DATA (BUFFERLET UNAVAILABLE)
	HC	RCVRTY-PSEG		:UNIDENTIFIABLE MSG
	HC	MERROR-PSEG		:FORCED COMPLETION BY BACKGROUND

:	WAITING FOR THE NETCMD RECORD : STNCMD
:	----------------------------------------
:	TERMINAL INTERFACE HAS GRANTED PERMISTION TO THE WORK STATION,
:	AND IS WAITING FOR THE NETCMD CARD.

STNCMD	HC	MERROR-PSEG		:GOOD DATA ETX BLOCK
	HC	TNCMD-PSEG		:GOOD DATA ETB BLOCK
	HC	MERROR-PSEG		:ENQ
	HC	MERROR-PSEG		:EOT
	HC	TNRNAK-PSEG		:NAK
	HC	MIDISC-PSEG		:DLE,EOT (DISCONNECT SIGNAL)
	HC	MERROR-PSEG		:ACK1
	HC	MERROR-PSEG		:WACK
	HC	TNRACK-PSEG		:ACK0
	HC	MERROR-PSEG		:RVI
	HC	MERROR-PSEG		:TTD (STX,ENQ)
	HC	TNRST-PSEG		:MULTI-LEAVING BID (SOH,ENQ)
	HC	MERROR-PSEG		:DATA ABORT (ENQ,EOT,NAK IN TEXT)
	HC	RCVRTY-PSEG		:LOST SYNCHRONIZATION
	HC	RCVRTY-PSEG		:OVERRUN (EXCEED RCV RESOURCE LIMIT)
	HC	RCVRTY-PSEG		:NO INITIAL BUFFERLET AVAILABLE
	HC	TNTMOT-PSEG		:TIMEOUT BEFORE MSG RECEIVED
	HC	TNCRCR-PSEG		:CRC ERROR IN DATA MESSAGE
	HC	RCVRTY-PSEG		:LOST DATA (BUFFERLET UNAVAILABLE)
	HC	RCVRTY-PSEG		:UNIDENTIFIABLE MSG
	HC	MERROR-PSEG		:FORCED COMPLETION BY BACKGROUND


:	WAITING FOR A EOF RECORD TO COMPLETE 'SIGNON' : STNEOF
:	--------------------------------------------------------
:	TERMINAL INTERFACE HAS RECEIVED A PRESUMMED NETCMD RECORD, AND
:	IS WAITING FOR THE MANDATORY EOF RECORD .

STNEOF	HC	MERROR-PSEG		:GOOD DATA ETX BLOCK
	HC	TEEOF-PSEG		:GOOD DATA ETB BLOCK
	HC	MERROR-PSEG		:ENQ
	HC	MERROR-PSEG		:EOT
	HC	TERNAK-PSEG		:NAK
	HC	MIDISC-PSEG		:DLE,EOT (DISCONNECT SIGNAL)
	HC	MERROR-PSEG		:ACK1
	HC	MERROR-PSEG		:WACK
	HC	TERACK-PSEG		:ACK0
	HC	MERROR-PSEG		:RVI
	HC	MERROR-PSEG		:TTD (STX,ENQ)
	HC	TERST-PSEG		:MULTI-LEAVING BID (SOH,ENQ)
	HC	MERROR-PSEG		:DATA ABORT (ENQ,EOT,NAK IN TEXT)
	HC	RCVRTY-PSEG		:LOST SYNCHRONIZATION
	HC	RCVRTY-PSEG		:OVERRUN (EXCEED RCV RESOURCE LIMIT)
	HC	RCVRTY-PSEG		:NO INITIAL BUFFERLET AVAILABLE
	HC	TETMOT-PSEG		:TIMEOUT BEFORE MSG RECEIVED
	HC	TECRCR-PSEG		:CRC ERROR IN DATA MESSAGE
	HC	TELTDT-PSEG		:LOST DATA (BUFFERLET UNAVAILABLE)
	HC	RCVRTY-PSEG		:UNIDENTIFIABLE MSG
	HC	MERROR-PSEG		:FORCED COMPLETION BY BACKGROUND


	SUBTTL	 ** MULTILEAVING **   STATE TABLES  - TERMINAL/HOST  **  MLVPTP

:	RESPONSE WHILE LINE IN NORMAL (ACTIVE) STATE : SNORM
:	----------------------------------------------------
:	NORMAL RECEIVING AND TRANSMITTING .....

SNORM	HC	MERROR-PSEG		:GOOD DATA ETX BLOCK
	HC	RDATB-PSEG		:GOOD DATA ETB BLOCK
	HC	MERROR-PSEG		:ENQ
	HC	MERROR-PSEG		:EOT
	HC	SNRNAK-PSEG		:NAK
	HC	MIDISC-PSEG		:DLE,EOT (DISCONNECT SIGNAL)
	HC	MERROR-PSEG		:ACK1
	HC	MERROR-PSEG		:WACK
	HC	SNRACK-PSEG		:ACK0
	HC	MERROR-PSEG		:RVI
	HC	MERROR-PSEG		:TTD (STX,ENQ)
	HC	SNRST-PSEG		:MULTI-LEAVING BID (SOH,ENQ)
	HC	MERROR-PSEG		:DATA ABORT (ENQ,EOT,NAK IN TEXT)
	HC	RCVRTY-PSEG		:LOST SYNCHRONIZATION
	HC	SNOVRN-PSEG		:OVERRUN (EXCEED RCV RESOURCE LIMIT)
	HC	RCVRTY-PSEG		:NO INITIAL BUFFERLET AVAILABLE
	HC	SNTMOT-PSEG		:TIMEOUT BEFORE MSG RECEIVED
	HC	SNCRCR-PSEG		:CRC ERROR IN DATA MESSAGE
	HC	SNLTDT-PSEG		:LOST DATA (BUFFERLET UNAVAILABLE)
	HC	RCVRTY-PSEG		:UNIDENTIFIABLE MSG
	HC	MERROR-PSEG		:FORCED COMPLETION BY BACKGROUND
	ENDPATCH(Eliminate HASP crash on protocol violation)

	FO	BSCPTP
	FO	BSCDEF
	
	EI	(NMLV)



:**********************************************************************
:
:	This patch removes some code from R100 in the SIGNOFF processing
:	routine which was supposed to allow a TYMNET session to be
:	split in the middle of a frame.  However, this code did not
:	function due to numerous bugs and is therefore being deleted.

:****************************************
:>>>>>>	This code belongs in R100

	LO	R100
	LO	FCB
	LO	IDS
	LO	LINE

	PATCH(860412,1708,NTD.E/BUELL,SOF3+8,,6)
	J	SOFFX1,,

	CONPATCH(PA1PTR,,28)
SOFFX1	ST	RLINK,F.RET1,RAFCB,	:SAVE RETURN ADDRESS
	LHI	RDATA,(EOTMSG-FSTCTL)*2	:FAKE THE EOT
	L	R3,F.BASI,RAFCB,
	LHL	R4,P.CTL,R3,RDATA
	JEFS	SOF3.5
	AR	R4,R3
	JALR	RLINK,R4
SOF3.5	L	RLINK,F.RET1,RAFCB,	:RESTORE RETURN ADDRESS
	J	CREBBA+12,,		:DELETE A BUNCH OF CODE

	CONPATCH(SOF4+38,,6)
	JAL	R5,IZPROC,,		:INITIALIZE THE LINE

	FO	R100
	FO	FCB
	FO	IDS
	FO	LINE

	ENDPATCH(Cleanup /$NETCMD SIGNOF processing)



:**********************************************************************
:
:	This patch fixes a small problem in the pseudo-interrupt logic
:	in BSCPTP.

	LO	BSCPTP
	LO	BSCDEF

	PATCH(860414,0917,NTD.E/BUELL,SXRENQ+4,,2)
	HC	IPSRV1-PSEG		:ENQ

	ENDPATCH(Fix fix pseudo-RVI problem)

	FO	BSCDEF
	FO	BSCPTP
:
:****************************************************************
:	This patch requires that the variable "ACCOUN" be defined
:	in the TYMfile by the use of the OPTION statement:
:
:	OPTION(ACCOUNT,12345)	:5 digit accounting host number
:
:
	IF	1-(ATHOST%AHOST)	:IF AHOST = ATHOST
	IF	\ACCOUN
	LO	R100
	IF	NMLV
	FO	R500
	EI	:(NMLV)
	PATCH(860514,1216,NTD.P/SPIES,LSG+$08A,,4)
	FO	R100
	LHI	R0,ACCOUN
	IF	NMLV
	LO	IMS50
	CONPATCH(CREIMS+$0160,,4)
	FO	IMS50
	LHI	R0,ACCOUN
	EI	:(NMLV)
	ENDPATCH(FORCE ACCOUNTING HOST NUMBER = "ACCOUNT")
	ELSE
	REMARK %
	REMARK %*** ACCOUNTING HOST NUMBER NOT PROPERLY DEFINED !!!
	REMARK %
	REMARK %*** ABORTING ASSEMBLY...
	REMARK %
	QUIT 	1	:EXIT TO SYSTEM
	EI	:(\ACCOUNT)
	EI	:1-(ATHOST%AHOST)

:**********************************************************************
:
:	>>>>>>>>>>>>>>>>  THIS STATEMENT MUST THE THE LAST
:	>>>>>>>>>>>>>>>>  IN THE PATCH FILE

	PATCHREPORT
    [@Ç