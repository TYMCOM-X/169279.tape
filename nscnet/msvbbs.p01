:====================================================================
:
: This patch is special only for demonstration of Japanese BBS.
: 
: PREMISE: HIF is tymcom connected CONSAT outdial host.
:
: FUNCTION:
:
:  (1) This patch allows users go back to menu when outdial fails.
:      For example Number busy, Call failed, Malfunction are detected
:      instead of 'Connect'.
:  (2) In case menu server gets correct answer (Connect) from consat,
:      menu server moves into transparent mode.
:
: EXAMPLE:
:
:       C.SeeincludingThen(Dialing)
:       C.SeeincludingThen("2e)
:       C.TosincludingThen("0D"0AConnect)
:
:====================================================================

        PATCH(880803,1530,NIS.YAMAUCHI,PA0PTR,,NPORT*7+38)

P.TEXT  WS      NPORT
P.FLG   BS      NPORT
P.CNT   BS      NPORT
P.MTAB  WC      P.MSG
P.MSG   SC      /"8d"8a"8a[ms] BBS host has not accepted your call"8d"8a/
P.R1    BS      1
BKPOFFBUF       BC      1,0A1           :BACK PRESSURE OFF
XPROFFBUF       BC      1,0A9           :XPR OFF

        CONPATCH(IDLE+26,,6)
        J       PA1PTR,,
        CONPATCH(PA1PTR,,12)
        STB     RINCOMINGR1,P.R1,,      :SAVE REGISTER R1
        lhl     rUCB,(PortToUCBmap),rIncomingR1,rIncomingR1     :
        J       IDLE+2C,,               :RETURN

        CONPATCH(SENDDATA,,6)
        J       PA1PTR,,
        CONPATCH(PA1PTR,,26)
SEND01  LIS     R0,0                    :
        LB      R1,P.R1,,               :GET PORT NO.
        STB     R0,P.FLG,R1,            :CLEAR FLG
        STB     R0,P.CNT,R1,            :CLEAR COUNTER
        SLLS    R1,2                    :R1 X 4
        ST      R0,P.TEXT,R1,           :CLEAR STRING
        lis     rMsgTypeR2,(1)          :R2=type irrelevant, using ELODR,
        lhi     r0,$09D+3               :ask for max room in ORING,
        J       SENDDATA+6              :RETURN

        CONPATCH(MAT3M,,6)
        J       PA1PTR,,
        CONPATCH(PA1PTR,,50)
        LB      R4,P.R1,,               :RESTORE R1
        CLHI    R0,2E                   :'2E' ?
        JNFS    CHECK1                  :NO
        STB     R0,P.FLG,R4,            :YES, SO FLAG SET
        J       MAT3N                   :GET NEXT CHARACTER
CHECK1  LB      R11,P.FLG,R4,           :GET FLAG
        CLHI    R11,2E                  :IS FLAG '2E' ?
        JN      MAT3N                   :NO
        LB      R11,P.CNT,R4,           :GET COUNTER
        CLHI    R11,04                  :COUNTER = 4 ?
        JGFS    MAT3N                   :NO , COUNTER > 4
        SLLS    R4,2                    :R4 X 4
        STB     R0,P.TEXT,R4,R11        :STORE CHARACTER
        SRLS    R4,2                    :R4/4
        AIS     R11,1                   :COUNTER +1
        STB     R11,P.CNT,R4,           :SAVE COUNTER
MAT3N   sis     rMsgTypeR2,(1)          :
        jn      mat2,,                  :if some still left in message
        J       MAT3M+6,,               :RETURN

        CONPATCH(MAT5-0A,,6)
        J       PA1PTR,,
        CONPATCH(PA1PTR,,74)
        JGFS    MAT5A
        JAL     R4,ELOR,,               :1 or 2
        JFS     MAT5B
MAT5A   LR      R15,R7                  :3 or 4 sort of pop 15
MAT5B   LB      R1,P.R1,,               :GET PORT NO.
        LB      R12,P.CNT,R1,           :COUNTER GET
        CLHI    R12,04                  :COUNTER = 4 ?
        JL      IDLE,,                  :NO , COUNTER IS < 4
        SLLS    R1,2                    :R1 X 4
        L       R12,P.TEXT,R1,
        CI      R12,$00D0A4D61          :'Malfunction' ?
        JEFS    MAT5Z
        CI      R12,$00D0A4361          :'Call failed' ?
        JEFS    MAT5Z
        CI      R12,$00D0A4E75          :'Number busy" ?
        JEFS    MAT5Z
        J       IDLE,,
MAT5Z   LR      R2,RTSATSIDER10         :GET PORT NO.
        LA      R3,BKPOFFBUF,,          :BACK PRESSURE OFF
        JAL     R5,OCM,,                :SEND IT TO TERMINAL
        LR      R2,RTSATSIDER10         :GET PORT NO.
        LA      R3,XPROFFBUF,,          :XPR OFF
        JAL     R5,OCM,,                :SEND IT TO TERMINAL
        L       R3,P.MTAB,,             :GET MESSAGE ADDRESS
        JAL     R5,SHIPTOTSAT,          :DISPLAY TEXT TO USER
        J       ZAPHOST,,               :GO TO ZAP

        ENDPATCH(Support Consat outdial BBS)
