:====================================================================
:File name = (NSCNET)MSVOUT.113
: <<<<< This patch apply to Menu sever version 1.13 >>>>>
:
: This patch is special only for demonstration of Japanese BBS.
: 
: PREMISE: HIF is tymcom connected CONSAT outdial host.
:
: FUNCTION: Consat outdial
:
:  (1) This patch allows users go back to menu when outdial fails.
:      For example Number busy, Call failed, Malfunction are detected
:      instead of 'Connect'.
:  (2) In case menu server gets correct answer (Connect) from consat,
:      menu server moves into transparent mode.
:
:  (3) In case of user make mistake to type password when he login , menu
:       server send password error message and zap the circuit.
:
: EXAMPLE: Consat outdial
:
:       C.SeeincludingThen(Dialing)
:       C.SeeincludingThen("2e)
:       C.TosincludingThen("0D"0AConnect)
:
:
: FUNCTION: Tymix modem
:
:  (1) This patch allows retries of dial command after session has
:      not been established because of host status (busy or no carrier).
:  (2) In case menu server gets correct answer (CONNECT 1200) from
:      modem, menu server moves into transparent mode.
:
: EXAMPLE: Tymix mode
:
:       C.String(ATFS2=19)
:       C.Tossincludingthen(OK)
:       C.Stirng(ATDT5555132)
:       C.Tossincludingthen(CONNECT 1200)
:
:
:--- Date------Time---:-----Design history-----------------------:-Designer--
: 03 AUG 1988 PM03:30 : INITIAL PATCH DESIGNED.                     BY YAMAUCHI
: 01 NOV 1989 AM09:45 : ADD SEND LTM(A9)TOWARD TERMINAL SIDE WHEN CIRCUIT
:                     : WAS ZAPPED BEFORE HOST CONNECTION COMPLETE. BY KANETA
:====================================================================

        PATCH(890606,1900,NIS.KANETA,PA0PTR,,NPORT*0B+80)

P.INDEX BS      NPORT
        WS      0
P.TEX1  WS      NPORT
P.TEX2  WS      NPORT
P.FLG   BS      NPORT
P.CNT1  BS      NPORT
P.MTAB  WC      P.MSG
P.MSG   SC      /"8d"8a"8a[ms] BBS host has not accepted your call"8d"8a/
P.LOGB  WC      P.LOG
P.LOG   SC      /"8D"8A[ms] Your login password is wrong."8D"8A     Please login again."8d"8a/
P.R1    BS      1
BKPOFFBUF       BC      1,0A1           :BACK PRESSURE OFF
XPROFFBUF       BC      1,0A9           :XPR OFF

        CONPATCH(IDLE+26,,6)
        J       PA1PTR,,
        CONPATCH(PA1PTR,,12)
        STB     RINCOMINGR1,P.R1,,      :SAVE REGISTER R1
        lhl     rUCB,(PortToUCBmap),rIncomingR1,rIncomingR1     :
        J       IDLE+2C,,               :RETURN

        CONPATCH(SENDDATA,,6)
        J       PA1PTR,,
        CONPATCH(PA1PTR,,3C)
SEND01  LIS     R0,0                    :
        LB      R1,P.R1,,               :GET PORT NO.
        STB     R0,P.INDEX,R1,          :CLEAR INDEX
        STB     R0,P.FLG,R1,            :CLEAR FLG
        STB     R0,P.CNT1,R1,           :CLEAR COUNTER 1
        SLLS    R1,2                    :R1 X 4
        ST      R0,P.TEX1,R1,           :CLEAR STRING
        ST      R0,P.TEX2,R1,           :CLEAR STRING
        lis     rMsgTypeR2,(1)          :R2=type irrelevant, using ELODR,
        lhi     r0,$09D+3               :ask for max room in ORING,
        J       SENDDATA+6              :RETURN

        CONPATCH(MAT3M,,6)
        J       PA1PTR,,
        CONPATCH(PA1PTR,,94)
        LB      R11,(LOGINEMPTYINDEX),RUCB
        LB      R4,P.R1,,               :RESTORE R1
        LB      R11,P.INDEX,R4,         :GET INDEX
        AIS     R11,1                   :ADD 1 TO INDEX
        STB     R11,P.INDEX,R4,         :STORE INDEX
        SIS     R11,3                   :INDEX - 3
        JL      CHECK
        CHI     R11,3
        JGFS    CHECK                   :IF INDEX > 3, SKIP
        SLLS    R4,2                    :R4 X 4
        STB     R0,P.TEX2,R11,R4        :STORE CHARACTER
        SRLS    R4,2                    :R4/4
CHECK   CLHI    R0,2E                   :'2E' ?
        JNFS    CHECK1                  :NO
        STB     R0,P.FLG,R4,            :YES, SO FLAG SET
        J       MAT3N                   :GET NEXT CHARACTER
CHECK1  LB      R11,P.FLG,R4,           :GET FLAG
        CLHI    R11,2E                  :IS FLAG '2E' ?
        JN      MAT3N                   :NO
        LB      R11,P.CNT1,R4,          :GET COUNTER
        CLHI    R11,04                  :COUNTER = 4 ?
        JEFS    MAT3N                   :NO , COUNTER > 4
        SLLS    R4,2                    :R4 X 4
        STB     R0,P.TEX1,R11,R4        :STORE CHARACTER
        SRLS    R4,2                    :R4/4
        AIS     R11,1                   :COUNTER +1
        STB     R11,P.CNT1,R4,          :SAVE COUNTER
MAT3N   sis     rMsgTypeR2,(1)          :
        jn      mat2,,                  :if some still left in message
        J       MAT3M+6,,               :RETURN

        CONPATCH(MAT5-0A,,6)
        J       PA1PTR,,
        CONPATCH(PA1PTR,,168)
        JGFS    MAT5A
        JAL     R4,ELOR,,               :1 or 2
        JFS     MAT5B
MAT5A   LR      R15,R7                  :3 or 4 sort of pop 15
MAT5B   LIS     R1,0
        LB      R4,(UBUFER),R11,RUCB    :CHECK DEFINED TEXT
        OR      R1,R4
        LB      R4,(UBUFER+1),R11,RUCB
        SLLS    R1,8
        OR      R1,R4
        LB      R4,(UBUFER+2),R11,RUCB
        SLLS    R1,8
        OR      R1,R4
        LB      R4,(UBUFER+3),R11,RUCB
        SLLS    R1,8
        OR      R1,R4
        CI      R1,$00D0AC3CF           :'CONNECT 1200' ?
        JE      ACOM                    :YES , THEN GOTO ACOM
CONSAT  CI      R1,$00D0AC3EF           :'Connect ' ?
        JN      MAT5E                   :NO , THEN GOTO MAT5E
        LB      R1,P.R1,,               :GET PORT NO.
        LB      R12,P.CNT1,R1,          :COUNTER GET
        CLHI    R12,04                  :COUNTER = 4 ?
        JL      IDLE,,                  :NO , COUNTER IS < 4
        SLLS    R1,2                    :R1 X 4
        L       R12,P.TEX1,R1,
        CI      R12,$00D0A4D61          :'Malfunction' ?
        JE      MAT5Z
        CI      R12,$00D0A4361          :'Call failed' ?
        JE      MAT5Z
        CI      R12,$00D0A4E75          :'Number busy" ?
        JE      MAT5Z
        J       IDLE,,
ACOM    LB      R1,P.R1,,               :RESTORE R1
        LB      R11,P.INDEX,R1,         :GET INDEX
        CHI     R11,8                   :
        JN      MAT5C                   :IF NOT EQ 8, SKIP
        SLLS    R1,2                    :R1 X 4
        L       R12,P.TEX2,R1,          :GET STRING
        CI      R12,$042555359          :BUSY ?
        JE      MAT5Z                   :RETRY
        J       IDLE,,                  :GO TO WAIT
MAT5C   CHI     R11,$A14                :
        JN      MAT5D                   :IF NOT EQ 14, SKIP
        SLLS    R1,2                    :R1 X 4
        L       R12,P.TEX2,R1,          :GET STRING
        CI      R12,$04E4F2043          :NO CARRIER ?
        JE      MAT5Z                   :RETRY
        J       IDLE,,                  :GO TO WAIT
MAT5D   CHI     R11,$A15                :
        JN      IDLE,,                  :IF NOT EQ 15, GO TO WAIT
        SLLS    R1,2                    :R1 X 4
        L       R12,P.TEX2,R1,          :GET STRING
        CI      R12,$04E4F2044          :NO DIALTONE ?
        JN      IDLE,,                  :GO TO WAIT
        J       MAT5Z
MAT5E   LB      R1,P.R1,,
        LB      R11,P.INDEX,R1,
        CHI     R11,$A20
        JN      IDLE,,
        LIS     R11,0
        STB     R11,P.INDEX,R1,
        SLLS    R1,2
        L       R12,P.TEX2,R1,
        CI      R12,$004E4F2050         :NO PORT ?
        JN      IDLE,,
MAT5Z   LR      R2,RTSATSIDER10         :GET PORT NO.
        LA      R3,BKPOFFBUF,,          :BACK PRESSURE OFF
        JAL     R5,OCM,,                :SEND IT TO TERMINAL
        LR      R2,RTSATSIDER10         :GET PORT NO.
        LA      R3,XPROFFBUF,,          :XPR OFF
        JAL     R5,OCM,,                :SEND IT TO TERMINAL
        L       R3,P.MTAB,,             :GET MESSAGE ADDRESS
        JAL     R5,SHIPTOTSAT,          :DISPLAY TEXT TO USER
        J       ZAPHOST,,               :GO TO ZAP

CONPATCH(ZAPHOST,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,3E)
        LHL     R2,(HOSTSIDE),RUCB,
        LIS     R3,0
        STB     R3,P.FLG,R2,              :CLEAR FLG
        STB     R3,P.CNT1,R2,             :CLEAR COUNTER 1
        SLLS    R2,2                      :R2 X 4
        ST      R3,P.TEX1,R2,             :CLEAR STRING
        ST      R3,P.TEX2,R2,             :CLEAR STRING
        SRLS    R2,2                      :R2 / 4
        LA      R3,(ZAPBUF),,             :ZAPBUF ADREES SET
        J       ZAPHOST+8,,

        IF      (\pwUsed)

CONPATCH(NLSIN1+6,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,86)
        CLHI    R12,(3^2)               :'Error type password' ?
        JE      PWERR                   :YES , THEN GOTO PWERR
        JAL     R5,SHIPTOTSAT,,         :DISPLAY TEXT TO USER
        CLHI    R12,(5^2)               :IF NLS CODE BYTE > 5
        J       NLSIN1+0E,,             :RETURN TO MAIN ROUTINE
PWERR   LR      R2,RTSATSIDER10         :GET PORT NUMBER
        LA      R3,BKPOFFBUF,,          :BACK PRESSURE OFF
        JAL     R5,OCM,,                :SEND IT TO TERMINAL
        LR      R2,RTSATSIDER10         :GET PORT NO.
        LA      R3,XPROFFBUF,,          :XPR OFF
        JAL     R5,OCM,,                :SEND IT TO TERMINAL
        LR      R2,RTSATSIDER10
        L       R3,P.LOGB,,             :GET MESSAGE ADDRESS
        JAL     R5,SHIPTOTSAT,          :DISPLAY TEXT TO USER
:       J       KODEQUIT,,              :GO TO ZAP
        JAL     R5,RESETPW,,            :RESET PASSWORD TEXT
        LR      R2,RTSATSIDER10
        LA      R3,(DETACHBUF),,        :DETACH MESSAGE
        JAL     R5,OCM,,
        JAL     R8,FREEUPUCB,,          :STOP INACTIVTY TIMER & SEND ZAPPER
        J       IDLE,,

        EI

CONPATCH(FOGH2+10,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,1E)
FOGH3   LR      R2,RTSATSIDER10         :GET PORT NUMBER
        LA      R3,XPROFFBUF,,          :XPR OFF
        JAL     R5,OCM,,                :SEND IT TO TERMINAL
        JAL     R6,PRZAP,,              :FROM SOURCE
        LHI     R6,CSMENU               :  "    "
        J       FOGH2+18,,              :RETURN TO MAIN ROUTINE

        ENDPATCH(Support Consat outdial & Tymix modem BBS)
    