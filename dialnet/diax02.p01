: THIS IS A COMBINATION OF (SOURCE)XCOM02.P01 AND (INTLTECH)INTL02.P01
: AND (PDIXON)DIXON02.P01.  WHICH PATCHES TO INCLUDE WAS DECIDED BY
: THE PATCH HISTORY IN NAD.  THIS COMPLETE PATCH FILE REPRESENTS ONLY
: THOSE PATCHES IN HOST 61 AND 803 AT THE TIME THE PRESENT CODE WAS
: GENNED.  PRESENT CODE IS (TYMNET)ND2011.N05 / CKSUM = SAXMOY  AND
: (TYMNET)ND2264.N05 / CKSUM = BECQOR
:
: MICHELLE KLEIN - JUNE 27, 1984
::					Last updated by LLIN, 4/4/84


:	******************************************************
:	*****        PATCH FILE FOR VERSION 02.01        *****
:	******************************************************

:	Refer to (NETMID:39)PATCH.DOC for details on implemen-
:	ting patches with PATCH.LIB.


:	>>>>>     START PATCHES HERE     <<<<<

  IF	.EQ.(PATCH1,2)
	SEG	0E
S2SIZE	EQ	P1SIZE+S2SIZE
   IF	.LT.(CODE.L,10000)
	ORG	AREAS+4
	BC	0,0,0,0
	ORG	AREAS+4
	WC	0FFFF		:FILL OUT SEGMENT 1
   EI	.LT.(CODE.L,10000)
	ORG	AREAS+8
	BC	0,0,0,0
	ORG	AREAS+8
	WC	S2SIZE
	SEG	2
PATCH1	BS	P1SIZE
PA1PTR	EQ	PATCH1
   IF	.GE.(CODE.L,10000)
CODE.L	EQ	CODE.L+P1SIZE
   ELSE
CODE.L	EQ	10000+P1SIZE	:FILL OUT SEGMENT 1
S1SIZE	EQ	0FFFF
   EI	.GE.(CODE.L,10000)
  EI	.EQ.(PATCH1,2)

PATCH(830106,0958,GML,TER040-20,,4)
	NHI	R0,1F		:GET LOWER 5 BITS OF ORGTID
ENDPATCH(Only look at lower 5 bits of ORGTID for echo tests)

PATCH(830106,1002,GML,L1PBA+0A,,6)
	LR	R0,R1		:TRANSFER OUTPUT BAUD RATE TO R0 FOR LATER USE
	J	L1PBC
ENDPATCH(Do not translate baud rate to CCITT until later)

PATCH(830110,1400,RDC,STP30A+0A,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,2E)
STP30C	EQ	STP30A+84
	TBT	R1,CHLOGN,,	:TEST IF CHANNEL IN LOGIN MODE
	JN	STP30C,,	:IF SO, DON'T REPLY TO ECHO COMMANDS
	LHL	R4,IPORT,,
	LB	R4,PCKSTE,R4,	:GET CURRENT PACKET LEVEL STATE
	CLHI	R4,PFLOWC	:TEST IF DATA TRANSFER STATE
	JL	STP30C,,	:IF NOT, NO REPLIES TO NET
	LB	R0,DPRM,,	:ELSE CONTINUE WITH STP REPLY
	J	STP30A+10,,
ENDPATCH(Do not reply to QTP/STP if in login mode)

PATCH(830110,1400,RDC,STP30B,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,2E)
	TBT	R1,CHLOGN,,	:TEST IF CHANNEL IN LOGIN MODE
	JN	TP.EX,,		:IF SO, DON'T REPLY INTO NET
	LHL	R4,IPORT,,
	LB	R4,PCKSTE,R4,	:TEST CURRENT PACKET STATE
	CLHI	R4,PFLOWC	:DATA TRANSFER STATE ??
	JL	TP.EX,,		:IF NOT
	LB	R0,DPRM,,	:ELSE CONTINUE WITH STP REPLY
	J	STP30B+6,,
ENDPATCH(Do not reply to QTP/STP if in login mode)

PATCH(830110,1638,GML,RTCALR+1E,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,1E)
	TBT	RL,LEVEL1,,
	JN	SNDCL0,,	:CLEAR CALL REQUESTS WITH Q BIT SET
	TBT	RL,SEEDBT,,
	JN	SNDCL0,,
	J	RTCALR+28,,
ENDPATCH(Clear any Call Requests that have Q bit set)

PATCH(830117,1213,GML,PCR630+4,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,18)
	LIS	R0,2		:INDICATE TWO BYTES FOLLOWING,
	JAL	R4,WCI,,
	LHI	R0,3B		:A SEMICOLON
	JAL	R4,WCI,,
	J	PCR630+0A,,	:AND A CARRIAGE RETURN (UPON RETURN)
ENDPATCH(End NTN logons without protocol id with semicolon carriage return)

PATCH(830117,1318,GML,RCON54-60,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,10)
	CLHI	R12,1
	JE	RCON54,,	:IF T-T GATEWAY
	J	RCON54-5A,,	:IF TO SUPE
ENDPATCH(Echo login data if to TYMNET-TYMNET gateway)

PATCH(830117,1529,GML,TER040-5A,,2)
	JFS	.+6		:DO NOT SET DEM
ENDPATCH(Avoid double echo if escape while entering login data)

   IF	X.75
PATCH(830121,1112,GML,ESP120-72,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,1C)
	LR	R5,R5
	JN	ESP120-6C,,	:GO BACK IF SOMETHING IN DIBUF
	LIS	R0,0		:INSERT ZERO FIELD LENGTHS
	JAL	R4,WCI,,	:ZERO ADDRESS LENGTHS
	J	ESP120-6C,,
CONPATCH(PFAC10-24,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,1A)
	JNFS	PA1PTR+0C
	THI	R9,1
	JER	R13		:RETURN IF CALL REQUEST
	LIS	R0,0		:DEFINE FIELD LENGTH OF ZERO
	JFS	PA1PTR+12
	JAL	R4,GCI,,
	LIS	R8,0
	J	PFAC10-1C,,
ENDPATCH(Make sure minimal X.75 Call Connected packet gets filled out)
   EI	X.75

PATCH(830126,0957,GML,CON041+2A,,6)
	JFS	.+6		:DO NOT COUNT PACKET AGAIN
CONPATCH(CON041+5A,,6)
	JFS	.+6		:DO NOT COUNT PACKET AGAIN
ENDPATCH(Do not count set PAD message twice in packet count)

PATCH(830204,1617,TAH,T64S+0C,,4)
	LHI	R5,AAGRP
CONPATCH(T64S30,,2)
	SIS	R5,1
ENDPATCH(Fix destruction of loop counter in 64 sec logic)

PATCH(830205,1405,TAH,LOGT20+0A,,4)
	AHI	R1,DIBIA	:ADD DI BIAS FACTOR
ENDPATCH(Use correct buffer bias factor in half sec logic)

PATCH(830204,1856,ANNATU,MMTEXT+4,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,90)
	TBT	R1,DFLUSH,,
	JE	MMTEXT+0E,,
	TBT	R1,TURKEY,,
	JE	MMFR35,,	:IF NOT IN DIALECT MODE, GO AHEAD AND FLUSH
	LB	R3,TURLEV,R1,
	JN	MMFR35,,	:IF NOT TURKEY LEVEL 0
	TBT	R1,IIXCAL,,
	JE	MMTEX5		:IF TALKING 'OLD' TURKEY
	TBT	R1,PIIX,,
	JE	MMFR35,,		:IF HAVE NOT RCVD SIIX YET
	JAL	R4,GETCH,,		:GET MSG LENGTH
	LR	R2,R0
	SIS	R2,2
	JN	MMFR38,,		:IF NOT LENGTH OF 2, FLUSH REST
	JAL	R4,GETCH,,
	LIS	R2,0
	CLHI	R0,IX.DMM^-8
	JN	MMFR35,,		:IF 1ST BYTE NOT DIALECT HEADER, FLUSH
	JFS	MMTEX8		:GO CHECK 2ND BYTE

MMTEX5	JAL	R4,GETCH,,	:GET MESSAGE LENGTH
	LR	R2,R0
	SIS	R2,1
	JN	MMFR38,,		:IF NOT LENGTH OF 1, FLUSH REST
MMTEX8	JAL	R4,GETCH,,		:GET DIALECT CODE
	JAL	R4,ELIR,,
	CLHI	R0,EXM11
	JN	MMFRA,,		:IF NOT ESCAPED RESET CONFIRMATION
	RBT	R1,DFLUSH,,
	J	MMFRA,,

ENDPATCH(Fix loss of reset confirmation from the net when turkey level 0)

PATCH(830215,1741,ISW,START1-8,,6)
	LHI	R5,ERRBL
	LR	R0,R0				:FILLER
ENDPATCH(Set up loop control counter for clearing error cells correctly)

PATCH(830301,1155,SCY,ESPF,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,0FC)
	LHL	R1,DIBUF
	CBCT(R4)
	LR	R4,R4
	JN	ESPF05
	LHL	R6,IPORT
	SLLS	R6,3	
	ST	R4,DTESAX,R6,
	ST	R4,DTESAV,R6,
	J	CONTRK,,
ESPF05	JAL	R4,GCI,,	:GET CALLING/CALLED ADDR LENGTH
	LR	R5,R0
	NHI	R5,0F		:GET CALLED ADDR LENGTH
	SRHLS	R0,4		:GET CALLING ADDR LENGTH
	LR	R7,R0		:SAVE A COPY
	LHL	R6,IPORT
	SLLS	R6,3		:FORM INDEX
	STB	R5,DTESAX,R6,	:SAVE LENGTH OF CALLED ADDR
	AIS	R6,1
	LR	R5,R5
	JE	ESPF20		:IF 0 LENGTH CALLED ADDRESS
	LHL	R1,DIBUF
ESPF10	JAL	R4,GCI,,	:GET TWO DIGITS OF CALLED ADDRESS(CLDADR)
	SIS	R5,1
	JE	ESPF50		:JUMP IF DONE, ODD NUMBER OF CLDADR LENGTH
	STB	R0,DTESAX,R6,
	AIS	R6,1
	SIS	R5,1
	JG	ESPF10

:	EVEN NUMBER OF CALLED ADDRESS

ESPF20
	LR	R7,R7
	JE	ESPF80
	LHL	R6,IPORT
	SLLS	R6,3		:FORM INDEX
	STB	R7,DTESAV,R6,	:STORE CALLING ADDRESS LENGTH
	AIS	R6,1
ESPF25
	JAL	R4,GCI,,	:GET TWO DIGITS OF CALLING ADDRESS(CLGADR)
	STB	R0,DTESAV,R6,
	AIS	R6,1
	SIS	R7,2
	JGBS	ESPF25
	J	ESPF80


:	ODD NUMBER OF CALLED ADDRESS

ESPF50
	LR	R4,R0
	NHI	R4,0F0		:MASK OFF DIGIT OF CALLING ADDRESS
	STB	R4,DTESAX,R6,	:STORE THE LAST TWO DIGITS OF CLDADR
	LR	R7,R7
	JE	ESPF80
	NHI	R0,0F		:GET THE RIGHT DIGIT WHICH BELONG TO CLGADR
	LR	R5,R0		:SAVE TO R5
	SLLS	R5,4		:SHIFT HIGH ORDER SEMI OCTECT INTO POSITION
	LHL	R6,IPORT
	SLLS	R6,3		:FORM INDEX
	STB	R7,DTESAV,R6,	:STORE THE CLGADR LENGTH
	AIS	R6,1
	SIS	R7,1
	JE	ESPF70
ESPF60
	JAL	R4,GCI,,	:GET TWO DIGITS OF CLGADR
	LR	R4,R0		:SAVE A COPY
	SRLS	R0,4		:GET THE LEFT DIGIT OF TWO DIGITS
	OR	R5,R0		:FORM TWO DIGITS
	STB	R5,DTESAV,R6,	:STORE IT
	AIS	R6,1
	SIS	R7,1
	JE	ESPF80
	NHI	R4,0F		:GET RID OF RIGHT DIGIT
	SLLS	R4,4
	LR	R5,R4		:SAVE TO R5
	SIS	R7,1
	JG	ESPF60
ESPF70
	STB	R5,DTESAV,R6,
ESPF80
	JAL	R4,EMPTY,,
	J	ESPF+8,,
CONPATCH(MCA006+10,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	SBT	RL,RMCNT,,
	STH	R0,TEMP3,,
	J	MCA006+16,,
ENDPATCH(Use addresses from 8F turkey message in Call Accept packet)

PATCH(830304,0930,CBS,LOGS10+14,,4)
	JN	LOGS15+6
ENDPATCH(Send correct clearing cause and diagnostic if gateway down or shut)

PATCH(830304,0946,ADC,CON040,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,24)
	LHL	R1,DPORT,,
	TBT	R1,TURKEY,,	:IS THIS A TURKEY CALL?
	JN	CON050,,	:DO NOT SEND LEVEL 1 PACKET
	TBT	R5,ITICAL,,
	JE	CON050,,
	J	CON040+0A,,
ENDPATCH(Do not send level 1 packet on turkey calls)

PATCH(830304,1011,ADC,UTV10,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	SRHLS	R2,4		:DEFAULT TMT TC
	CR	R4,R2
	JLE	UTV50,,
	LR	R4,R2
	J	UTV50,,
ENDPATCH(Isolate default tmt throughput class)

   IF	X.25
PATCH(830307,0856,ANNATU,MAKESC,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,$a22)
	LHL	R6,TEMP3,,		:GET SAVED CALL ACCEPT LENGTH
	CLHI	R12,EXMF
	JE	MAKS15,,
	J	MAKESC+8,,
ENDPATCH(fix lost call accept when containing "0000")
   EI	X.25

   IF	XOM
PATCH(830308,0928,GML,XCCSI7+0C,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,10)
	JAL	R4,XMROTM,,
	AIS	R1,1		:BUMP POINTER TO CST.LT
	AIS	R5,1
	J	XCCSI7+12,,
ENDPATCH(Move index to circuit speed table for CSITTC XOM modify)
   EI	XOM

PATCH(830314,1029,GML,GMEXP2+4,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	RBT	R2,EXTWDR,,		:NO NEED TO WAIT FOR OLD DIALECT RESPONSE
	SBT	R2,IIXCAL,,
	J	GMEXP2+0A,,
CONPATCH(GMEXP3+4,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	RBT	R2,EXTWDR,,		:NO NEED TO WAIT FOR OLD DIALECT RESPONSE
	RBT	R2,IIXCAL,,
	J	GMEXP3+0A,,
ENDPATCH(Do not wait for old dialect response if got new dialect response)


  IF	LAPB
PATCH(830317,1511,ANNATU,SFR010+1A,,2)
	JNFS	SFR020			:IF IT WAS A COMMAND FRAME
CONPATCH(SFR040+18,,2)
	JNFS	SFR050			:IF IT WAS A COMMAND FRAME
ENDPATCH(fix erroneous setting of CR bit on frame reject to command frames)
  EI	LAPB
   IF	DNICND
PATCH(830317,1614,ANNATU,FNDD20+2,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,$a12)
	JNFS	FNDD40			:IF NOT THE FIRST ENTRY
	AIS	R1,2
FNDD40	LH	R4,NODTAB,R1,		:GET DEFAULT VALUE
	JR	R9			:RETURN
ENDPATCH(fix TNIC of 0FFFF when no DNICNODES definition)
  EI	DNICND

   IF	X.25
PATCH(830321,1134,GML,ESP123-4,,4)
	J	ESP125+0C
ENDPATCH(Do not update facility count if no need to send in Call Accept)
   EI	X.25

PATCH(830323,1411,GML,PCR580+2,,6)
	LCS	R6,1		:SET BYTE COUNT TO -1
	J	PCR620
CONPATCH(PCR660-6,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,14)
	LR	R6,R6
	JL	PCR550,,	:IF NULL CUD
	JAL	R4,WCI,,	:WRITE 1ST BYTE OF CUD
	J	PCR660,,
ENDPATCH(Do not mistake 0 in 1st byte of non-CCITT CUD for null CUD)


   IF	XOM
PATCH(830325,1600,SCY,XMCADP+0E,,6)
	LA	R1,ADRPO,RL,RL	:HALFWORD ARRAY ADDRESS	
ENDPATCH(Changed to use halfword index array address)
   EI	XOM

PATCH(830512,1643,GML,IBRK42+14,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,0E)
	JAL	R4,WCI,,
	LIS	R0,1		:INITIALIZE ROUGH COUNT FOR SEGMENT ACCOUNTING
	J	IBRK42+1A,,
CONPATCH(IBRK44-6,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,8)
	JAL	R4,WCI,,
	LIS	R0,3		:SET ROUGH COUNT FOR SEGMENT ACCOUNTING
CONPATCH(IBRK44,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,18)
	LHL	R7,DPORT,,
	STH	R0,RUFCNT,R7,R7
	JAL	R4,COSP,,
	J	IBRK50,,
ENDPATCH(Count indication of break in segment accounting)

   IF	DDTDIA
PATCH(830330,1454,GML,PRNR,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,0C)
	LA	R9,M.RNR,,
	J	PREJ10,,
ENDPATCH(In packet trace do not replace RNR message with REJ message)
   EI	DDTDIA

PATCH(830405,2143,ANNATU,ICP040-6,,2)
	JFS	ICP040			:DELAY SETTING OF DATA FORWARDING TIMER
ENDPATCH(Fix occasional clear diag 148 from IIX due to data forwarding timer)

PATCH(830406,1500,FAK,PCR110-4A,PCR160)


:	GET DATA FROM CALL REQUEST PACKET AND IF VALID BUILD A PSEUDO-
:	NEEDLE

:	GET AND VALIDATE THE CALLED AND CALLING ADDRESSES, PUT THEM IN
:	DTESAX AND DTESAV BUFFERS

:	ROUTINE HEIRARCHY;
:		PCR100 - MAIN
:			VALADR - ADDRESS VALIDATION
:				GDG100 - GET DIGIT (SEMI-OCTET)
:				SDG100 - STORE DIGIT (SEMI-OCTET)

:	REGISTER USAGE;
:		ALL LEVELS
:		  R3,R7,R8,R12,R6
:			VALIDATION ON DOWN
:			  R9,R1,R2
:				GET DIGIT
:				  R4


:	LOCAL STORAGE;
	SEG	A.WTBL
PCRTMP	HS	1		:SAVE AREA
				:  1ST BYTE-GET RTN ADDR SAVE AREA
				:  2ND BYTE-UPPER/LOWER DIGIT SWITCHES
				:	BIT 6 - GET RTN SWITCH
				:	BIT 7 - STORE RTN SWITCH

:MAIN ROUTINE
	SEG	A.CODE
PCR100	LR	R6,R6			:PACKET BYTES LEFT
	JE	RTESHT			:   NO
	JAL	R4,PICKCH		:GET ADDR. LENGTHS
	LR	R7,R0
	NHI	R7,0F			:GET CALLED ADDR LENGTH
	SRLS	R0,4			:GET CALLING ADDR LENGTH
	LHL	R3,IPORT		:SETUP ADDR. BUFFR INDEX
	SLLS	R3,3
	LIS	R4,0
	ST	R4,DTESAX,R3,		:CLEAR ADDR. BUFFRS
	ST	R4,DTESAX+4,R3,
	ST	R4,DTESAV,R3,
	ST	R4,DTESAV+4,R3,
	STB	R7,DTESAX,R3,		:STORE CALLED ADDR. LENGTH
	STB	R0,DTESAV,R3,		:STORE CALLING ADR. LENGTH
	STH	R4,PCRTMP,,		
	LHI	R12,DIA067		:SET UP 'ILLEGAL CALLED ADR'
	LA	R8,DTESAX,,	
	JAL	R7,VALADR		:VALIDATE CALLED ADDRESS
	J	RTECAL			:ERROR RETURN
	LHL	R3,IPORT		:SETUP ADDR.BUFFR INDEX
	SLLS	R3,3
	LIS	R4,0F
	RBT	R4,PCRTMP,,		:CLEAR ONLY 'STORE DGT RTN' SWITCH
	LHI	R12,DIA068		:SETUP 'ILLEGAL CALLING ADDR'
	LA	R8,DTESAV,,	
	JAL	R7,VALADR		:VALIDATE CALLING ADDRESS
	J	RTECAL			:ERROR RETURN
	J	PCR160

:VALIDATION ROUTINE
VALADR	LIS	R1,0			:INITIALIZE ADDR LENGTH COUNTER
	LB	R0,,R8,R3		:GET ADDR LENGTH
	LR	R2,R0			
	CLHI	R0,0F			:ERROR > 14
	JER	R7			:   YES
	CLHI	R0,00			:NO ADDRESS
	JE	VAL150			:   YES, NORMAL RETN
VAL100	JAL	R9,GDG100		:GET DIGIT
	CHI	R0,9			:TEST IF BCD
	JGR	R7			:   NO, ERROR RETN
	JAL	R9,SDG100		:STORE DIGIT
	AIS	R1,1			
	CR	R2,R1			:ALL DIGITS PROCESSED
	JN	VAL100			:   NO, CONTINUE
VAL150	AIS	R7,4			:TAKE NORMAL RETURN
	JR	R7

:GET DIGIT ROUTINE
GDG100	LIS	R0,0E
	TBT	R0,PCRTMP,,		:GET UPPER (NIBBLE) DIGIT
	JN	GDG150			:   NO
	CBT	R0,PCRTMP,,		:FLIP SWITCH
	LR	R6,R6			:PACKET BYTES LEFT
	JLE	RTESHT			:   NO
	JAL	R4,PICKCH		:GET BYTE FROM RING
	STB	R0,PCRTMP,,		:SAVE BYTE
	SRLS	R0,4			:SEND BACK UPPER DIGIT
	JR	R9			:RETURN
					:GET LOWER (NIBBLE) DIGIT
GDG150	CBT	R0,PCRTMP,,		:FLOP SWITCH
	LB	R0,PCRTMP,,		:GET BYTE SAVED PREVIOUSLY
	NHI	R0,0F			:SEND BACK  LOWER DIGIT
	JR	R9			:RETURN

:STORE DIGIT ROUTINE
SDG100	LIS   	R4,0F
	TBT	R4,PCRTMP,,		:STORE UPPER (NIBBLE) DIGIT
	JN	SDG150			:   NO
	CBT	R4,PCRTMP,,		:FLIP SWITCH
	SLLS	R0,4			:MOVE DIGIT TO UPPER NIBBLE
	AIS	R3,1
	STB	R0,,R8,R3		:STORE DIGIT IN BUFFER
	JR	R9			:RETURN
					:STORE LOWER DIGIT
SDG150	CBT	R4,PCRTMP,,		:FLOP SWITCH
	LB	R4,,R8,R3		:GET BYTE FROM BUFFER
	OR	R0,R4			:COMBINE TWO DIGITS
	STB	R0,,R8,R3		:STORE BACK IN BUFFER
	JR	R9			:RETURN
ENDPATCH(Check 15 digit addresses, fix 1 digit Called Addr processing)

PATCH(830406,1515,ISW,L1TBL6+18,,2)
	HC	L1PIEA+6-L1TBL
CONPATCH(L1PIEA,,6)
	J	PA1PTR,,		:move routine "L1PIEL" to patch area.
CONPATCH(L1PIEA+6,,0C)
: HERE TO SEND PARAMETER INDICATION FOR X.3 PARAMETER 6
	LB	R0,PADPRX,R8,	:PARAMETER REFERENCE NUMBER
	OHI	R0,100		:PARAMETER VALUE = 1
	JBS	L1PXE		:
CONPATCH(PA1PTR,,16)
	SBT	RL,L1PIEL,,	:SEE XCOMRT.F31 FOR COMMENTS
	LB	R0,PADPR1,R8,	:
	SBT	R0,L1PIEP,,	:
	J	4,R9
ENDPATCH(make TPAD report parameter 6 = 1)

PATCH(830406,1630,ANNATU,ICP040-30,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	STB	R0,INBCT,R1,
	STB	R0,TURLEV,R1,	:INITIALIZE TURKEY LEVEL TO ZERO
	J	ICP040-2A,,
CONPATCH(NCC005,,2)
	JFS	NCC005+6
ENDPATCH(Initialize turkey level to zero when origination)


PATCH(830406,1640,SCY,RTCLRC+24,,4)
	LHI	R0,AA.DBU
ENDPATCH(Fix crash code DA caused by termination code FF)

PATCH(830406,2245,ANNATU,DDN008+3A,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,$A36)
	STB	R0,TMTLIM,R1,	:INITIALIZE TRANSMIT LIMIT TO MAX DATA
	SBT	R1,POCHNG,,	:MAKE SURE FINAL COUNTS GET REPORTED
	SBT	R1,PICHNG,,
	SBT	R1,SICHNG,,
	SBT	R1,SOCHNG,,
	J	DDN008+40,,
ENDPATCH(Fix non reporting of final packet/segment counts when Tymsat calls)
ENDPATCH(are cleared during call set-up)

PATCH(830412,1948,ANNATU,LOGS05-8,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,$A22)
	AHI	R1,IDBIA
	JAL	R4,EMPTY,,		:FLUSH INPUT
	RBT	R5,INPROG,,		:CLEAR MSG IN PROGRESS ARRAY
	J	LOGS05,,
ENDPATCH(Fix incomplete flushing of login data when flush input msg from sup)

PATCH(830509,1342,GML,LIM500+40,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,2E)
	LH	R2,IE.MT,R3,R3		:GET EPORT
	STH	R0,IE.MT,R3,R3
	STH	R0,EI.MT,R2,R2
	SBT	R2,EPA.F,,
	LIS	R0,0
	SLLS	R3,2
	STH	R0,BF+IEDBIA,R3,	:CLEAR BUFFER FLAGS FOR IED BUFFER
	STH	R0,BF+IECBIA,R3,	:CLEAR BUFFER FLAGS FOR IEC BUFFER
	J	LIM500+52,,
ENDPATCH(Get EPORT from IE table and clear IED and IEC buffer flags)

PATCH(830415,0000,ANNATU,PCA150+12,,6)
	TBT	R1,IIXCAL,,		:TEST IF CALL FROM OLD INTERFACES
CONPATCH(MAKS25-8,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,$A32)
	CLHI	R12,EXMF
	JE	MAKS25,,		:IF SHORT CALL ACCEPT
	LHL	R9,DPORT
	LB	R9,TURLEV,R9,		:LOOK AT TURKEY LEVEL
	JE	MAKS25,,		:LEVEL 0 EXM12 MSG HAS 1 BYTE OF LENGTH
	J	MAKS25-2,,
ENDPATCH(send extended call accept of length byte one when turlev=0)

PATCH(830416,0004,ANNATU,MMD020+12,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,$A12)
	LHL	R5,DPORT
	SLLS	R5,2			:FORM DIBUFFER BIAS
	J	MMD020+18,,
ENDPATCH(Fix possible generation of erroneous data packets due to data timer)

PATCH(830418,2048,ANNATU,PSUBF2,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,$A26)
	LHL	R1,DPORT
	JE	RMK300,,		:IF DISPATCHER PORT DOES NOT EXIST
	THI	R8,01
	JN	PSUBF6,,		:IF PREDIGESTED ISIS RESPONSE
	J	PSUBF2+8,,
ENDPATCH(Fix dispatcher crash caused by outputting STP response when no DPORT)

PATCH(830419,1740,ANNATU,SECN2C+16,,4)
	JFS	SECN2C+1A
ENDPATCH(Fix possible back pressure releasing problem)

  IF	FINACT
PATCH(830422,2020,ANNATU,FID040+76,,6)
	STB	R5,XSSTAT,RL,
CONPATCH(PRRT30+4,,4)
	JN	RFG210		:IF RR COMMAND RECOVERY MODE, PRESERVE P/F
ENDPATCH(Fix RR command frame sending and collision problem)
  EI

   IF	XOM
PATCH(830427,1817,GML,XOMHS9+0E,,10)
	LR	R7,RL
	SRLS	R7,1		:CREATE LINE NUMBER
	TBT	R7,HPA.FC,,
	JNFS	.+6
	OHI	R0,80
ENDPATCH(Do not test host ports available array with 2 times line number)
   EI	XOM

PATCH(830503,1010,GML,MMD050+32,,4)
	LHL	R7,DPORT
ENDPATCH(Get DPORT instead of IPORT to figure DI buffer)

PATCH(830503,1015,ANNATU,PCA140+10,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,18)
	STH	R3,TEMP3+2,,
	LHL	R1,IPORT,,
	RBT	R1,TCRCV,,	:INIT 'SAW THROUGHPUT CLASS' BIT
	J	PCA140+16,,
CONPATCH(PCA153,,2)
	AIS	R6,2		:ACCOUNT FOR TC FACILITY
CONPATCH(PCA153+20,,6)
	LB	R0,TCLTR,R2,	:GET TC VALUE TO SEND
CONPATCH(PCA156+2,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,14)
	L	R2,TEMP1,,	:END OF FACILITY
	LR	R9,R6
	AR	R9,R5		:END OF CALL ACCEPT
	LR	R5,R2		:POSITION TO ADD TC FACILITY
	SIS	R9,1		:LAST BYTE OF CALL ACCEPT
	J	PCA158,,
CONPATCH(PCA158+2,,4)
	JL	PCA153		:IF FINISH COPYING
CONPATCH(PCA158+0A,,4)
	STB	R0,2,R9
ENDPATCH(Fix illegally formated EXM12 msg when TC negotiation set)

PATCH(830509,1437,GML,T15S30+6,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,0E)
	LCS	RL,1		:NO LINKS FOR HPA REPORT
	JAL	R9,HPARPT,,
	J	T15S05,,
CONPATCH(HPARPT,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	STH	R4,TEMP3+2,,
	STH	R5,TEMP3,,	:SAVE R5
	J	HPARPT+6,,
TCH(Initialize registers for host port availability reporting)


PATCH(830513,1530,ADC,L1P4E,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	RBT	R8,NDATIM,,	:Enable timer 
	LB	R0,X3IDLE,R2,
	J	L1P4E+6,,
ENDPATCH(Fix not resetting of timer bit when idle timer value was 0)


PATCH(830517,2105,SKW,NPMT20+1A,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,5E)
        LHL     R1,DPORT
        LB      R0,ORGTID,R1,   :TEST FOR HALF DUPLEX TERMINAL
        THI     R0,20
        JE      CRLFP2          :IF NOT
        LIS     R0,5            :INSERT ADDITIONAL CR,LF
        LHL     R1,DPORT        
        LIS     R2,2
        JAL     R4,SLOR,,
        LHI     R0,8D           :<CR>
        JAL     R4,PUTCH,,
        LHI     R0,8A           :<LF>
        JAL     R4,PUTCH,,
        JAL     R4,ELOR,,
CRLFP2  LIS     R0,4
        LHL     R1,DPORT
        LIS     R2,1
        JAL     R4,SLOR,,
        LHI     R0,3B           :SEND USER SEMI COLON PROMPT
        JAL     R4,PUTCH,,
        JAL     R4,ELOR,,
        J       NPMT20+38,,
ENDPATCH(Insert additional CRLF Semicolon prompt for half duplex terminal)

PATCH(830603,1304,GML,IBRK40+0E,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,22)
	LHL	R1,IPORT,,
	TBT	R1,FLINT,,	:STILL WAITING FOR INTERRUPT CONFIRMATION?
	JN	IBRK50,,	:IF SO
	LHL	R1,IEDBUF,,
	LHI	R0,ZDATA
	J	IBRK40+16,,
ENDPATCH(Send only one break indication per interrupt packet)

  IF	DDTDIA
PATCH(830601,0924,GML,RMBL75,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,28)
	STH	R14,BYTCNT,R13,
	LB	R2,CURSEC,RL,
	LHL	R7,SECBIA,RL2,	:GET BIAS INTO OUTPUT SECTORS
	AR	R7,R2		:ADD IN CURSEC TO GET WORD POINTER
	SLLS	R7,2		:CHANGE WORD POINTER INTO BYTE POINTER
	L	R7,SECADR,R7,	:GET SECTOR ADDRESS
	STH	R14,0,R7,	:STORE BYTE COUNT IN SECTOR
	J	RMBL75+0A,,
ENDPATCH(Put byte count into output sector to help XOSTR)
  EI

PATCH(830613,1155,GML,LOGS20+12,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,18)
	TBT	R1,NTNCAL,,
	JE	LOGS30,,	:IF NTN CALL, SEND "PASSWORD" PROMPT
	TBT	R1,NUNCAL,,
	J	LOGS20+18,,
ENDPATCH(Prompt non-NTN caller for password regardless of TKSUP)


  IF	DDTDIA
PATCH(830614,1639,GML,PBT12+0A,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	L	R13,PBTTBL,R6,
	NI	R13,0FFFFFF
	J	PBT12+10,,
ENDPATCH(Mask link number from packet buffer trace XPBTR)
  EI	DDTDIA

  IF	1-OLDTUR
PATCH(830713,1737,GML,NCC028-0A,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,5C)
	RBT	R1,EXTWDR,,
	RBT	R8,NEWLOG,,	:NO LONGER LOGGING IN
	LA	R9,DTESAX,,
	LI	R8,AA.CD4
	JAL	R13,ACTADR,,	:REPORT CALLED ADDRESS TO ACCOUNTING
	LA	R9,DTESAV,,
	LI	R8,AA.CL4
	JAL	R13,ACTADR,,	:REPORT CALLING ADDRESS TO ACCOUNTNG

:	REPORT ORIGINATING HOST NUMBER.
	LHL	R9,IPORT,,
	LI	R7,AA.COH	:CIRCUIT ORIGINATING HOST MSG
	LB	R9,IL.MT,R9,	:IPORT TO LINK #
	LB	R9,LH.MT,R9,	:LINK # TO HOST ORDINAL
	LHL	R0,HOSTS,R9,R9	:HOST ORDINAL TO HOST #
	OR	R7,R0
	JAL	R9,GENRPT,,
	J	CONUP,,
ENDPATCH(Report address accounting for calls to non-turkeys)
  EI

PATCH(830614,1533,GML,PCR455+8,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,1C)
	AIS	R8,1
	SIS	R7,1
	JN	PCR455,,
	ST	R5,TEMP,,	:SAVE RING POINTER
	ST	R6,TEMP1,,	:SAVE RING CHAR COUNT
	J	PCR500,,
ENDPATCH(After gateway username processing`, take care of call user data)

PATCH(830623,1454,GML,LOGT45+5A,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,16)
	LHL	R1,IPORT
	SLLS	R1,2
	AHI	R1,IECBIA
	STH	R1,IECBUF,,
	J	LOGT45+60,,
ENDPATCH(Set up IECBUF for login timer logic)

PATCH(830621,1743,GML,LOGT45+24,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,0E)
	JAL	R4,WCI,,
	LIS	R0,2		:DATA LENGTH IS 2
	J	LOGT45+2A,,
ENDPATCH(Set length of invitation to clear packet)

PATCH(830627,1245,GML,ESP15+8,,2)
	JNFS	ESP15B
CONPATCH(ESP16+4,,2)
	JFS	ESP16A
ENDPATCH(Do not confuse Clear and Reset causes)

  IF	XOM
PATCH(830707,1258,GML,XIIXD2+0A,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,36)
	CLI	R3,(IX.SDR^10)!ID.NEC
	JE	XIIXD6,,
	CLI	R3,(IX.SDR^10)!ID.ASC	:ASCII DIALECT?
	JN	XIIXD2+14,,		:IF NOT
	LR	R0,R6
	SIS	R0,1
	JAL	R4,FLUSH,,		:TRASH REST OF MESSAGE
	RBT	R1,PIIX,,		:DONE PROCESSING IIX
	RBT	R1,IIXCAL,,		:ASCII DIALECT DOES NOT NEED IIX TREATMENT
	LR	R2,R1			:GET DPORT FOR XOM LOGIN
	J	NXOM10+4E,,		:GO GREET USER
CONPATCH(NXOM11+1C,,4)
	LIS	R0,9
	LIS	R2,6
CONPATCH(NXOM11+30,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,16)
	LI	R0,((IX.SDC&0FF)^18)!(ID.NEC^8)!(ID.ASC^-8)
	JAL	R4,PUTW,,
	LHI	R0,ID.ASC&0FF		:INCLUDE ASCII
	J	NXOM11+3E,,
ENDPATCH(Allow XOM to accept ASCII dialect)
  EI	XOM

PATCH(830708,1048,GML,GME040+6,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,16)
	JAL	R9,SNDDR,,
	LHL	R1,FACBUF
	JAL	R4,EMPTY,,	:EMPTY FACILITY BUFFER
	J	CONUP,,
ENDPATCH(Empty facility buffer if choosing ASCII dialect)

PATCH(830714,0210,LLIN,HANGAL,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	STH	R5,PA0PTR,,	:save message flag
	ST	R9,HNGRET,,
	J	HANGAL+6,,
CONPATCH(HANG10+20,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,18)
	LHL	R7,PSDIAG,RL,RL	:transfer cause and diagnostic from 
	STH	R7,PSDIAG,R2,R2	:line to IPORT
	TBT	R8,TURKEY,,
	J	HANG10+26,,
CONPATCH(HANG20,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	LHL	R5,PA0PTR,,	:get message flag
	LA	R3,CLMSHD,,
	J	HANG20+6,,
CONPATCH(PA0PTR,,2)
	HS	1		:flag storage
ENDPATCH(Save message flag and PSDIAG when hanging all users)


PATCH(830830,1630,JPM,GME060+0A,,6)
	JE	GME010+56,,		:Check other offerings
ENDPATCH( Check other dialect offerings if DSP not in use )

PATCH(830902,1646,ISW,PSUB8A+0E,,0C)
	STB	R0,PSDIAS+1,,	:STORE INTERRUPT DATA BYTE AS DIAGNOSTIC CODE
	SBT	RL,RMDIAG,,	:SO ZCAUS OPTION DOESNT ZERO IT OUT
ENDPATCH(Store interrupt data byte as diagnostic so ZCAUS option doesnt zero)

PATCH(830906,1521,GML,CKAD22-12,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	SIS	R3,3
	CLHI	R3,1
	JLE	CKAD25,,	:IF < 5 DIGITS IN ADDRESS
	J	CKAD22-0C,,
CONPATCH(CKAD22+8,,2)
	JGBS	CKAD22
ENDPATCH(Do not try to zero out called address of 4 digits when NONTN set)

PATCH(830906,1527,GML,GETPPA+6,,4)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	XHI	R2,0FFFF
	JFFOH	R2,.+8		:GO TO GETPPB
	J	GETPPC,,	:CHECK FOR ALL PMP BLOCKS IN USE
	J	GETPPB,,
CONPATCH(GETPPB+4,,8)
	CLHI	R1,PMPCNT	:PAST LAST USABLE FLAG?
	JGE	GETPPD
CONPATCH(GETPPC+2,,4)
	CLHI	R1,((PMPCNT+0F)/10)*2	:MORE TO CHECK?
ENDPATCH(Correct availabililty and range checks for PMP blocks)

   IF	(LAPB!LAP)&HDLC
PATCH(830907,0912,GML,IIC050+0E,,2)
	JGBS	IIC050
CONPATCH(IIC030+0A,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,8)
	AHM	R0,EMPTYI,RL,RL
	JR	R5
ENDPATCH(Account for empty I frames)
   EI


PATCH(830915,1410,LLIN,T2S130+1E,,6)
	JFS	.+6
ENDPATCH(Let OB routine clear W.ITP array)

   IF	HDLC&LAPB
PATCH(830930,1709,GML,RFG130-4,,4)
	J	SNFMRY		:TO SEND FRMR WITH Y BIT SET
ENDPATCH(RECEIVING A FRAME EXCEEDED MAX CAPACITY)
   EI	HDLC&LAPB

PATCH(830920,1430,GML,PCR630+4,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,2A)
	LIS	R0,1
	TBT	RL,TEMP2,,
	JEFS	.+0E		:IF NOT NETUSERPREFIX LOGIN 
	AIS	R0,1
	JAL	R4,WCI,,
	LHI	R0,3B
	JAL	R4,WCI,,
	LIS	R0,0D
	JAL	R4,WCI,,
	J	PCR640,,
ENDPATCH(Only add semi-colon to NETUSERPREFIX login string)

PATCH(830926,1030,SCY,HANG60+1C,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,10)
	JAL	R9,IDONE,,	:DO INTERNAL PORT CLEAN UP
	LHL	R7,IPORT
	J	HANG60,,
ENDPATCH(Restore R7 since it'll get destroyed in routine IDONE)

PATCH(831028,0940,GML,NDLST2+52,,4)
	LHL	R1,EPORT
ENDPATCH(Correct port calculation for port availability check)

PATCH(831024,1830,JPM,ESC050+02A,,4)
	JN	ESC110
CONPATCH(ESD010+01C,,4)
	JN	ESD012
CONPATCH(PFRA00+08,,4)
	JN	PFAC52
ENDPATCH( Correct tests for passing national options to link )


PATCH(831019,1201,JPM,PCAN10+028,,04)
  IF	X.75
	JE	PCAN12-012
  ELSE	X.25
	JN	RTECA0
  EI	
ENDPATCH( Set RVCHRG bit for collect calls )

PATCH(831020,1130,JPM,CON010-024,,06)
	J	PA1PTR,,
CONPATCH(PA1PTR,,01C)
	TBT	RL,RCNA.F,,
	JE	CON010,,
	LHI	R0,AA.ERR
	STH	R0,TERMCD,,
	J	CON010-01A,,
ENDPATCH( Test Reverse Charge Acceptance before clearing collect call )

PATCH(830614,1240,SKW,CLD040+1A,,6)
	J       PA1PTR,,
CONPATCH(PA1PTR,,2A)
	LHL     R1,DPORT
	LB      R0,ORGTID,R1,   :TEST FOR HALF DUPLEX TERMINAL
	THI     R0,20
	JE      CLD045          :IF NOT
	J       CLD040+3E,,     :RETURN FOR HALF DUPLEX TERMINAL
CLD045  TBT     6,DETBL,,
	JE      CLD040+3E,,     :TEST FOR ECHOABLE
	J       CLD040+22,,
ENDPATCH(Do not echo Called Address to Half Duplex Terminal)
          
PATCH(830614,1250,SKW,NERR20+0C,,6)
	J       PA1PTR,,
CONPATCH(PA1PTR,,2A)
	LHL     R1,DPORT
	LB      R0,ORGTID,R1,   :TEST FOR HALF DUPLEX TERMINAL
	THI     R0,20
	JE      NERR21          :IF NOT
	J       NERR20+30,,     :RETURN FOR HALF DUPLEX TERMINAL
NERR21  TBT     R6,DETBL,,      :TEST IF ECHOABLE
	JE      NERR20+30,,
	J       NERR20+14,,
ENDPATCH(Do not echo Invalid Called Address to Half Duplex Terminal )
          
PATCH(830614,1300,SKW,CGTU10+16,,6)
	J       PA1PTR,,
CONPATCH(PA1PTR,,2A)
	LHL     R1,DPORT
	LB      R0,ORGTID,R1,   :TEST FOR HALF DUPLEX TERMINAL
	THI     R0,20
	JE      CGTU15          :IF NOT
	J       CGTU10+3A,,     :RETURN FOR HALF DUPLEX TERMINAL
CGTU15  TBT     R6,DETBL,,      :TEST IF ECHOABLE
	JE      CGTU10+3A,,
	J       CGTU10+1E,,
ENDPATCH(Do not echo Call User Data to Half Duplex Terminal)

  IF	XOM
PATCH(831108,1707,GML,XOMNA0+6,,20)
	XOMTAP(NAC.F)		
	XOMTAP(NAP.F)
CONPATCH(XOMUN0+6,,20)
	XOMTAP(UNC.F)		
	XOMTAP(UNP.F)
CONPATCH(XOMBC0+6,,20)
	XOMTAP(BCC.F)		
	XOMTAP(BCP.F)
CONPATCH(XMCNAT,,0C)
	LA	R7,NAC.F,,	
	LA	R8,NAP.F,,
CONPATCH(XMCUNK,,0C)
	LA	R7,UNC.F,,	
	LA	R8,UNP.F,,
  IF	X.75
CONPATCH(XMCTRA,,0C)
	LA	R7,TRC.F,,	
	LA	R8,TRP.F,,
  EI
CONPATCH(XMCBCU,,0C)
	LA	R7,BCC.F,,	
	LA	R8,BCP.F,,
ENDPATCH(Reverse XOM handling of NATIONAL`, UNKNOWN`, TRAFFICLASS`, BCUG)
  EI	XOM

PATCH(831109,1653,GML,PSUBDA-12,,6)
	STB	R0,PSDIAS+1,,	:STORE DIAGNOSTIC
ENDPATCH(Store diagnostic in correct byte of PSDIAS)


PATCH(831102,1540,LLIN,ESP14,,20)
	RBT	R7,WTFLGB,,	:WHAT WE ARE DOING IS TO MOVE
	JN	ESD010		: AIS R5,1  DOWN AFTER WE HAVE
	RBT	R7,WTCNT1,,	:HAD THE FLAG AND LEN BYTES
	JN	ESD020		:SO WE CAN CALL ESD100 TO
	RBT	R7,WTCNT2,,	:COPY THE REST OF ISIS MESSAGE
	JN	ESD030		
	AIS	R5,1		:READY FOR ESD100 ROUTINE
CONPATCH(ESP14+34,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,20)
	SIS	R5,1		:IF NOT FOR ESD100 READJUST R5
	TBT	R7,QDSCD,,
	JN	ESD070,,
	RBT	R7,WTQMSG,,
	JN	ESD040,,
	J	ESP14+3E,,
CONPATCH(ESC325-4,,4)
	J	ESC310		:WE HAVE TO RESET WTCNT2
CONPATCH(ESC330+6,,4)
	JE	ESC350+0C	:WE DO NOT FETCH ISIS CHAR, AVOID TO
                                :SUBTRACTING 1 FROM R5
ENDPATCH(FIX TYPE 94 ESCAPED MESSAGE LENGTH COUNTER)

PATCH(840111,1714,GML,PCR520+0E,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,018)
	TBT	R2,GATWAY,,
	JN	PCR550,,	:IF GATEWAY CALL, DON'T ADD TERMINATOR
	TBT	R2,NTNCAL,,
	J	PCR520+14,,
CONPATCH(PCR525+08,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,18)
	SBT	R2,CUDUSD,,
	TBT	R2,GATWAY,,
	JN	PCR530,,	:IF GATEWAY CALL, DON'T ADD TERMINATOR
	J	PCR525+0E,,
CONPATCH(PCR578+4,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,18)
	LCS	R6,1		:INIT COUNT
	TBT	R2,GATWAY,,
	JN	PCR620,,	:IF GATEWAY CALL, DON'T ADD TERMINATOR
	TBT	R2,NTNCAL,,
	J	PCR578+0A,,
CONPATCH(PCR620+2,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,18)
	TBT	R2,GATWAY,,
	JN	PCR640,,	:IF GATEWAY CALL, DON'T ADD TERMINATOR
	TBT	R2,NTNCAL,,
	J	PCR620+08,,
ENDPATCH( Correct tests for login usage to include GATEWAY option )

PATCH(840130,1400,LLIN,ESD049+6,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,0C)
	GL	ESD115		:END OF ISIS DATA MESSAGE SHALL
	SIS	R5,1		:JUMP TO ESD115 INSTEAD OF ESD120
	JE	ESD115		:R5 IS LENGTH OF ISIS DATA MSG.
	J	ESD050,,	:END OF DATA PACKET SHALL JUMP
CONPATCH(ESD050+18,,6)		:TO ESD120
	J	PA1PTR,,	:R6 IS LENGTH OF DATA PACKET
CONPATCH(PA1PTR,,0C)
	SIS	R5,1
	JE	ESD115
	J	ESD060,,
CONPATCH(ESD060+4,,18)
	JAL	R4,WCI,,	:MAKE SURE WE HAVE MORE DATA FOR 
	SIS	R6,1		:THIS PACKET TO SET WTQPAR FLAG
	JLE	ESD120
	SBT	R7,WTQPAR,,	:WE HAVE MORE DATA FOR THIS PACKET
	J	PA1PTR,,
CONPATCH(PA1PTR,,0C)
	SIS	R5,1
	JG	ESD050,,
	J	ESD115
CONPATCH(ESD100+6,,0C)
	JG	PA1PTR,,
	J	ESD115,,
CONPATCH(PA1PTR,,26)
	JAL	R4,GETCH,,		:GET CHAR. IN IRING
	JAL	R4,WCI,,		:WRITE IT IN DIBUFFER
	SIS	R6,1			:DECREMENT THE COUNTER
	J	ESD100,,
ESD115	STH	R6,DNCNT,R7,R7		:STORE THE COUNT OF REMAINING
	JAL	R4,ELIR,,		:DATA PACKET
	J	MMFRA,,			:GET THE REST OF ISIS MSG.
ENDPATCH(FIX BUG IN PROCESSING END OF ISIS DATA MSG TYPE 94)

PATCH(840103,1500,ISW,PCA300,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,1C)
	LHL	R1,DPORT
	TBT	R1,TURKEY,,
	JN	PCA310+0C,,	:IF TURKEY, DON'T BOTHER WITH TPAD PARAMETERS
	LB	R0,TREF3,RL,	:OTHERWASE, SAME AS BEFORE
	J	PCA300+0A,,	:AND CONTINUE
ENDPATCH(FIX THE PREMATURE M-BIT DATA PACKET FORWARDING BUG)

PATCH(840111,0900,GML,CONTRK+3C,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,48)
	RBT	R2,INPROG,,
	JN	CONTRK+46,,
	CLHI	R9,3
	JLE	CON025,,	:IF NOT ENOUGH CHARACTERS TO CONTAIN A CUD
	LHL	R4,IPORT
	TBT	R4,GATWAY,,
	JE	CON025,,	:IF NOT GATEWAY CALL
	JAL	R4,GCI,,	:DISCARD GOUGING CHARACTER COUNT
	JAL	R4,GCI,,	:DISCARD GOUGING CHARACTER
	JAL	R4,GCI,,	:GET CUD LENGTH
	LR	R5,R0
	SIS	R9,3		:ADJUST CHARACTER COUNT BY CHARACTERS READ
	SR	R9,R5		:REDUCE CHARACTER COUNT BY CUD LENGTH
	JGE	CON015,,	:GO COPY CUD
	TRAP(R1,DB)		:CUD LENGTH GREATER THAN CONTENTS OF BUFFER
ENDPATCH(Pass CUD of GATEWAY call to non-turkeys)

	IF	TPIDS
PATCH(831220,1500,STUTZMAN,PCR550+4,,16)
	LHI	R4,TIDHST	:All we are doing is moving the test and jump
	TBT	R1,ITICAL,,	:of TPID.F below the test and jump of ITICAL.
	JEFS	PCR552		:This is necessary to make sure that TEMP1 is
	TBT	RL,TPID.F,,	:not garbage if a call was placed with a non-
	JNFS	PCR553		:CCITT protocol ID on a link with TPID set.
	NOPR	R4,R4		:Note that the Telenet protocol ID has the
				:first byte set specifying that it is CCITT
				:even though it is an aberration. Thus, ITICAL
				:will normally be set on Telenet PAD calls.
				:Also note that the symptom of this problem
				:is the sending of a request for psuedo needle
				:with TID of C0 (aux circuit?).  Without ISIS
				:aux circuit status, the dispatcher zaps us.
ENDPATCH(Fix call clear if receive non-CCITT protocol ID on Telenet PID link)
	EI


 IF	HCO.MX
PATCH(831108,1324,PSD,HCOST5+0E,,4)
        JN      HCOST0,0
ENDPATCH(Fix HOSTCOST when NUMSTEPS equals DPORTS)
 EI	HCO.MX

PATCH(840206,0940,GML,PA0PTR,,2*NDGRPS)
W.FCI	HS	NDGRPS		:FLAGS INDICATING WAITING TO FINISH INITIALIZATION
CONPATCH(LIM600+1E,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	SBT	R1,W.FCI,,	:INDICATE NEED TO GO THROUGH NDLST3
	TBT	R1,W.ITP,,
	J	LIM600+24,,
CONPATCH(NDLST3+4,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,18)
	LHL	R2,DPORT
	RBT	R2,W.FCI,,
	JER	R0		:IF ALL DONE WITH INITIALIZATION
	LHL	R1,BF,R1,
	J	NDLST3+0A,,
ENDPATCH(Fix PERR7 caused by indication of break received from host)


PATCH(830613,1700,SKW,STP30A+56,,4)
        J       STP30C			:Was  LB  R0,DPRM,,
ENDPATCH(Do not send Enter Deferred Echo message to a Tymcom)


PATCH(830629,1230,SKW,L1TBL4+18,,2)
        HC      L1PXE+6-L1TBL
CONPATCH(L1TBL4+1A,,2)
        HC      L1PXE+6-L1TBL
ENDPATCH(Stop TPAD from reporting error indication for Parameter 6)




: The following patch has been replaced by SWONG, (840426,2020)
: to ensure that TC is only sent once in the CALL CONNECTED packet.
:PATCH(830817,1640,JPM,ADDTC,,0C)
:ADDTC	TBT	RL,TCN.F,,
:	JER	R9
:	AIS	R9,4
:	JR	R9
:ENDPATCH( Always confirm throughput class if negotiation set )


PATCH(830830,1500,JPM,STP3HA+10,,4)
	LHI	R0,01
ENDPATCH( Use X.3 parameter 7 value of 01 for ISIS STP 17 00 )


PATCH(830930,1300,JPM,CON040+16,,4)
	JE	.+0AC
ENDPATCH( Do not restore X.3 parameters if no response yet )


:	The following patch was the first attempt to fix the orange ball
:	timeout problem.  The patch works but NTD prefers the method in
:	the GML patch dated 840207,1557.
:PATCH(831225,1615,SKW,TER090+52,,2)
:        LIS     R0,8            :Eight times through 2 second timer
:ENDPATCH(Extend timer to allow Orange Ball to come back via SAT link)


PATCH(840207,1557,GML,MMI.20+0A,,2)
        JFS     MMI.20+10
ENDPATCH(Fix orange ball timer if timeout occurs due to long network delay)


PATCH(840426,2020,SWONG,ADDTC,,16)
        LHL     R2,IPORT
        TBT     R2,TCRCV,,      :TEST IF DEST HOST RETURN TC
        JNR     R9              :NORMAL JUMP IF REVEIVED FROM HOST
        LB      R3,TCLTR,R2,    :GET FINAL TC VALUE
        AIS     R9,4            :SKIP RETURN AND SEND FINAL TC VALUE
        JR      R9           
ENDPATCH( RETURN TC IF DEST HOST DOES NOT SEND ONE CALL ACCEPT )


:**** end of patch area ************************************************

 PATCHREPORT                    :REPORT BYTES OF PATCH AREAS USED
 FINPATCH                       :MAKE FINAL REPORT ON SEGMENT USAGE
@	Aá