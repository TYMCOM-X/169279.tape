:
:
: PATCH FOR VERSION 3.01
:
: TASK 1.
: THIS PATCH WILL CHECK THE FLG NOTNIC.CRQ TO DECIDE WHETHER WE WANT TO
: ADD OUR DINC AS TNIC (WHEN SENDS CALL REQUEST PACKET TO THE LINK) OR NOT.
: CALL COMES FROM EITHER NON-XCOM OR XCOM. TWO PLACES ARE MODIFIED TO BE
: ABLE TO CHECK NOTNIC.F FLAG BEFORE ADDING OUR DNIC AS TNIC.
:
: TASK 2.
: CHECK THE FLAG NOTNIC.CON TO DECIDE WHETHER WE NEED TO SKIP ADDING
: OUR DINC AS TNIC WHEN WE SEND CALL CONNECT PACKET OR CLEAR REQUEST PACKET
: TO THE LINK IN RESPONSE TO THE CALL REQUEST FROM THE LINK.
:
:
: NOTNIC.CRQ & NOTNIC.CON ARE 32 LINKS FLAGS. DEFAULT VALUE FOR EACH LINK IS 0.
: (I.E., THIS PATCH WILL NOT AFFECT THOSE NEED ODNIC AS TNIC.)
:
: IF THE kth LINK REQUIRES THE FUNCTION---NOT TO ADD OUR DINC AS TNIC---
: THEN SET THE CORRESPONDING FLAG (kth BIT OF FLAG NOTNIC.CRQ OR NOTNIC.CON)
;
: FOR EXAMPLE:
:    LINK 2,3,5,8,9,12,16,17,26(decimal)NEED TO SKIP ADDING OUR DNIC AS TNIC
:    IN THE CALL REQUEST SENT TO LINK AND LINK 4,6,7 NEED TO SKIP ADDING OUR
:    DNIC AS TNIC IN CALL CONNECT AND CLEAR REQUEST SENT TO TEH LINK IN
:    RESPONSE TO CALL REQUEST FROM THE LINK, THEN ADD 'NOTNIC.CON[0B00:' AND
:    ADD TWO LINES 'NOTNIC.CRQ[34C8:' AND 'NOTNIC.CRQ+2[0C020:' IN THE COMMAND
:    RIGHT BEFORE OPEN NIB FILE AND AFTER ALL PATCH FILES.
:
: WHENEVER WE NEED TO ADD OR NOT TO ADD ODNIC AS TNIC, JUST ENTER SLOT DDT
: AND MODIFY THE MEMORY NOTNIC.CRQ OR NOTNIC.CON 4 BYTES CONTENT.

  IF X.75
:*******************************************************************
: TASK 1. CHECK THE FLAG TO SEE IF THERE IS A NEED TO SKIP ADDING
:         OUR DNIC AS TNIC IN THE CALL REQUEST SENT TO THE LINK.
:*******************************************************************

PATCH(860606,1300,JWANG,PA0PTR,,8)
NOTNIC.CRQ      WC      0       :FLAG TO SKIP ADDING OUR DINC AS TNIC
                                :DEFAULT VALUE IS 0 --- TO ADD ODNIC AS TNIC
                                :IN THE CALL REQUEST SENT TO TH LINK.
NOTNIC.CON      WC      0       :FLAG FOR CALL CONNECT AND CLEAR REQUEST
:
: CASE 1. CALL IS FROM ANOTHER XCOM.
CONPATCH(ESP957,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,1C)
        TBT     RL,NOTNIC.CRQ,,   :NOT TO ADD ODNIC AS TNIC?
        JN      ESP958,,        :YES,SKIP
        LHI     R0,UT.TNI       :NO, ADD IT
        JAL     R4,WCI,,
        J       ESP957+8,,
:
: CASE 2. CALL IS FROM NON-XCOM
:
   IF DNICND
CONPATCH(CRQ304+60,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,1C)
        TBT     RL,NOTNIC.CRQ,,   :NOT TO ADD ODNIC AS TNIC?
        JN      CRQ306,,        :YES, SKIP
        LHI     R0,UT.TNI       :NO, ADD IT
        JAL     R4,WCI,,
        J       CRQ304+68,,
   EI DNICND


:*******************************************************************
: TASK 2. CHECK THE FLGA NOTNIC.CON TO SEE IF THERE IS A NEED TO SKIP
:         ADDING OUR DNIC AS TNIC IN THE CALL CONNECT OR CLEAR REQUEST
:         PACKET SENT TO THE LINK IN RESPONSE TO THE CALL REQUEST FROM
:         THE LINK.
:**********************************************************************

CONPATCH(ADDT18,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,18)
        TBT     RL,NOTNIC.CON,,         :NEED TO SKIP?
:      -----------------------------------------------------------------
:      THE FOLLOWING INSTRUCTION IS CHANGE TO UPGRADE PATCH TO VERSION 4.03
        JN      ADDT22,,  : JN      ADDT20,,    MODIFIED ON DEC 1,1988
        LB      R4,TNICNT,R7,
        J       ADDT18+6,,
ENDPATCH(NOT ADD OUR DNIC AS TNIC IF NOTNIC.CRQ OR NOTNIC.CON SET IN CMD FILE)
 EI X.75

