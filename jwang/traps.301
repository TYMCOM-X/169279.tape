:**************************************************************
:
: PART 1.
:     SAVE THE TRACES WHEN THE FOLLOWING TRAP OCCURS: (not crash the
:     slot. the first 4 bytes in FFTRTB,PPTRTB records the trap type)
:
:        TRAP LOCATION        TRAP TYPE
:        --------------      -----------
:
:
: PART 2.
:      SAVE THE TRACES WHEN THE FOLLOWING OPTIONAL TRAP OCCURS: (flag
:      needs to be turned on in command file with '1[CASE??:')
:
:
:  CASE 00. THE CODE RECEIVES CALL RESET PACKET WITH DIAGNOSTIC=01 FROM 
:           THE LINK.
:  CASE 01. IN RTEPS RTEPS2. THE INTERFACE RECEIVES A DATA PACKET
:           FROM THE LINK AND FINFS THAT P(S) IN INVALID. THE  CODE
:           WILL RETURN CALL RESET PACKET WITH (C5,D1)-X.25 OR (C7,D1)-X.75.
:  CASE 02. IN RTEPS2. THE CODE RECEIVES A DATA PACKET FROM THE LINK AND FINDS 
:           P(S) IS NOT ALLOWED--OUT SIDE WINDOW. THE CODE WILL THEN RETURN
:           CALL RESET PACKET WITH (C5,D1)-X.25 OR (C7,D1)-X.75.
:  CASE 03. IN PSUB5, RMK TO MAKE A CALL RESET PACKET WITH DIAG=01
:  CASE 11. AT QCR ROUTINE. WE ARE GOING TO SEND CALL CLEAR TO THE LINK.
:  CASE 15. ON TYPE 11 DISCONNECT.
:  CASE 1B. AT PSUB5. THE CODE IS GOING TO SEND OUT RESET INDI.
:  CASE 2B.
:    CASE NUMBER 0000002B -- GOOD RESET REQUEST RECEIVED FROM THE LINK.
:    CASE NUMBER 0000012B -- RESET REQUEST WITH NO C/D FIELD
:    CASE NUMBER 0000022B -- RESET REQUEST TOO LONG RECEIVED FROM THE LINK.
:    CASE NUMBER 0000032B -- RESET REQ WITH CAUSE FIELD ONLY
:  CASE FB.  (CASE NUMBER 000001FB AND 000002FB)
:   (01FB): THE CODE IS GOING TO SEND 2nd RESTART INDI AFTER
:           THE SLOT IS RELOADED. WE HAVE JUST CHANGED OUR STATE TO 
:           PWRSTC. TRACE SAVED AT ROUTINE PSUBB.
:   (02FB): THE CODE IS GOING TO SEND 2nd RESTART INDI AFTER
:           THE SLOT IS RELOADED. WE HAVE ALREADY RECEIVED A RESTART
:           REQUEST. SO, WE CHANGE OUR STATE TO PREADY. TRACE
:           SAVED AT ROUTINE PSUBCA.
:  
:
:
:  CASE 43. WHEN RECEIVED DISC (43/53/A0 ON INFORMATION XFER STATE,
:           SAVE THE TRACSES.
:
:  CASE 53. AT SDSNAR. SVE THE TRACES WHEN XCOM RECEIVES DISC POLL
:           IN SECADM STATE. (NOT ARMEED STATE)

:  CASE E2. SAVE THE TRACES WHEN THE ERROR CELL SM.E2 IS INCREMENTED BY ONE.
:      (CASE NUMBER 000000E2, TRACES SAVED AT SNDSAM--- IN INFO XFER STATE,
:              'FRMR' OR 'RR/RNR/REJ WITH FINAL RESPONSE' RECEIVED.
:      (CASE NUMBER 000001E2, TRACES SAVED AT CHKS15-- CLOCK IS GONE
:              OR IDLE IN PINFO OR PTRECV STATE.
:  CASE B7. SAVE THE TRACES WHEN THE CODE FINS NO CALLED/CALLING ADDRESS
:       TO REPORT VIA ISIS B7 MESSAGE.
:       FFTRTB+0E CONTAINS OCURENCE COUNTER (2 BYTES)
:       FFTRTB+0C CONTAINS THE LENGTH OF CALLED/CALLING ADDRESS
:       FFTRTB+8 CONTAINS RETURN ADDRESS, THIS ADDRESS CAN TELL US 
:                THAT WE ARE DEALING WITH CALLED OR CALLING ADDRESS AT THIS
:                TIME.
:      FFTRTB+0 TO FFTRTB+7 ---- SEE BELOW.
:
:  CASE 23. SAVE TRACES WHEN THE CODE IS TRYING TO BUILDING AN INTERRUP
:       PACKET AT THE ROUTINE PSUB8. THIS PACKET WILL ALSO BE SAVED IN PPTRTB
:       TRACE SAVE AREA. PPTRTB+8 CONTAINS ONE BYTE OF INTERRUPT USER DATA.
:
:
:
:
:
:
: ADD '1[CASE01:' OR '1[CASE00:' OR 1[CASE02:' OR '1[CASE03:' OR '1[CASEFB:'
:   OR '1[CASE11:' OR '1[CSE53:' OR '1[CASE15:' OR '1[CASE1B:' OR '1[CASE2B:'

:   IN THE COMMAND FILE BEFORE ASSEMBLING THIS PATCH
:   TO ENABLE THE CORRESPONDING TRACE SAVE.
:************************************************************************
:
: XCOM VERSION 3.01
:
:
:****************************************************************
:
: PART 1 --- REMOVE THE CODE TRAPS. SAVE TRACES INSTAED OF CRASHING
:            SLOT.
:
:******************************************************************


:*****************************************************************
:
: PART 2 --- USER OPTIONAL  TRAP.
:            USER CAN INTEND TO CAPTURE ANY SYSPTOM BY TURN ON
:            THE FLAG (CASE NUMBER) IN THE COMMAND FILE.
:
:*****************************************************************
:
:****************************************************************
:
:
: case 01.
: To enable this tracee save, add '1[case01:' in the command file

  IF   \CASE01
PATCH(860701,1300,JWANG,RTEPS+6,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,22)
        ST      R7,SAV1
        ST      R8,SAV2
        LIS     R7,1
        JAL     R8,TRCSAV,,
        L       R7,SAV1
        L       R8,SAV2
        LHI     0D,1
        J       SNDRSE,,
ENDPATCH(SAVE TRACE WHEN RECEIVES A DATA PACKET WITH INVALID PS)
  EI    CASE01
:
: CASE 02.
:  ADD '1[CASE02:' IN THE COMMAND FILE TO ENABLE THIS TRACE SAVE
:

  IF   \CASE02
PATCH(860601,1300,JWANG,RTEPS2+6,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,22)
        ST      R7,SAV1
        ST      R8,SAV2
        LIS     R7,2
        JAL     R8,TRCSAV,,
        L       R7,SAV1
        L       R8,SAV2
        LHI     0D,1
        J       SNDRSE,,
ENDPATCH(SAVE TRACE WHEN RECEIVES A DAT PACKET WITH PS OUT OF WINDOW)
  EI    CASE02
:
: CASE 00.
:  ADD '1[CASE02' TO ENABLE THIS TRACE SAVE. (IN THE COMMAND FILE)
:
  IF   \CASE00
PATCH(860601,1300,JWANG,TRSE05,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,2E)
        STB     R0,PSDIAG,R7,R7
        CLHI    R0,1
        JN      TRSE05+6,,
        ST      R7,SAV1
        ST      R8,SAV2
        LIS     R7,0
        JAL     R8,TRCSAV,,
        L       R7,SAV1
        L       R8,SAV2
        J       TRSE05+6,,
ENDPATCH(SAVE TRACE WHEN RECEIVES RESET PACKET)
  EI    CASE00
:
: CASE 03.
:  ADD '1[CASE03:' IN THE COMMAND FILE TO ENABLE THIS TRACE SAVE.
:
  IF   \CASE03
PATCH(860601,1300,JWANG,PSUB5+18,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,34)
        LHL     R0,PSDIAG,R4,R4
        LR      R4,R0
        NHI     R4,7F
        CLHI    R4,1
        JN      PSUB5+1E,,
        ST      R7,SAV1
        ST      R8,SAV2
        LIS     R7,3
        JAL     R8,TRCSAV,,
        L       R7,SAV1
        L       R8,SAV2
        J       PSUB5+1E,,
ENDPATCH(SAVE TRACE WHEN RMKAE BUILD A RESET WITH DIAG 01)
  EI   CASE03
:
: case number 11 for call clear at the QCR routine.
:
:
  IF   \CASE11
PATCH(860601,1300,JWANG,QCR,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,28)
        ST      R7,SAV1
        ST      R8,SAV2
        LHI     R7,11
        JAL     R8,TRCSAV,,
        L       R7,SAV1
        L       R8,SAV2
        LHL     R1,IPORT
        LHI     R0,PSCLRQ
        J       QCR+8,,
ENDPATCH(SAVE PTRTBL AND FTRTBL AND IO RING UNDER SOME CONDITION)
  EI   CASE11

:
:
: CASE 13  WHEN SENDS SHG
:
   IF  \CASE13
PATCH(860903,1200,BPC,CLRF7A+2E,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,3E)
        CTLMSG(SHG)
        ST      R8,SAV1,,
        ST      R7,SAV2,,
        LHI     R7,13
        JAL     R8,TRCSAV,,
        L       R8,SAV1,,
        L       R7,SAV2,,
        J       CLRF7D,,
ENDPATCH(SAVE INFO WHEN SENDS SHG)
  EI


:
:
:     CASE 15, ADDED BY JL, ON DISCONNECT TYPE 11 MESSAGE TO ACCOOUNTING
:
   IF  \CASE15
PATCH(870117,1420,JL,DDN008+72,,6)
         J         PA1PTR,,
CONPATCH(PA1PTR,,3E)
         LHL       R7,TERMCD,,        :IN DDONE ROUTINE, CHECK TERM. CODE
         JE        DDN020,,           :...FOR AA.UNR WHICH IS 0B FOR
         CLHI      R7,AA.UNR          :...TYPE 11 DISCONNECT
         JN        DDN008+78,,
         ST        R7,SAV1,,
         ST        R8,SAV2,,
         LHI       R7,15
         JAL       R8,TRCSAV,,
         L         R7,SAV1,,
         L         R8,SAV2,,
         J         DDN008+78,,
ENDPATCH(SAVE TRACE DATA ON DISCONNECT TYPE 11)
   EI
:
:
:
:
: CASE FB.  (CASE NUMBER 000001FB AND 000002FB)
:   (01FB): THE CODE IS GOING TO SEND 2nd RESTART INDI AFTER
:           THE SLOT IS RELOADED. WE HAVE JUST CHANGED OUR STATE TO 
:           PWRSTC. TRACE SAVED AT ROUTINE PSUBB.
:   (02FB): THE CODE IS GOING TO SEND 2nd RESTART INDI AFTER
:           THE SLOT IS RELOADED. WE HAVE ALREADY RECEIVED A RESTART
:           REQUEST. SO, WE CHANGE OUR STATE TO PREADY. TRACE
:           SAVED AT ROUTINE PSUBCA.
:
:
:
  IF  \CASEFB
PATCH(861215,1000,JWANG,PA0PTR,,2)
SRSTID  HC      0
CONPATCH(PSUBC-1E,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,32)
        STB     R0,TNCNTR,RL,
        SBT     RL,SRSTID,,     :TEST IF NOT THE FIRST TIME TO SEND RESTART IND
        JE      PSUBC-18,,      : IF THE 1st TIME--IE., SLOT WAS JUST RELOADED
                                : NOT TO SAVE TRACE FOR THE 1st WE SEND RESTART
                                : INDI.
        ST      R7,SAV1
        ST      R8,SAV2
        LHI     R7,1FB
        JAL     R8,TRCSAV,,
        L       R7,SAV1
        L       R8,SAV2
        J       PSUBC-18,,
CONPATCH(PSUBCA+2,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,32)
        STB     R0,PCKSTE,RL,
        SBT     RL,SRSTID,,
        JE      PSUBCA+8,,
        ST      R7,SAV1
        ST      R8,SAV2
        LHI     R7,2FB
        JAL     R8,TRCSAV,,
        L       R7,SAV1
        L       R8,SAV2
        J       PSUBCA+8,,
ENDPATCH(SAVE TRACE WHEN THE CODE SENDS RESTART IND -EXCEPT 1ST TIME)
  EI

:
: CASE 43.
: SAVE THE TRACES WHEN THE DISC (43/53) RECEIVED FROM THE LINK ON
:  INFORMATION XFER STATE.
: SDMXXY IS IN  (SOURCE)XCOM03.P01. SDMXXY JUMPED FROM SDSINF.
: FFTRTB+0E WILL CONTAIN THE OCCURRENCE OF THIS CASE.
: FFTRTB WILL CONTAINS CASE NUMBER 00000043.
: ADD '1[CASE43: IN THE COMMAND FILE TO ENABLE THIS PATCH.
:
   IF   \CASE43
      IF  LAPB
PATCH(870805,1100,JWANG,SDMXXY,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,36)
        LIS     R5,0
        STB     R5,XPSTAT,RL,
        ST      R7,SAV1
        ST      R8,SAV2
        LHI     R7,43           :CASE NUMBER
        JAL     R8,TRCSAV,,
        LHL     R8,FFTRTB+0E,,
        AIS     R8,1
        STH     R8,FFTRTB+0E,,
        L       R7,SAV1
        L       R8,SAV2
        J       SDMXXY+6,,
ENDPATCH(SAVE TRACES WHEN DISC 43/53 RECEIVED ON  INFO XFER STATE.)
   EI
  EI
:
:
: case 53
:
: SAVE THE TRACE WHEN XCOM RECEIVES DISC POLL IN NOT ARM STATE.
: (I.E., IN SECADM STATE)
:

  IF  \CASE53
  IF   LAPB
PATCH(861223,1200,JWANG,SDSNAR,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,32)
        LIS     R5,XMSDM
        STB     R5,XSSTAT,RL,
        ST      R7,SAV1
        ST      R8,SAV2
        LHI     R7,53
        L       R8,FFTRTB,,
        CR      R7,R8
        JEFS    .+8
        JAL     R8,TRCSAV,,
        L       R7,SAV1
        L       R8,SAV2
        J       SDSNAR+6,,
ENDPATCH(SAVE TRACE WHEN DISC POLL REECEIVED)
  EI
  EI


:
:
:   CASE 1B, THE CODE IS GONING TO SEND OUT RESET INDICATION.
:            THE RESET INDI CAN BE FOUND IN THE PT TRACE.
:
  IF  \CASE1B
PATCH(870119,1200,JWANG,PSUB5B-18,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,2E)
        STB     R0,TNCNTR,R1,
        ST      R7,SAV1,,               :THE RESET INDI PACKET HAS JUST BEEN 
        ST      R8,SAV2,,               :BUILT AND ADDED IN THE PT TRACE.
        LHI     R7,1B                   :CASE NUMBER IS 0000001B
        JAL     R8,TRCSAV,,             :SAVE THE TRACES
        L       R7,SAV1,,
        L       R8,SAV2,,
        J       PSUB5B-12,,
ENDPATCH(SAVE THE TRACES AT THE TIME WE ARE SENDING RESET INDI)
  EI

:
:
:  CASE 2B, THE CODE HAS JUST RECEIVED RESET REQUEST FROM THE LINK.
:
:       CASE NUMBER 0000002B ---  GOOD RESET REQUEST RECEIVED.
:       CASE NUMBER 0000012B ---  RESET REQUEST WITH NO C/D FIELD
:       CASE NUMBER 0000022B ---  RESET REQUEST PACKET TOO LONG
:       CASE NUMBER 0000032B --- RESET WITH CAUSE FIELD ONLY
:  NOTE: PPTRTB+8 CONTAINS CAUSE FILED IF THIS EXISTS.
:        PPTRTB+9 CONTAINS DIAGNOSTIC FILED IF THIS EXISTS.
:
:
  IF  \CASE2B
PATCH(870119,1300,JWANG,RTRSE+10,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,50)
        ST      R7,SAV1,,
        ST      R8,SAV2,,
        LR      R6,R6
        JGFS    .+0A
        LHI     R7,12B          :CASE NUMBER 0000012B -- NO C/D FIELD
        J       TRSE03
        CLHI    R6,2
        JLEFS   .+0A
        LHI     R7,22B          :CASE NUM 0000022B -- LENGTH TOO LONG
        J       TRSE03
        CLHI    R6,2
        JEFS    .+0A
        LHI     R7,32B          :CASE NUM 0000032B -- RESET WITH CAUSE ONLY
        J       TRSE03
        LHI     R7,2B           :RESET REQ WITH CAUSE AND DIAGNOSTIC FIELD
TRSE03  JAL     R8,TRCSAV,,
        L       R7,SAV1,,
        L       R8,SAV2,,
        J       RTRSE+16,,
CONPATCH(TRSE05+0A,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,24)
        LB      R0,PSDIAG,R7,R7
        STB     R0,PPTRTB+8,,           :CAUSE FIELD
        LB      R0,PSDIAG+1,R7,R7
        STB     R0,PPTRTB+9,,           :DIAGNOSTIC FIELD
        TBT     RL,PTRLIN,,
        J       TRSE05+10,,
ENDPATCH(SAVE TRACES IF RESET REQUEST RECEIVED FROM THE LINK)
  EI



:
:  CASE E2.
:  SAVE THE TRACES AT THE TIME THE ERROR CELL IS INCREMENTED BY ONE.
:  THE FFTRTB+8 CONTAINS THE VALUE OF ERROR CELL 'SM.E2'.
:  TWO PLACES TO INCREMENT SM.E2 --'SNDSAM' AND 'CHKSIO'.
:
:  AT THE ROUTINE 'SNDSAM'--
:     CASE NUMBER IS 000000E2. THE SYMPTOM IS THAT WE RECEIVE 'FRMR'
:     OR 'RR/RNR/REJ RESPONSE WITH FINAL BIT ON' IN INFO XFER STATE.
:  AT THE ROUTINE 'CHKSIO'--
:     CASE NUMBER IS 000001E2. IN PINFO OR PTRECV STATE, CLOCK IS GONE
:     OR LINE IDLE.

  IF  \CASEE2
PATCH(870205,1500,JWANG,SNDSAM+2,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,3A)
        AHM     R5,SM.E2,RL,RL
        ST      R7,SAV1,,
        ST      R8,SAV2,,
        LHI     R7,00E2         :CASE NUMBER
        JAL     R8,TRCSAV,,
        L       R7,SAV1,,
        L       R8,SAV2,,
        LB      R5,SM.E2,RL,RL
        STB     R5,FFTRTB+8,,
        J       SNDSAM+8,,
CONPATCH(CHKS20-6,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,3A)
        AHM     R0,SM.E2,RL,RL
        ST      R7,SAV1,,
        ST      R8,SAV2,,
        LHI     R7,01E2         :CASE NUMBER
        JAL     R8,TRCSAV,,
        L       R7,SAV1,,
        L       R8,SAV2,,
        LB      R0,SM.E2,RL,RL
        STB     R0,FFTRTB+8,,
        J       CHKS20,,
ENDPATCH(SAVE THE TRACES WHEN THE ERROR CELL SM.E2 IS INCREMENTED BY 1)
  EI

:
:CASE B7.
: TO TURN ON THIS PORTION OF PATCH, ADD '1[CASEB7:' IN THE COMMAND FILE.
: SAVE THE IRING/ORING/FTRTBL/PTRTBL WHEN THE ACCOUNTING REPORT ROUTINE
: (ACTADR) FINDS THE CALLED OR CALLING ADDRESS IS ZERO OR GREATER THAN 
: 14 (decimal). THESE TWO CASES FORCE THE CODE NOT TO REPORT CALLED/CALLING
: ISIS 'B7' MESSAGE. ALASCOM NET IS SAYING THAT MORE THAN 10% OF THE TIME
: XCOM DOESN'T REPORT CALLED/CALLING ADDRESS. THIS PATCH IS USED TO CATCH THE 
: PROBLEM .
:
: THE FRAME TRACE STARTS AT FFTRTB+10 WITH THE LENGTH 'FTRLEN'.
: THE 10 (hexdecimal) BYTES STARTING AT FFTRTB ARE:
:   /CASE NUMBER/FTRLEN/FTRINP/CONTENT OF R13/CONTENT OF R5/OCCURING COUNTER/
:  |-->  4         2      2         4             2          2
:  |
: # OF BYTES

  IF   \CASEB7
PATCH(870217,1000,JWANG,ACTADR+10,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,54)
        LR      R5,R5
        JE      ACTSAV
        AIS     R1,1
        CLHI    R5,0E           :LENGTH GREATER THAN 14?
        JG      ACTSAV
        J       ACTADR+1C,,
ACTSAV  ST      R7,SAV1,,
        ST      R8,SAV2,,
        LHI     R7,0B7
        JAL     R8,TRCSAV,,
        LHL     R8,FFTRTB+0E,,
        AIS     R8,1            :INCREMENT OUCCURENCE COUNTER BY ONE.
        STH     R8,FFTRTB+0E,,
        L       R7,SAV1,,
        L       R8,SAV2,,
        ST      R13,FFTRTB+8,,  :SAVE THE RETURN ADDRESS
        STH     R5,FFTRTB+0C,,
        JR      R13
ENDPATCH(SAVE TRACES WHEN WE FIND NO CALLED/CALLING TO REPORT VIA ISIS B7)
  EI




:
:
: CASE 23
: ADD 1[CASE23: IN THE COMMAND FILE TO TURN ON THIS SAVE TRACE FUNCTION.
: THE TRACES WILL BE SAVED IN PATCH AREA WHEN THE CODE IS BUILDING A INTERRUPT
: PACKET IN RMAKE ROUTINE. THIS PACKET CAN BE FOUND IN THE PT TRACE--PPTRTB.
:  PPTRTB+8 CONTAINS ONE BYTE OF INTERRUPT USER DATA.

    IF   \CASE23
PATCH(870305,1400,JWANG,PSUB8A+14,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,3A)
        JAL     R4,RMKBLD,,
        ST      R7,SAV1,,
        ST      R8,SAV2,,
        LHI     R7,23           :CASE NUMBER,CAN BE FOUND AT PPTRTB,FFTRTB
        JAL     R8,TRCSAV,,
        LB      R7,PSDIAG+1,,   :INTERRUP USER DATA
        STB     R7,PPTRTB+8,,   :SAVED AT PPTRTB+8
        L       R7,SAV1,,
        L       R8,SAV2,,
        J       RMK300,,
ENDPATCH(SAVE TRACES WHEN THE CODE IS BUILDING INTERRUPT PACKET 23)
    EI


 