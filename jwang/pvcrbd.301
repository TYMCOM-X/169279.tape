:
:******************************************************************************
: PROBLEM:   XCOM VERSION 3.01.
: PVC CIRCUIT GOT LOST IN THE NETWORK AND HIGH HOST XCOM DOESN'T KNOW
: ABOUT THIS AND NOT TRY TO REBUILD PVC CIRCUIT.
: BOTH END USER CONTINUE TO SEND DATA, THIS WILL CAUSE LOTS OF PROBLEM.
:
: TASK OF THE PATCH:
:   1. BOTH XCOM SEND IIX 9C MESSAGE TO THE OTHER END TO LET HIM
:      KNOW HE IS STILL ALIVE. (SEND IIX 9C ONCE EVERY 1 MINUTES.)
:   2. BOTH END HOSTS CHECK IF HE HAS RECEIVED IIX 9C IN THE PAST
:      THREE MINUTES OR NOT. (CHECK IT ONCE EVERY 3 MINUTES.)
:      a. IF LOW HOST FIND THAT HE DOESN'T RECEIVED IIX 9C IN THE PAST
:         THREE MINUTES, HE WILL THEN CLEAN UP THE CIRCUIT AND SENDS
:         RESET TO THE LINK.
:      b. IF HIGH HOST FINDS THAT HE DOESN'T RECEIVE IIX 9C IN THE PAST
:         THREE MINUTES, HE WILL THEN SEND OUT RESET TO THE LINK AND 
:         REBUILD THE PVC CIRCUIT VIA 'PSEUDO NEEDLE REQUEST,LOGIN STRING
:         TO SUP, PPVC REQUEST....'.
:    3. USE THE BIT ARRAY TSTEPT TO DEFINE THOSE CHANNELS WHO WANT TO
:       ENABLE THIS PATCH. THIS ARRAY TSTEPT IS BASED ON EPORT NUMBER.
:       USE 'EPORT = CHANNEL -LCC.LT + EPB.LT' TO COMPUTE THE EPORT
:       NUMBER. NOTE THAT LCC.LT AND EPB.LT HAVE TWO BYTES FOR EACH LINK.
:
:***************************************************************************
:
:
   IF   PVC&HDLC&(LAP!LAPB)
PATCH(870331,1500,JWANG,PA0PTR,,2*NIGRPS+2*NEGRPS+44)
PVCREG  WS      10
PVCACT  HS      NIGRPS
PVCDIE  HC      0                       :ERROR CNTR
PVC9C   HC      0                       :CNTR FOR SENDING 9C
TSTEPT  HS      NEGRPS
:
: TSTEPT IS A TESTING EPORT FLAG. THE CORRESPONDING BIT CAN BE SET
: TO SELECT PARTICULAR EPORT (IE., PARTICULAR PVC CHNNEL) TO ENABLE
: THE EXCAHNGE PVC STATUS ACTIVITY.  BOTH END XCOM EPORTS OF THE
: SAME PVC CIRCUIT SHOULD BE CALAULATED AND THE CORRESPONDING BITS
: ON BOTH END XCOM SHOULD BE SET. EPORT = CHANNEL - LCC.LT + EPB.LT.
: (LCC.LT AND EPB.LT ARE TWO BYTES FOR EACH LINK)
: IF ALL THE FLAG BITS ON TSTEPT ARE SET TO 1 ON ALL INVOLVED PVC
: SLOTS, TWO END XCOMS WILL EXCHANGE PVC STATUS FOR 'ALL PVC' CIRCUITS.
: YOU MAY ENABLE/DISABLE ON SOME PVC CIRCUITS BY CHANGING THE FLAG TSTEPT.
:
:***************************************************************************
: PART 0. PUT THE SENDING-9C ROUTINE AND CHECKING-RCVD-9C RUTINE
:         INTO THE TIMEOUT LIST.
:         INITIAL SENDING IIX 9C TIMER IS 60 SECONDS. AFTER FIRST TIME,
:                 IIX 9C WILL BE SENT ONCE EVERY 60 SECONDS.
:         INITIAL CHECKING-RECEIVE-9C TIMER IS 180 SECONDS. AFTER FIRST TIME,
:                 THIS ROUTINE WILL BE EXECUTED ONCE EVERY 120 SECONDS.
:****************************************************************************
:
: THE TIMER LIST ONLY ALLOWS A FIXED NUMBER OF TIMER ROUTINE. HERE
: WE PUT THE TIMER ROUTINES CHKSIO AND T15S TOGETHER WITH T5S ROUTINE.
: SO, WE WILL THEN REPLACE CHKSIO BY SND9C AND REPLACE T15S BY 
: PVCST.

CONPATCH(TGB010+2,,4)
        JL      CHKSIO+0A       :CHAINED TO THE ROUTINE CHKSIO
CONPATCH(CHKS10+2,,6)
        JL      T15S+0A,,       :CHAINED TO ROUTINE T15S
CONPATCH(INI130+50,,6)          :WAS CHKSIO ROUTINE
        J       PA1PTR,,
CONPATCH(PA1PTR,,2A)
        LI      R1,RATE*$A60,R7
        LA      R0,SND9C,,
        JAL     R5,TOPUT,,
        LI      R1,RATE*$A180,R7
        LA      R0,PVCST,,
        JAL     R5,TOPUT,,
        J       INI130+5E,,
   IF   OLDTUR
CONPATCH(INI139+1C,,6)          :WAS T15S ROUTINE
        J       INI139+2A,,
  ELSE
CONPATCH(INI139+0E,,6)  
        J       INI139+1C,,
  EI
:
:*****************************************************************************
:PART 1. SEND IIX C09C MESSAGE EVERY 60 SECOND
:      DPORT/04/C09C/0000     (LAST TWO BYTES UNUSED ---FOR FUTURE USE)
:***************************************************************************

CONPATCH(PA1PTR,,0A4)
SND9C   AI      R1,RATE*$A60            :GET NEW 60 SECONDS TIMER
        JAL     R5,TOPUT,,
        STM     R0,PVCREG,,
	LHI	R8,NIGRPS*2		:LOOP ON IPORTS
SND910	SIS	R8,2
	JL	SND970
	LHL	R1,PVCCAL,R8,		:GET PVC CHANNEL FLAG
	JEBS	SND910			:NO REQUEST IN THIS GROUP, TRY NEXT
	STH	R1,TEMP			:KEEP A COPY
SND920	LHL	R1,TEMP
	JFFOH	R1,SND930		:FIND FIRST ONE BIT (INDEX IN R2)
	JBS	SND910			:NO MORE IN THIS GROUUP, TRY NEXT
SND930	RBT	R2,TEMP
	LR	R9,R8			:(IGRP-1) * 2
	SLLS	R9,3			:((IGRP-1)*2) * 8, PARTIAL CHANNEL #
	AR	R2,R9			:FORM INTERNAL PORT #
	STH	R2,IPORT,,
        LB      R5,PCKSTE,R2,
        CLHI    R5,PFLOWC               :IN FLOW CONTROL STATE?
        JN      SND920                  :IF NOT, DON'T WORRY
        LH      R5,IE.MT,R2,R2
        TBT     R5,TSTEPT,,             :THIS PVC WANTS ENABLE THIS PATCH?
        JE      SND920                  :IF NOT, DON'T WORRY, JUST SKIP
        LH      R5,ID.MT,R2,R2
        STH     R5,DPORT,,
:
:SEND IIX 9C FOR THIS PVC
:
        LHI     R12,9C                  :IIX 9C
        LIS     R7,2                    :MESSAGE LENGTH
        JAL     R9,ESCAPN,,
        LIS     R0,0
        JAL     R4,BPUTCH,,
        LIS     R0,0
        JAL     R4,BPUTCH,,
        JAL     R4,BELOR,,
        LIS     R1,1
        AHM     R1,PVC9C,,              :INCREMENT CNTR--NUM OF SENDING 9C
        J       SND920
SND970  LM      R0,PVCREG,,
        J       TORET,,
:
:****************************************************************************
:PART 2. WHEN XCOM RECEIVES IIX C09C, SET A FLAG 'PVCACT' ON.
:***************************************************************************
CONPATCH(ESC010+0E,,4)
        CLHI    R0,1C                   :GREATER THAN 1C?

CONPATCH(ESC010+18,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,46)
        CLHI    R2,1C                   :IS THIS A IIX 9C?
        JE      ESP9C,,                 :GO PROCESS IIX 9C
        LHL     R2,ESCM,R2,R2
        J       ESC010+1E,,
ESP9C   JAL     R4,GETCH,,              :FLUSH THE LAST UNUSED BYTES
        JAL     R4,GETCH,,
        JAL     R4,ELIR,,
        LHL     R4,IPORT,,
        TBT     R4,PVCCAL,,             :IS A PVC PORT?
        JE      MMFRA,,                 :IF NOT, DISCARD IT
        SBT     R4,PVCACT,,             :SET BIT ON
        J       MMFRA,,
:
:
:****************************************************************************
: PART 3. THREE MINUTES TIMER ROUTINE CHECK IF WE HAVE RECEIVED
:         IIX MESSAGE C09C IN THE PAST THREE MINUTES FROM OTHER END XCOM
:         HOST. IF YES, NORMAL RETURN. IF NOT, WE CAN EXPECT THAT THERE IS
:         SOMETHING WRONG ON THE PATH IN THE NETWORK. IN THIS CASE,
:         HIGH HOST GOES THRU DDONE AND THEN REBUILD THE CIRCUIT VIA
:         PVCBLD ROUTINE, THE LOW HOST JUST CLEAR THE CIRCUIT VIA DDONE AND 
:         IDONE. BOTH WILL SEND RESET TO THE LINK.
:**************************************************************************

CONPATCH(PA1PTR,,1E0)
PVCST 	AI	R1,RATE*$A180		:GET NEW 180 SECOND TIME
	JAL	R5,TOPUT,,		:PUT BACK ON TIMEOUT LIST
        STM     R0,PVCREG,,
	LHI	R8,NIGRPS*2		:LOOP ON IPORTS
PVCS10	SIS	R8,2
	JL	PVCS70
	LHL	R1,PVCCAL,R8,		:GET PVC CHANNEL FLAG
	JEBS	PVCS10			:NO REQUEST IN THIS GROUP, TRY NEXT
	STH	R1,TEMP			:KEEP A COPY
PVCS20	LHL	R1,TEMP
	JFFOH	R1,PVCS30		:FIND FIRST ONE BIT (INDEX IN R2)
	JBS	PVCS10			:NO MORE IN THIS GROUUP, TRY NEXT
PVCS30	RBT	R2,TEMP
	LR	R9,R8			:(IGRP-1) * 2
	SLLS	R9,3			:((IGRP-1)*2) * 8, PARTIAL CHANNEL #
	AR	R2,R9			:FORM INTERNAL PORT #
        STH     R2,IPORT,,
        LB      R5,PCKSTE,R2,
        CLHI    R5,PFLOWC               :IN FLOW CONTROL STATE?
        JN      PVCS20                  :IF NOT, DON'T WORRY
        RBT     R2,PVCACT,,
        JN      PVCS20                  :HAVE RCVD IIX C09C IN LAST 2 MINUTES
:
: HERE WE FIND THAT OTHER END DOESN'T SEND IIX C09C TO US 
: IN THE PAST THREE MINUTES. THE FOLLOWING ERROR ROUTINES ARE THEN EXECUTED.
:	IF HOST NUMBER ON THIS END IS HIGHER THEN CLEAR DPORT AND
:	 SCHDULE A PVC BUILDING REQUEST ON THE IPORT
:	ELSE /* HOST NUMBER ON THIS END IS LOWER */
:	 CLEAR DPORT, FREE IPORT
:
:  PVC INDEX ON A LINK = EPORT - EPB.LT + LCC.LT - LPC.LT
        LB      RL,IL.MT,R2,
        LIS     R1,AA.NET               :NETWORK DISCONNECT THE PATH
        STH     R1,TERMCD,,
        LR      R1,R2
:
	LH	R5,IE.MT,R2,R2	:GET EPORT
	STH	R5,EPORT,,	:SAVE EPORT FOR LATER
        TBT     R5,TSTEPT,,             :THIS PVC WANTS TO ENBALE THIS PATCH?
        JE      PVCS20                  :IF NOT, DON'T WORRY
	SH	R5,EPB.LT,RL,RL
	AH	R5,LCC.LT,RL,RL
	SH	R5,LPC.LT,RL,RL
	SLLS	R5,S.PVC
	LH	R6,PVCLNK,RL,RL	:OFFSET INTO PVCTAB FOR 1ST PVC ON THIS LINK
	LH	R7,PVCTAB,R6,R5	:GET DEST. HOST NUMBER
	LB	R5,LH.MT,RL,	:HOST ORDINAL
	CLH	R7,HOSTS,R5,R5	:WHICH IS HIGHER?
	JG	PVCS50		:JUMP IF THIS END LOWER
:
: IF LINK IS UP THEN QUEUE A RESET  PACKET (NETWORK CONGESTION) TO LINK.
:
	LB	R0,IL.MT,R1,	:GET LINK #
	TBT	R0,PACKUP	:IS PACKET LEVEL UP ON THIS LINK?
	JE	PVCS40		:NO, BRANCH.
	SBT	R1,IFLUSH	:FLUSH DATA COME FROM LINK.
	LHI	R0,RNETCG|8	:RESET CAUSE=NETWORK CONGESTION, DIAG=0
	STH	R0,PSDIAG,R1,R1	:
	LHI	R0,ZRESET	:
	JAL	R9,CNR,,	:DO RESET CLEANING UP, QUEUE UP A RESET
	LHL	R1,IPORT,,
PVCS40	LIS	R0,PWPVCR	:STATE = WAITING FOR PVC TO BE BUILT
	STB	R0,PCKSTE,R1,
	LHL	R2,ID.MT,R1,R1
	STH	R2,DPORT,,
	JAL	R9,DDONE,,	:DETACH, CLEAN UP AND FREE DPORT
	LHL	R1,IPORT,,
	SLLS	R1,2
	AHI	R1,IEDBIA	:FLUSH THE BUFFER
	JAL	R4,EMPTY,,
	LHL	R1,IPORT,,
	LIS	R0,1
	LA	R2,,R1,R1
	STH	R0,BF+IEDBIA,R2,R2	:MODIFY POINTER TO BUFFER FLAGS
	SBT	R1,PVCTMR,,	:REQUEST PVC TIMER SERVICE
	LIS	R0,2		: 20 SECONDS FROM NOW
	STB	R0,PVCTIK,R1,
        LIS     R1,1
        AHM     R1,PVCDIE,,
        J       PVCS20

:
: HOST AT THE OTHER END HAS HIGHER NUMBER, THIS END SHOULD,
:
:	SEND A RESET ON THE ILLEGAL CHANNEL (SINCE IPORT GONE)
: 	FREE UP IPORT
:  	WAIT FOR THE OTHER END TO REBUILD THE PVC
:
:   ELCI = EPORT - EPB.LT(RL2) + LCC.LT(RL2)

PVCS50	RBT	R1,PVCCAL
	LHL	R2,ID.MT,R1,R1	:GET DPORT
	STH	R2,DPORT,,
	JAL	R9,DDONE,,	:DETACH, CLEAN UP AND FREE DPORT
	LHL	R1,IPORT,,
	LB	R0,IL.MT,R1,	:GET LINK #
	TBT	R0,PACKUP	:IS PACKET LEVEL UP ON THIS LINK?
	JE	PVCS60		:NO, BRANCH.
	LR	R1,RL
	SLLS	R1,2
	AHI	R1,IECBIA	:FORM BUFFER POINTER
	LHI	R0,ZILLCN	:PUT ON CHANNEL 0
	JAL	R4,WCI,,
	LHL	R4,EPORT,,	:DERIVE THE CHANNEL
	SH	R4,EPB.LT,RL,RL	:SUBTRACT EPORT BIAS
	AH	R4,LCC.LT,RL,RL	:ADD LOWEST CONF CHANNEL
	STH	R4,ELCI	          :SAVE GROUP AND CHANNEL NUMBERS
	LB	R0,ELGN	           :GET GROUP NUMBER
	JAL	R4,WCI,,
	LB	R0,ELCN	:GET CHANNEL NUMBER
	JAL	R4,WCI,,
	LHI	R0,YREST	:REQUEST RESET PACKET BE SENT
	JAL	R4,WCI,,
	LHI	R13,RNETCG!DIA000|8 :CAUSE "NET CONG", DIAG "0"
	LR	R0,R13
	JAL	R4,WCI,,		:WRITE CAUSE
	EXBR	R0,R13
	JAL	R4,WCI,,		:WRITE DIAGNOSTIC
PVCS60	JAL	R9,IDONE,,	:FREE UP IPORT
	LHL	R1,EPORT,,	:RESET THE EPORT AVAILABLE FLAG
	RBT	R1,EPA.F	:BECAUSE IDONE CLEARED IT.
        LIS     R1,1
        AHM     R1,PVCDIE,,
        J       PVCS20
:
:
PVCS70  LM      R0,PVCREG,,
        J       TORET,,
ENDPATCH(CHECK IF THE PVC CIRCUIT IS TILL ALIVE PROIDICALLY)
ENDPATCH(IF NOT, HIGH HOST WILL REBUILD THE CIRCUIT AND)
ENDPATCH(LOW HOST WILL ZAP THE CIRCUIT)
  EI
 