:---------------------------------------------------------------------
: Patch name:  DEFUSR.SPL                     Product and Version:  CMTI 7.04
:     Author:  JEFF MORDEN                            Organization:  STS
:   Customer:  SOUTHWESTERN BELL                  Date Written:  6/7/91
: Escalation Number:  #         PIR:  #              NSR:  #
: Description of Problem:  #
:       SOUTHWESTERN BELL USES A LOGON SCHEME THAT INVOLVES 10 CHARACTER
:       USERNAMES FOR ROUTING.  SINCE THE CMT ONLY LOOKED AT THE FIRST 8
:       CHARACTERS WHEN PROCESSING AN INCOMING NEEDLE THEY COULDN'T 
:       CONFIGURE IT TO DISCRIMINATE BETWEEN ALL USERS.  THIS PATCH 
:       EXPANDS THE CMT CAPACITY TO HANDLE USERNAMES OF UP TO 12 CHARACTERS
:       IN THE DEFUSR STATEMENT.  WILDCARDING RULES ARE THE SAME AS FOR
:       THE CMT WITHOUT THIS PATCH.  
:
:---------------------------------------------------------------------

:FIRST NEED TO MAKE AN ALTERNATIVE DATA AREA TO HOLD USERNAMES
:GENNED INTO THE TYMFILE AS DEFUSR MACRO STATEMENTS.  THESE USERMANES
:ARE ACCESSED IN UNITS 1 WORD IN SIZE SO TO BE CONSISTANT AND 
:ALLOW CONTINUED USE OF SOME EXISTING INSTRUCTIONS WE WILL CREATE
:AN ALTERNATIVE USERNAME LIST IN WHICH EACH ENTRY IS 3 WORDS LONG.
:THIS IS ONE MORE WORD THAN THE ORIGINAL TABLE ENTRY SIZE.

	LO	TM
	LO	TP
	LO	TPF
	LO	DSP
	LO	LOGON
PATCH(051891,1100,J/MORDEN,PA0PTR,,NUMUNM*0F)
AUNLST  WS      0
Q       EQ      1
        RE      NUMUNM		:NUMBER OF DEFUSR USERNAMES DEFINED
        WC      80808080,80808080,80808080
QQ      EQ      .
        ORG     .-0C
        USNM|Q|			:STRING FROM DEFUSR MACRO IN TYMFILE
        ORG     QQ
Q       EQ      Q+1
        ER

:NOW NEED TO MAKE A DATA AREA ALTERNATIVE TO THOSE IN THE DCB'S USED
:TO STORE THE USERNAME THAT ARRIVES IN THE NEEDLE.  THOUGH IT IS NOT
:EFFICIENT IN REGARDS TO SPACE UNTILIZATION IT SEEMS BEST TO USE
:MAKE A STORAGE AREA INDEXED BY THE TERMINAL PORT NUMBER CONTAINED IN 
:THE DCB.

CONPATCH(PA0PTR,,NPORT*0C+3)
BGUNAM	WS	0
ATUNAM	BS	(NPORT*0C)+3

:NOW NEED TO MODIFY INSTRUCTIONS TO PROPERLY HANDLE THE USERNAME FIELD 
:THAT HAS BEEN EXPANDED TO 12 CHARACTERS.  THIS NEXT PART DEALS WITH
:THE SAVING OF THE USERNAME AS IT ARRIVES IN THE NEEDLE.

	IF	PRTSW
CONPATCH(EXTR+2C,,2)
	ELSE
CONPATCH(EXTR+2A,,2)
	EI

	LCS	R6,0C		:BRING IN UP TO 12 CHARACTERS OF USERNAME

	IF	CUDNAM
CONPATCH(ACTN15-8,,6)
	ELSE
CONPATCH(ACTN20-8,,6)
	EI
	J	PA1PTR,,
CONPATCH(PA1PTR,,22)
	LHL	R4,TPORT,R11,	:GET TERMINAL PORT NUMBER OF THIS CCB
	ST	R5,MULR5,,	:SAVE FOR WORK
	LIS	R5,0C		:BUILDING INDEX INTO NEW USERNAME TABLE
	MHR	R4,R5
	STB	R0,ATUNAM+0C,R4,R6	:PUT IN ALTERNATE UNAME SAVE AREA
	L	R5,MULR5,,	:RESTORE R5
	
	IF	CUDNAM
	J	ACTN15-2,,
	ELSE
	J	ACTN20-2,,
	EI
CONPATCH(PA0PTR,,0B)
MULR4	WS	1
MULR5	WS	1
CONPATCH(ACTN40,,6)		:PAD USERNAME WITH 80'S UP TO 12 CHARACTERS
	J	PA1PTR,,
CONPATCH(PA1PTR,,2E)
	ST	R4,MULR4,,	:SAVE FOR WORK
	LHL	R4,TPORT,R11,	:GET TERMINAL PORT NUMBER OF THIS CCB
	ST	R5,MULR5,,	:SAVE FOR WORK
	LIS	R5,0C		:BUILDING INDEX INTO NEW USERNAME TABLE
	MHR	R4,R5
	STB	R0,ATUNAM+0C,R4,R6	:PUT IN ALTERNATE UNAME SAVE AREA
	L	R5,MULR5,,	:RESTORE R5
	L	R4,MULR4,,	:RESTORE R4
	J	ACTN40+6,,

:NOW MODIFY THE PROCESS THAT USES THE USERNAME TO ASSIGN A USER PROFILE FROM
:THE USER PROFILE TABLE

CONPATCH(FNDUPF,,4)
	LHI	R9,NUMUNM*0C-0C		:LOOP CONTROL
CONPATCH(FNU000,,8)
	LCS	R8,0C		:SET LOOP TO 12 CHARACTERS
	LB	R0,AUNLST+0C,R9,R8	:ADJUST FOR 12 CHARACTER NAME
CONPATCH(FNU010+0C,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,42)
	ST	R4,MULR4,,	:SAVE FOR WORK
	LHL	R4,TPORT,R11,	:GET TERMINAL PORT NUMBER OF THIS CCB
	ST	R5,MULR5,,	:SAVE FOR WORK
	LIS	R5,0C		:BUILDING INDEX INTO NEW USERNAME TABLE
	MHR	R4,R5
	CLB	R0,ATUNAM+0C,R4,R8	:TEST FOR A MATCH
	JNFS	ANU030		:NO MATCH - SKIP THIS NAME
	L	R5,MULR5,,
	L	R4,MULR4,,
	J	FNU020,,	:GO ON TO NEXT CHARACTER IN USERNAME
ANU030	L	R5,MULR5,,
	L	R4,MULR4,,
	J	FNU030,,	:ON TO NEXT USERNAME

CONPATCH(FNU030+2,,2)
	SIS	R9,0C		:MODIFY TABLE INDEX TO ACCOUNT FOR 12 CHAR

	IF	IATFLG
CONPATCH(FNU900+6,,6)
	ELSE
CONPATCH(FNU900+2,,6)
	EI
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	LR	R8,R9		:NEED TO CONVERT INDEX IN R9 BASED ON 12
	LIS	R9,0C		:CHARACTER FIELD SIZE TO ONE BASED ON FIELD
	DHR	R8,R9		:SIZE OF 4 CHARACTERS 
	LIS	R8,4
	MHR	R9,R8
	L	R0,UPFLST,R9,
	JR	R4		:BACK TO SOURCE

:NOW NEED TO MODIFY EXTENDED DDT ROUTINE THAT PRINTS LIST OF TYMFILE USERNAMES

	LO	COM
CONPATCH(NAMES+0C,,4)
	LHI	R9,NUMUNM*0C-0C
CONPATCH(NXNAM+4,,8)
	LCS	R8,0C
	LB	R0,AUNLST+0C,R9,R8
CONPATCH(OKCHAR+8,,2)
	SIS	R9,0C

:NOW NEED TO ADJUST COM ROUTINE THAT GETS THE USERNAME ARGUMENT TO MENU
:COMMAND AND STORES IT SO IT CAN BE COMPARED WITH THE LIST OF TYMFILE
:USERNAMES AND DISPLAY LOGON STRINGS.

CONPATCH(PA0PTR,,0F)
ASTOKN	WS	3		:NEED TO PROVIDE FOR 12 CHARACTER USERNAME

CONPATCH(TOKEN0+2,,6)
	LA	R3,ASTOKN,,	:USE ALTERNATE TOKEN BUFFER
CONPATCH(TOKEN0+0C,,4)
        J       PA1PTR
CONPATCH(PA1PTR,,0C)
        ST      R0,4,R3
        ST      R0,8,R3
        J       TOKEN1
CONPATCH(TOKEN2+22,,4)
	CHI	R4,0C		:ALLOW LOOP TO GO TO 12 CHARACTERS
CONPATCH(TOKEN4+6,,4)
	CHI	R4,0C		:MORE CONTROL
CONPATCH(TOKEN3+6,,0C)
	L	R3,ASTOKN,,
	L	R2,ASTOKN+4,,

:NOW MODIFY PROCESSES THAT COMPARE THE USERNAME OBTAINED AS THE COMMAND
:ARGUMENT ABOVE WITH THE LIST OF THE TYMFILE USERNAMES

CONPATCH(NM00-4,,4)
	LHI	R9,NUMUNM*0C-0C
CONPATCH(NM1,,6)
	LB	R0,AUNLST+4,R9,R8
CONPATCH(NM3,,2)
	SIS	R9,0C
CONPATCH(FNDNM1,,6)
	LB	R0,AUNLST+8,R9,R8
CONPATCH(FNDNM3,,2)
	SIS	R9,0C
CONPATCH(FNDIT,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,66)
	L	R2,ASTOKN+8,,
	OI	R2,80808080
FNDNA0	LCS	R8,4
FNDNA1	LB	R0,AUNLST+0C,R9,R8
	THI	R0,80			:WILDCARD?
	JEFS	FNDNA2			:AUTO MATCH
	RLL	R2,8
	LBR	R1,R2
	CLR	R0,R1			:MATCH?
	JNFS	FNDNA3
FNDNA2	AIS	R8,1			:NEXT CHAR
	JLBS	FNDNA1
	J	AFNDIT			:MATCHED NAME
FNDNA3	SIS	R9,0C			:GO TO NEXT NAME
	JGE	NM00
FNDBAM	PRINT(NOUSR)			:INVALID USERNAME 
	J	DSTART
					:NEED TO USE FOR WORK OF CONVERTING
AFNDIT	ST	R10,SR10CM,,		:TO AN INDEX BASED ON AN 8 CHARACTER 
	ST	R11,SR11CM,,		:USERNAME FIELD
	LIS	R11,0C			:NEW USERNAME FIELD SIZE
	DHR	R9,R11			:FIND THE NUMBER OF USERNAME WE ARE ON
	LR	R9,R10
	LIS	R10,4			:AND CONVERT TO INDEX BASED ON 8 CHAR
	MHR	R9,R10
	L	R10,SR10CM,,
	L	R11,SR11CM,,
	L	R1,UPFLST,R9,
	J	FNDIT+8,,

CONPATCH(PA0PTR,,0E)
SR10CM	WS	1
SR11CM	WS	1

:NOW MODIFY THE COM ROUTINE THAT PROCESSES THE COMMAND "USER"

CONPATCH(CMPNM1+0A,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,46)
	SRLS	R1,2			:RESTORE PORT NUMBER IN R1
	LIS	R6,0C			:SIZE OF USERNAME STORAGE ELEMENT
	MHR	R1,R6			:MAKE THE INDEX
	STH	R1,SVPTNU		:SAVE PORT BASED INDEX FOR LATER USE
	L	R6,ATUNAM,R1,		:FIND THE USERNAME
	CR	R8,R6			:FIRST FOUR CHARACTERS
	JN	DOAGN3,,
	L	R6,ATUNAM+4,R1,		:NEXT 4 CHARACTERS
	CR	R7,R6
	JN	DOAGN3,,
	L	R7,ASTOKN+8,,		:GET LAST 4 CHARACTERS
        OI      R7,80808080             :SET ON WILDCARD
	L	R6,ATUNAM+8,R1,
	CR	R7,R6
	JN	DOAGN3,,
	J	CMPNM1+1E,,
CONPATCH(PA0PTR,,2)
SVPTNU	HS	1

:NOW MODIFY ROUTINE THAT OUTPUTS INFORMATION ABOUT THE USERNAME WE JUST FOUND

CONPATCH(TRM1-2,,2)
	LCS	R3,0C			:LOOP CONTROL FOR 12 CHARACTERS
CONPATCH(TRM1,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	LHL	R4,SVPTNU,,		:GET INDEX TO USERNAME
	LB	R0,ATUNAM+0C,R4,R3	:GET A BYTE OF USERNAME
	J	TRM1+6,,

:NOW MODIFY THE COM ROUTINE THAT PROCESSES THE "PORTS" COMMAND

CONPATCH(PRTNAM,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,0E)
	SRLS	R1,2			:RESTORE PORT NUMBER
	LIS	R3,0C			:SIZE OF USERNAME STORAGE ELEMENT
	MHR	R1,R3			:MAKE THE INDEX FOR THE NEW TABLE
	STH	R1,SVPTNU		:SAVE THE NEW INDEX FOR LATER USE
	ST	R0,PSAV1,,		:FROM SOURCE
	ST	R2,PSAV2,,		:
	J	PRTNAM+8,,
CONPATCH(PRTN1-2,,2)
	LCS	R3,0C			:ADJUST LOOP FOR 12 CHARACTERS
CONPATCH(PRTN1,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,12)
	LHL	R9,SVPTNU,,
	LB	R0,ATUNAM+0C,R9,R3	:GET A BYTE OF USERNAME FROM NEW TABLE
	J	PRTN1+6,,

	FO	COM	

ENDPATCH(TEST TO EXPAND DEFUSR TO 12 CHARACTERS)
	
	FO	DSP
	FO	TM
	FO	TP
	FO	TPF
	FO	LOGON
  