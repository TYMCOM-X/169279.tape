:
:FOLLOWING IS THE BODY OF MANY PATCHES THAT NORMALLY WOULD BE PART OF
:THE SEGMENT 1 PATCH AREA.  HOWEVER, DUE TO THE LIMITIED SPACE AVAILABLE IN
:THE SEGMENT 1 PATCH AREA THE EXECUTABLE PORTION OF THESE PATCHES HAS BEEN
:MOVED INTO UNUSED SEGMENT NINE AND JUMPED TO FROM THE SEGMENT ONE PATCH
:AREA.  DEFINING THIS CODE IN SEGMENT NINE REQUIRES APPROPRIATE MODIFICATION
:OF CONTROL BITS IN SEGMENT E AND MODIFICATION OF THE VERSION 10.04 COMMAND
:FILE SO THAT THE SEGMENT SIZE IS DEFINED AT THE END OF GEN.
:THIS CODE IS DEFINED BEFORE THE PATCHES SO THAT ITS LABELS WILL BE KNOWN
:TO NAD WHEN IT IS TIME TO ASSEMBLE THE PATCHES.
:


PATCH(012890,1100,J/MORDEN,0E0042+8,,4)
        HC      605             :MAKES SEG 9 EXECUTABLE
CONPATCH(0E002C,,4)
        WC      500             :SIZE OF SEGMENT 9 PATCH AREA


ENDPATCH(ALLOW SEGMENT 9 TO BE USED FOR PATCH CODE)


        ORG     90000

NUM1PA  PUSH(R5)
        PUSH(R0)                :WORK WITH R0
        LH      R0,CP.ON,,        :IS TRACE ENABLED?
        JL      TNO             :TRACE NOT ON
        LH      R0,CP.LN,,        :LINE OR SLOT TRACE?
        JLFS    ATCT            :IF SLOT THEN ADD TO COUNT OF DATA CHARACTERS
        LHL     R5,DDEV,RDCB,   :GET DEVICE NUMBER
        CLB     R0,DEVLN,R5,    :IS THIS THE LINE BEING TRACED?
        JNFS    TNO             :NOT TRACING THIS LINE THEN LEAVE
ATCT    AM      RPKT,CP.CNT,,   :ADD DATA CHARACTERS TO TOTAL
TNO     POP(R0)
        POP(R5)
        LHL     R0,DIR0,RDCB,
        J       DSPQ20+6,,      :BACK TO ROUTINE DISPATCHING DSP MESSAGES

NUM2PA  PUSH(R0)
        PUSH(R5)
        LH      R0,CP.ON,,      :CPS TRACE ON?
        JL      TNO1,,            :TRACE NOT ON
        LH      R0,CP.LN,,      :LINE OR SLOT TRACE?
        JLFS    ATCT1           :IF SLOT THEN ADD TO COUNT OF DATA CHARACTERS
        LHL     R5,DDEV,RDCB,   :GET DEVICE NUMBER
        CLB     R0,DEVLN,R5,    :IS THIS THE LINE BEING TRACED?
        JN      TNO1,,            :IF NOT THEN LEAVE
ATCT1   LHL     R4,DOR0,RDCB,   :GET OUTPUT BLOCK CONTROL BITS
        THI     R4,FIRBIT       :IS THIS THE FIRST BLOCK?
        JE      NADC            :IF NOT THEN DON'T ADJUST DATA COUNT
        LR      R0,R2           :IF FIRST THEN SUBTRACE 1 FOR THE STX NOT SENT
        SIS     R0,1
        AM      R0,CP.CNT,,
        J       TNO1,,
NADC    AM      RCNT,CP.CNT,,
TNO1    POP(R5)
        POP(R0)
        LHL     R4,DOR0,RDCB,
        THI     R4,FIRBIT               :SET UP CONDITION CODE FOR RETURN
        J       OND10-6,,

NUM3PA  PUSH(R0)
        PUSH(R5)
        LH      R0,CP.ON,,
        JL      TNO2
        LH      R0,CP.LN,,
        JLFS    ATCT2
        LHL     R5,DDEV,RDCB,
        CLB     R0,DEVLN,R5,
        JNFS    TNO2
ATCT2   AM      RCNT,CP.CNT,,
TNO2    POP(R5)
        POP(R0)
        LHL     R1,DPORT,RDCB,
        J       OND110+8,,

        LO      BSCRCV
        LO      BSCCHR
        LO      BSCRRC
        LO      BSCEXC

NUM4PA  LB      RC,0,R15,R14    :GET A CHARACTER FROM THE INPUT SECTOR
        S       R3,R3SV,,         :SAVE R3 FOR WORK
        LH      R3,NISETX,RL2,
        JN      INSEXT
        LA      R3,RSNDAT-RSEG,,        :ADDRESS OF NON-TRANS DATA TABLE
        CR      R3,RST          :ARE WE LOOKING AT NON-TRANSPARENT DATA
        JE      INDM            :GO TO IN NORMAL DATA MODE
        LA      R3,RSTDAT-RSEG,,         :ADDRESS OF TRANSPARENT DATA TABLE
        CR      R3,RST          :ARE WE PROCESSING TRANSPARENT DATA
        JE      ITDM            :GO TO IN TRANSPARENT DATA STATE
PEXIT3  L       R3,R3SV,,         :RESTORE R3
        J       RSTP10+6,,      :BACK TO SOURCE
INDM   CHI     RC,0FF          :IS CHARACTER AN FF PAD
        JN      NDNFF           :NORMAL DATA AND NOT AN FF PAD
        LH      R3,NUMFF,RL2,    :GET NUMBER OF CONSECUTIVE PADS SO FAR
        AIS     R3,1
        STH     R3,NUMFF,RL2,    :STORE NUMBER OF CONSECUTIVE PADS
        CHI     R3,$A50           :SHOULD NOT EXCEED $A50 HEX PADS
        JG      RECOVN          :RECOVER FROM PROBLEM CONDITION
        J       PEXIT3          :OR LEAVE PATCH
NDNFF   LIS     R3,0            :IF NOT A PAD CHARACTER
        STH     R3,NUMFF,RL2,   :RESET PAD COUNTER FOR LINE
        J       PEXIT3
ITDM   CHI     RC,0FF          :IS CHARACTER A PAD
        JN      TDNFF           :TRANSPARENT DATA AND NOT A PAD
        LH     R3,NUMFF,RL2,    :NUMBER OF PADS SO FAR ON THIS LINE
        AIS     R3,1
        STH     R3,NUMFF,RL2,
        CHI     R3,$A2500       :HOW MANY PADS SO FAR
        JG      RECOVT          :IF EXCEEDED THEN GO RECOVER FROM EVENT

        J       PEXIT3
TDNFF   LIS     R3,0
        STH     R3,NUMFF,RL2,    :RESET PAD COUNTER
        J       PEXIT3          :LEAVE PATCH
RECOVN  LHI     RC,0C0          :RECOVER BY FORCING MESSAGE TERMINATION
        LIS     R3,0            :RESET PAD COUNTER TO ZERO
        STH     R3,NUMFF,RL2,
        J       PEXIT3          :LEAVE PATCH
RECOVT  LIS     RC,8            :INSERT A DLE TO START FORCING MESSAGE TERMINAT
        LIS     R3,1
        STH     R3,NISETX,RL2,
        J       PEXIT3
INSEXT  LI     RC,0C0          :INSERT AN ETX
        LIS     R3,0
        STH     R3,NUMFF,RL2,    :RESET PAD COUNTER
        STH     R3,NISETX,RL2,
        J       PEXIT3
        FO      BSCRCV
        FO      BSCEXC
        FO      BSCCHR
        FO      BSCRRC

NUM5PA  LHL     R3,DIFBA,RDCB,
        JE      DOZA20,,
        CH      R3,DIRBA,RDCB,
        JE      DOZA10+2E,,     :IF FIRST BUFFER OF MESSAGE JUST FREE BUFFER
        PUSH(RBX)
        PUSH(RBA)
        LHL     RBA,DIRBA,RDCB,
        JN      DBUFFX                  :BUFFER REPRESENTS DATA FROM TIF
DNAX    POP(RBA)
        POP(RBX)
	LIS	R5,0			:RESET DIRBA TO 0
	STH	R5,DIRBA,RDCB,		:IF IT'S NOT CLEARED IN LAST SESSION
        LHL     RBA,DIFBA,RDCB,         :ELSE LOGON MODE - BUFFER HOLDS NEEDLE
        J       DOZA10+2E,,             :RETURN BUFFER AND CONTINUE
DBUFFX	LH	RBX,DDEV,RDCB,		:IS DEVICE ASSIGNED OK?
	JL	DNAX			:NO, RESET DIRBA & EXIT
	LH      RBX,DIRBX,RDCB,
        PUSH(R5)
        PUSH(RDEV)
        LB      RDEV,DLSEQ,RDCB,
        LHI     R0,0B4
        JAL     R5,PBCXMT,,
        JAL     R5,PBEXMT,,
        POP(RDEV)
        LHL     R5,DDEV,RDCB,
        LIS     R0,0
        STH     R0,DIFBA,RDCB,
        STH     R0,DIRBA,RDCB,
        STB     R0,DIAID,RDCB,
        LHL     R0,DIR0,RDCB,
        OHI     R0,ENQBIT
        STH     R0,DIR0,RDCB,
        PUTCMD(XMT,R5,R5)
        RLCMD(XMT,R5,R5)
        LIS     RBA,0
        LHL     R5,DPORT,RDCB,
        RBT     R5,ILOCK,,
        RBT     R5,PLOCK,,
        POP(R5)
        POP(RBA)
        POP(RBX)
        J       DOZA20,,

NUM6PA  AIS     R0,1                    :DO NOT DECREMENT INPUT SEQ NUMBER
        STB     R0,DISEQ,RDCB,          :FOR MESSAGES ABORTED BY MEANS OF
                                        :DSP RESPONSE ABORTED MESSAGE
        PUSH(R2)                        :SAVE REGISTERS FOR WORK
        PUSH(R5)
        LHI     R2,EOTCMD               :GET EOT CODE
        LHL     R5,DDEV,RDCB,           :MAKE SURE WE HAVE DEVICE NUMBER
        PUTCMD(XMT,R5,R5)               :PUT COMMAND ON XMT Q
        RLCMD(XMT,R5,R5)                :AND RELEASE IT TO FOREGROUND
        LHI     R3,0C4C4                :SETUP OP CHECK STATUS FOR HOST
        LHI     R2,STACMD               :GET STATUS COMMAND FOR CMDLK
        PUTCMD(XMT,R5,R5)
        RLCMD(XMT,R5,R5)
        LHI     R5,ENQBIT               :GET ENQ CONTROL BITS AND SAVE AS
        STH     R5,DIR0,RDCB,           :LAST MESSAGE TERMINATOR
        POP(R5)
        POP(R2)                         :RESTORE WORK REGISTERS
        JALR    RSL,RSL                 :GET ERROR CODE FROM ABORT MESSAGE
        PUSH(R0)
        AIS     R2,2                    :MAKE MESSAGE TYPE A 12 OR 13
        SLLS    R2,8    
        OR      R2,R0                   :COMBINE MESSAGE TYPE WITH ERROR CODE
        LR      R3,R2                   :AND PASS CODE TO SUP
        JAL     R5,SUPLOG,,
        LIS     RBA,0                   :SET UP TO END PROPER
        POP(R0)
        CHI     R0,5
        JE      SNDFAC,,                :IF ERROR CAUSED BY CRC ERROR AT PBT
        J       FSHIIX,,                :EXIT FROM THIS PROCESS BY CLEANING UP
S9SIZE  EQ      .-SEG9                  :HOW BIG IS SEGMENT 9
                                        :INPUT MESSAGE WITHOUT ZAPPING CIRCUIT
:
: PATCH FOR 3270 HOST INTERFACE VERSION 10.04
:

:  THIS PATCH MODIFIES THE RESULTS OF THE BOM ECPS COMMAND SO THAT ONLY
:  3270 DATA WILL BE COUNTED AS IT IS TAKEN FROM OR PLACED IN THE
:  DISPATCHER RINGS.  IT WAS PREPARED AS A RESULT OF SOUTHWESTERN
:  BELL'S EXPRESSED CONCERNS OVER THE DIFFERENCES BETWEEN THE CHARACTER
:  COUNTS REPORTED BY THE HOST AND TERMINAL INTERFACES WHEN COUNTING
:  THE SAME DATA.  WITH THIS PATCH, ONLY NATIVE MODE(FROM 3270 TERMINAL
:  INTERFACE OR CMT) WILL BE COUNTED.  VIRTUAL HOST MODE DATA WILL
:  NOT BE COUNTED.  TO MAINTAIN CONSISTANCY, 3270 TERMINAL INTERFACES
:  WHICH COMMUNICATE WITH THIS INTERFACE SHOULD INCLUDE THE PATCH
:  PBT09.XXX.  
PATCH(870831,1100,J/MORDEN,DATIIX+20,,6)        :CHARACTERS OF INPUT RING
        J       DATI08+6        :JUMP OVER TEST FOR TRACE
CONPATCH(DSPQ20,,6)                     :COUNT CHARS FROM IRING HERE IF TRACE
        J       PA1PTR,,
CONPATCH(PA1PTR,,6)
        J       NUM1PA,,
CONPATCH(OSCA05+6,,6)           :NOW ADJUST COUNT OF CHARACTERS TO ORING
        J       OSCA14,,        :JUMP OVER COUNTING IN OUTPUT CMD QUE SCANNER
CONPATCH(OND10-10,,6)            :COUNT DATA FOR NON-READ MODIFIED COMMANDS
        J       PA1PTR,,
CONPATCH(PA1PTR,,6)
        J       NUM2PA,,
CONPATCH(OND110+2,,6)           :ADDS CHARACTERS IN SUBSEQUENT PARTIAL DATA
                                :BLOCKS WHEN EDF OCCURING.
        J       PA1PTR,,
CONPATCH(PA1PTR,,6)
        J       NUM3PA,,
ENDPATCH(NSR#1639 ADJUST COUNT OF CHARACTERS WHEN USING BOM ECPS COMMAND)

:  PATCH IS DESIGNED TO CORRECT A PROBLEM CONDITION IN WHICH THE PBH
:  BEGINS TO READ PAD CHARACTERS FROM THE LINE AS IF THEY WERE DATA.
:  THIS IMPLIES THAT A MESSAGE TERMINATOR(ETB,ETX) WAS NOT RECIEVED
:  BY THE INTERFACE.  THE RESULT IS THAT A MASSIVE AMOUNT OF THESE FF's
:  ARE SENT TO THE REMOTE INTERFACE CAUSING THE BUFFER USAGE FOR CONTAINING
:  THESE CHARACTERS TO EXCEED MAXIMUM ALLOWED LIMITS.  THIS PATCH WILL WATCH
:  FOR THE CONDITION AND CORRECT IT BY INSERTING AN ETX IN THE DATA STREAM
:  TO FORCE A MESSAGE TERMINATION.  THIS CAUSES A FORWARD ABORT AND A
:  SUBSEQUENT RETRANSMISSION FOR PROPER RECOVERY.


        LO      BSCRCV
        LO      BSCCHR
        LO      BSCRRC
        LO      BSCEXC
PATCH(871102,1450,J/MORDEN,PA0PTR,,30)
NISETX    HS      NBILIN
R3SV    WS      1


NUMFF   HS      NBILIN

CONPATCH(RSTP10,,6)           :NOW WATCH FOR THE CONDITION
        J       PA1PTR,,
CONPATCH(PA1PTR,,6)
        J       NUM4PA,,
ENDPATCH(NSR#1640 PATCH FOR FIXING PROBLEMS WITH PAD CHARACTERS IN DATA MODE)
        FO      BSCRCV
        FO      BSCEXC
        FO      BSCCHR
        FO      BSCRRC
:  THIS PATCH CORRECTS PROBLEMS WITH THE RECEPTION OF FORWARD ABORTED
:  EDF DATA WHEN IN NATIVE MODE OPERATION.

        LO      FRNTND
        LO      BSCCHR
PATCH(871221,1130,J/MORDEN,DSPQ07+10,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,20)
        NHI     R0,@ETBBIT      :FROM SOURCE - TURN OFF ETB BIT
        NHI     R0,@ENQBIT      :CORRECTION - ALSO TURN OFF ENQBIT
        STH     R0,DIR0,RDCB,   :FROM SOURCE
        J       DSPQ07+1A,,
CONPATCH(DSPQ11-46,,4)
        THI     R0,ETXBIT!ENQBIT
ENDPATCH(NSR#1641 INSURE PROPER RECOGNITION OF RETRANSMITTED ABORTED DSP PACKETS)
        FO      FRNTND
        FO      BSCCHR
:  PBH'S HAVE BEEN CRASHING DUE TO THE INTERFACE BEING OUT OF COMMAND LINKS
:  OR BUFFERS.  THIS CONDITION CAN OCCURE ON HEAVILY LOADED INTERFACES IN
:  WHICH THERE HAS POSSIBLY BEEN SOME BACKPRESSURE FROM THE NETWORK OR
:  MAYBE SOME DELAYS ON THE HOST LINE.  THERE IS A SPACE MANAGEMENT ROUTINE 
:  THAT IS SUPPOSED TO WATCH FOR CRITICALLY LOW RESOURCES AND ZAP THE USER OF 
:  THE GREATEST AMOUNT OF RESOUCE AND RETURN THEM TO THE FREE POOL RATHER 
:  THAN CRASH THE SLOT.  PROBLEM WAS THAT THE ROUTINE ONLY RAN ABOUT EVERY
:  .78 SECONDS AND RESOURCES WERE BEING EXHAUSTED BETWEEN CHECKS.  THIS PATCH
:  WATCHES FOR RESOURCES TO DROP BELOW A GIVEN LEVEL AND THEN INCREASES THE
:  FREQUENCY OF CHECKS.  IT ALSO SLIGHTLY RAISES THE BOTTOM THRESHOLDS OF
:  BUFFER AND COMMAND LINK AVAILABILITY TO ALLOW A LITTLE MORE ROOM TO
:  RESPOND TO A RESOURCE SHORTAGE.
        LO      FRNTND
PATCH(871230,1100,J/MORDEN,SPAMAN,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,40)
        L       R1,FASTC,,
        LHL     R0,CMDCNT,,
        CI      R0,30           :IS NUMBER OF COMMAND LINKS LEFT LESS THAN 30
        JG      SPAMAN+6,,      :N0 - SET USUAL INCRIMENT FOR TIME OF NEXT RUN
        LHL     R0,BUFCNT,,     :GET NUMBER OF AVAILABLE BUFFERS
        CI      R0,25           :IS IT LESS THEN 25
        JG      SPAMAN+6,,
        AI      R1,250           :YES - NEXT RUN OF SPAMAN IN .38 SECONDS
        J       SPAMAN+0C,,     :RETURN TO PUT ON TIMEOUT LIST
CONPATCH(SPAMAN+1A,,6)          :RAISE CRITICAL THRESHOLD FOR COMMAND LINKS
        CI      RT1,15
CONPATCH(SPAM10+4,,6)           :RAISE THRESHOLD FOR BUFFER COUNT
        CI      RT1,15
ENDPATCH(NSR#1643 PREVENTS CRASHING DUE TO SUDDEN DROP IN NUMBER OF AVAILABLE CMD LINKS)
        FO      FRNTND
PATCH(880122,1100,J/MORDEN,ASSN87+18,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,16)
        ST      R0,LEMPTR,,
        LCS     RDEV,1
        STH     RDEV,DADEV,RDCB,
        J       ASSN90+8,,
CONPATCH(ASSN90,,6)
        J       ASSN90+8,,
CONPATCH(ASSN74+46,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,14)
        SBT     R3,PRTWAT,,
        STH     R3,DADEV,RDCB,
        J       ASSN76,,
ENDPATCH(NSR#1644 ALLOW PROPER ASSIGNMENT OF ASSOCIATED PRINTER IN VHM)
        LO      BSCPTP
:  PATCH FOR FUJITSU MODEL K SERIES HOST.  THIS MODEL OF HOST
:  IS DESIGNED TO RESPOND TO A FORWARD ABORT FROM THE PBH WITH AN EOT
:  RATHER THAN A NAK.  THE STATE TABLE WAS NOT DESIGNED TO EXPECT THAT
:  TYPE OF RESPONSE(IBM RESPONDS WITH A NAK) AND CRASHED WHEN TRYING TO
:  PROCESS THE EOT.  THIS PATCH WILL CAUSE THE PBH TO RESPOND TO THE
:  EOT THE SAME AS IF IT RECEIVED A NAK.  

PATCH(050988,1100,A/LO,SENQ+6,,2)
        HC      FUJEOT-PSEG
CONPATCH(PA1PTR,,14)
FUJEOT
	LB	R5,LNTYPE,RL	: GET LINE TYPE
	CLHI	R5,FUJITSU	: IF NOT FUJITSU HOST
	JN	PCRASH,,	: 	CRASH DUE TO UNEXECTED EOT RECEIVED
	J	XSQNAK,,	: ELSE PROCESS EOT AS NAK
ENDPATCH(NSR#1645 ALLOW EOT RESPONSE TO FORWARD ABORT FOR FUJITSU MODEL K HOST)
        FO      BSCPTP

:---------------------------------------------------------------------
: Patch name:  TRNTBL.104                     Product and Version:  PBH 10.04
:     Author:  JEFF MORDEN                            Organization:  STS
:   Customer:  SECURITY PACIFIC                         Date Written:  1/20/89
: Description of Problem:  #
:
:---------------------------------------------------------------------

: THIS PATCH IS PROVIDED FOR SECURITY PACIFIC AUTOMATION TO FIX ERRORS
: IN THE EBCDIC TO ASCII AND ASCII TO EBCDIC TRANSLATION TABLES USED IN
: VIRTUAL HOST MODE.

PATCH(871222,1100,J/MORDEN,EBCASC+79,,2)
        BC      60,3A
CONPATCH(ASCEBC+60,,2)
        BC      79,81
CONPATCH(ASCEBC+7E,,2)
        BC      0A1,5F
ENDPATCH(CORRECT ERRORS IN TRANSLATION TABLES)
:---------------------------------------------------------------------
: Patch name:  XPRFAB.104                     Product and Version:  PBH 10.04
:     Author:  JEFF MORDEN                            Organization:  STS
:   Customer:  NIS                            Date Written:  2/9/89
: Description of Problem:  #
:               PROBLEM WAS THAT THE FORWARD ABORTING OF TRANSPARENT TEXT
:               MESSAGES TO THE PBT WAS NOT BEING HANDLED PROPERLY.  THE
:               PBH WAS TREATING THE RETRANSMITTED BLOCK FROM THE HOST AS
:               A SUBSEQUENT DATA BLOCK(AS IF PREVIOUS BLOCK ENDED IN ETB)
:               RATHER THAN AS A FIRST BLOCK.  THIS RESULTED IN IMPROPER DSP
:               HEADERS AND ERRORS IN THE AMOUNT OF DATA THAT BACKGROUND
:               EXPECTED TO FIND IN THE BUFFERS SENT UP FROM FOREGROUND. 
:               THIS CAUSED BACKGROUND TO LOOK FOR ADDITIONAL BUFFERS IN A 
:               CHAIN THAT DID NOT EXIST AND RESULTED IN A CRASH.  
:
:               THE PROBLEM WAS CAUSED BY THE SETTING OF FIRBLK WHEN TRANSPARENT
:               DATA MESSAGES WERE SENT TO BACKGROUND.  THERE APPEARS TO BE
:               NO FUNCTIONAL PURPOSE FOR THIS AND IT CAUSES ERRORS IN
:               THE RESETING OF RECEIVE STATES AFTER A FORWARD ABORT.  THE 
:               PATCH JUST REMOVES THIS INSTRUCTION.
:
:
:---------------------------------------------------------------------


        LO      BSCRCV
PATCH(020989,1100,J/MORDEN,RCVS3A+1E,,4)
        J       RCVS32
ENDPATCH(ENSURE PROPER RESET OF RECEIVE STATE WHEN FORWARD ABORT XPRNT BLOCK)
        FO      BSCRCV

:---------------------------------------------------------------------
: Patch name:  ENDMSG.104          Product and Version:  PBH 10.04/14.01
:     Author:  JEFF MORDEN                Organization:  STS
:   Customer:  TRANSAMERICA               Date Written:  9/16/88
:                                         Modified for AUTONET on 8/9/89
: Description of Problem:  #
:       PROBLEM IS THAT PARTIAL BLOCKS THAT HAVE BEEN EARLY DATA FORWARDED
:       ARE NOT BEING PROPERLY TERMINATED AND FLUSHED FROM THE XMT QUEUE.
:       THIS RESULTED IN FORGROUNDS ATTEMPT TO PUT BACK THE SAME BUFFER
:       TWICE TO THE FREE POOL WITH A RESULTING CRASH OR LOOP IN PBBC.
:       THIS PATCH WILL FLUSH THE PARTIAL BUFFER IF EDF HAS NOT OCCURED
:       AND IT IS THE FIRST BUFFER IN THE MESSAGE OR ELSE TERMINATE THE
:       MESSAGE WITH AN ENQ FORWARD ABORT.
:	The crash of AT ticket #254493 caused by a device got zapped
:	without clean up it's DIRBA, then next user logged on and ran into
:	this patch during logon mode, with the leftover of DIRBA from last
:	session it tried to reset some bit array out of range.  
:
:---------------------------------------------------------------------


PATCH(091588,1100,J/MORDEN,DOZA10+26,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,6)
        J       NUM5PA,,
ENDPATCH(PROPERLY TERMINATE PARTIAL INPUT MESSAGE)

:---------------------------------------------------------------------
: Patch name:  SVQFIX.104                     Product and Version:  PBH 10.04
:     Author:  JEFF MORDEN                            Organization:  STS
:   Customer:  ACME STEEL                            Date Written:  1/6/89
: Description of Problem:  #
:       PROBLEM OCCURES WHEN HOST USES READ MODIFIED COMMANDS AGAINST LARGE
:       INPUT MESSAGES THAT REQUIRE TRANSMISSION IN MANY BLOCKS.  THE
:       HOST WILL RESPOND WITH RVI AFTER THE FIRST BLOCK IN PREPARATION
:       FOR ISSUING THE READ MODIFIED.  THE RVI CAUSES THE INPUT MESSAGE TO
:       BE PLACED ON THE SAVEQ FOR RETRANSMISSION WHEN THE READ MODIFIED IS
:       ISSUED.  IF THE ENTIRE MESSAGE HAS NOT BEEN RECEIVED FROM THE
:       IRING WHEN THIS RVI IS ISSUED THEN AN INCOMPLETE BISYNC MESSAGE
:       WILL BE STORED ON THE SAVEQ AND THE PBH WILL EOT THE HOST.  THE 
:       HOST WILL THEN ISSUE ITS READ MODIFIED WHICH RESULTS IN THE PBH
:       ATTEMPTING TO TRANSMIT THE MESSAGE ON TH SAVEQ WHICH IS INCOMPLETE.
:       THIS CAN CREATE STREAMING SYCN CONDITIONS AND POSSIBLY EVEN A 
:       PROTOCOL VIOLATION.  DISABELING EARLY DATA FORWARDING BY SETTING
:       MINN0 AND MAXN0 TO 2000 OR GREATER ONLY STOPS THE EDF OF PARTIAL
:       BLOCKS.  ETB BLOCKS WILL STILL BE FORWARDED PRIOR TO RECEIPT OF
:       AN ETX WHICH DOESN'T PREVENT THE PROBLEM.  THIS PATCH PREVENTS THE
:       XMT Q FROM BEING RELEASED WHEN EDF IS DISABELED UNTIL AN ETX
:       IS SEEN.
:---------------------------------------------------------------------


PATCH(010688,1100,J/MORDEN,DSPQ80-18,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,60)
        PUSH(R0)
        LI      R0,MINN0
        CI      R0,2000
        JL      NOTDIS
        LI      R0,MAXN0
        CI      R0,2000
        JL      NOTDIS
        LHL     R0,DIR0,RDCB,           :GET CONTROL BYTES TO CHECK FOR ETX
        THI     R0,ETXBIT
        JN      NOTDIS                  :ETX - RELEASE XMT Q
        POP(R0)
        J       DSPQ80-6,,              :ETB - DO NOT RELEASE XMT Q
NOTDIS  POP(R0)
        LHL     R6,PPXMT,R5,R5          :RESTORE SOURCE INSTRUCTION
        J       DSPQ80-12,,             :BACK TO SOURCE TO RELAESE XMT
:
: NOW ALLOW BACKPRESSURE TO BE RELEASED EVEN THOUGH A LARGE NUMBER OF 
: INPUT CHARACTERS HAVE BEEN ACCUMULATED.
:
CONPATCH(TSBP-26,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,50)
        PUSH(R0)
        LI      R0,MINN0                :CHECK IF EDF IS DISABLED
        CI      R0,2000
        JL      EDFON
        LI      R0,MAXN0
        CI      R0,2000
        JL      EDFON
        POP(R0)
        CI      R0,1000
        J       TSBP-20,,               :IF NO EDF SET DCREL VALUE HIGHER
EDFON   POP(R0)                         :ELSE IF EDF THEN USE OLD DCREL VALUE
        CI      R0,400
        J       TSBP-20,,
CONPATCH(TSBP20-0A,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,50)
        PUSH(R0)
        LI      R0,MINN0                :CHECK IF EDF IS DISABLED
        CI      R0,2000
        JL      EDFO1
        LI      R0,MAXN0
        CI      R0,2000
        JL      EDFO1
        POP(R0)
        CI      R0,1000
        J       TSBP20-4,,               :IF NO EDF SET DCSET VALUE HIGHER
EDFO1   POP(R0)                         :ELSE IF EDF THEN USE OLD DCSET VALUE
        CI      R0,800
        J       TSBP20-4,,
ENDPATCH(IF EDF DISABELED DO NOT RELEASE XMT Q UNTIL ETX)

:---------------------------------------------------------------------
: Patch name:  MLTDMS.104                     Product and Version:  PBH 10.04
:     Author:  JEFF MORDEN                            Organization:  STS
:   Customer:  NIS                            Date Written:  3/14/89
: Description of Problem:  #
:
:---------------------------------------------------------------------

:This patch is used to ignore DSC message received and continues next
:SND message if defined.
:For version 10.04 multiple Disconnect option.
:JEFF MORDEN 1/20/89
:
          IF        DMSSWT
PATCH(012089,0730,J/MORDEN,DMRS10+0C,,2)
          JFS       .+4
ENDPATCH(IGNORE THE DSC MESSAGE FROM THE HOST)
:
:THE FOLLOWING PATCH IS AN ENHANCEMENT TO THE ABOVE PATCH.  THE PROBLEM
:WAS THAT LARGE DMSRCV FROM THE HOST WERE ONLY PARTIALLY BEING FLUSHED
:BY THE ROUTINE THAT EVALUATED THE MESSAGE TO SEE IF THE DISCONNECT 
:SEQUENCE COULD CONTINUE.  THE RESULT WAS THE SUBSEQUENT BUFFERS IN THAT
:MESSAGE WERE BEING SEEN BY THE EVALUATING ROUTINE AS A NEW MESSAGE AND
:THE DISCONNECT SEQUENCE WAS CONTINING PREMATURLY.  THIS HAD THE EFFECT
:OF HAVING THE DEVICE BEING INDICATED AS UNAVAILABLE EVEN THOUGH THE HOST
:WAS STILL ATTEMPTING TO RESPOND TO QUEUED DMSSND MESSAGES.  THIS RESULTS
:IN RVI'S AND INTERVENTION REQUIRED STATUS BEING SENT TO THE HOST BEFORE
:THE COMPLETE DISCONNECT SEQUENCE WAS EXECUTED.
:
        LO BSCCHR
        LO FRNTND
PATCH(012089,1100,J/MORDEN,DMRS10+1A,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,30)
        JAL     R5,FLRCV,,
        THI     R0,ETXBIT
        JE      NAFLSH
        JAL     R4,RDMSTM,,
        J       DMRS10+22,,
NAFLSH  POP(R5)
        J       DMRS60,,
ENDPATCH(DO NOT DO NEXT DMS SEND UNTIL ALL OF DMSRCV IS FLUSHED)
        FO BSCCHR
        FO FRNTND
        EI


:  THIS LAST PATCH FLUSHES THE RECEIVE Q OF THE RECEIVED ZAP MESSAGE FROM THE
:  HOST BEFORE STARTING THE MULTIPLE DISCONNECT SEQUENCE.  THIS PREVENTS THE
:  OUTPUT COMMAND Q SCANNER FROM RE-READING THE ZAP MESSAGE INSTEAD OF 
:  PROCESSING THE NEXT RECEIVE PORTION OF THE MULTIPLE DISCONNECT SEQUENCE.
:  THIS WAS CAUSING THE MULTIPLE DISCONNECT SEQUENCE TO NOT PROGRESS PROPERLY
:  AND THE PBT TO SEND REPEATED B7 ACCOUNTING MESSAGES FOR THIS DEVICES ZAP.

        IF      ZAPSWT
PATCH(031289,1100,J/MORDEN,OSCA14,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,62)
        PUSH(R7)
        TBT     0B,ZAPBIT,,
        JE      LZAPP
        LHL     R1,ZAPTBL,0B,0B         :GET POINTER TO ZAP MESSAGE
        LHL     R5,0,R1,                :GET THE LENGTH OF THE DEFINED MESSAGE
        LIS     R7,4                    :INDEX INTO BUFFER HOLDING RCV'd MSG
CZC     LB      R6,BBUFER,R3,R7         :GET BYTE FROM BUFFER - START AFTER WCC
        CLB     R6,2,R1,                 :COMPARE WITH DEFINED MSG
        JN      LZAPP
        AIS     R1,1                    :BUMP UP INDEX's
        AIS     R7,1
        SIS     R5,1                    :DECRIMENT LOOP CONTROL
        JG      CZC                     :BACK TO CHECK NEXT CHARACTER FOR MATCH
        JAL     R5,FLRCV,,
        LIS     R5,$A2
        STB     R5,DZAP,RDCB,
        POP(R7)
        J       OSCA16+1C,,
LZAPP   POP(R7)
        J       OSCA38,,
ENDPATCH(FLUSH ZAP MESSAGE FROM RCVQ BEFORE STARTING MULTIPLE DISCONNECT)
        EI

:---------------------------------------------------------------------
: Patch name:  STOFIX.TST                     Product and Version:  PBH 10.04
:     Author:  JEFF MORDEN                            Organization:  STS
:   Customer:  NIS                            Date Written:  3/31/89
: Description of Problem:  #
:       CRASH CAUSED BY PBH ATTEMPTING TO ZAP A DEVICE THAT HAD ALREADY
:       BEEN ZAPPED.  THE ZAP WAS INITIATED BY THE STATUS MESSAGE TIMEOUT
:       ROUTINE WHICH THOUGHT THAT NO DEVICE END STATUS MESSAGE HAD BEEN
:       RECEIVED FROM THE REMOTE IN RESPONSE TO A CIRCUIT ENABLE.  THOUGH
:       THE AVAILABLE INFORMATION IS NOT ABSOLUTLY CONCLUSIVE, IT APPEARS
:       THE CONDITION COULD ONLY HAVE BEEN STARTED BY A CIRCUIT ZAP AFTER
:       CIRCUIT ENABLE HAD BEEN SENT BY THE PBH AND BEFORE STATUS HAD
:       BEEN RECEIVED.  THIS WOULD HAVE LEFT THE TIMER RUNNING AND OUT OF
:       SYNC WITH REAL EVENTS.  IF A STATUS MESSAGE TIMEOUT IS BEING
:       EXECUTED THIS PATCH WILL VERIFY THE DEVICE STATE BY CHECKING
:       TO SEE IF THE DCB OF THE DEVICE BEING CHECKED IS THE DUMMY DCB.
:       IF IT IS THEN NO ZAP WILL BE PERFORMED.
:
:---------------------------------------------------------------------


PATCH(033189,1100,J/MORDEN,STMTOR+0C,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,26)
        LHL     RDCB,DEVTAB,RDEV,RDEV           :FROM SOURCE
        CHI     RDCB,2                          :IS IT THE DUMMY DCB
        JN      STMTOR+12,,                      :NO -- GO ZAP DEVICE
        POP(R6)
        POP(R5)
        JR      R5                              :ELSE EXIT WITHOUT ZAPPING
ENDPATCH(IF DEVICE NOT ACTIVE THEN DO NOT DO STATUS MESSAGE TIMEOUT)


:---------------------------------------------------------------------
: Patch name:  HSDOWN.104                     Product and Version:  PBH 10.04
:     Author:  JEFF MORDEN                            Organization:  STS
:   Customer:  ANHEUSER BUSH                            Date Written:  9/13/89
: Description of Problem:  #
:       PROBLEM WAS THAT THE HOST INTERFACE WAS REPEATEDLY REPORTING
:       DOWN FOR SHORT INTERVALS EQUAL TO THE VALUE OF POLT3 EVEN THOUGH
:       STILL BEING POLLED BY HOST.  THIS WAS CAUSED BY LOGIC IN THE
:       ROUTINE CKPLT WHICH CAUSED HOST DOWN REPORT TO BE SENT AT TIME
:       NOPOLL WAS FIRST DETECTED FOR ALL DEVICES ON THAT HOST.  DEPENDING
:       ON WHAT IS HAPPENING ON THE LINE IT IS POSSIBLE THAT THE ROUTINE
:       WILL SEE BRIEF MOMENTS WHEN POLL INDICATORS SHOW A NOPOLL SITUATION.
:       THIS CAN HAPPEN WHEN THE LINE IS TIED UP WITH SERVICING ONE
:       DEVICE FOR A PERIOD GREATER THAN POLT3 WHICH DEFAULTS TO 5
:       SECONDS.  THE PATCH IMPLIMENTS A TIMER TO WAIT FOR A NOPOLL
:       CONDITION TO LAST FOR NOPOLL SECONDS BEFORE SENDING A HOST DOWN
:       MESSAGE.
:
:---------------------------------------------------------------------


PATCH(091289,1100,J/MORDEN,CKPL50+12,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,58)
        LR      R7,R5           :GET RELATIVE HOST NUMBER
        SLLS    R7,2            :MAKE INTO FULL WORD INDEX
        L       R9,DWNTIM,R7,   :GET HOW LONG DOWN SO FAR
        JE      CLNRUN          :IF 0 THEN CLOCK NOT RUNNING
        S       R9,FASTC,,      :ELSE CLOCK RUNNING -- IS IT EXPIRED YET?
        JL      CESHD           :CLOCK EXPIRED SO DO HOST DOWN MESSAGE
        J       CKPL80,,        :NOT EXPIRED SO DO NOTHING
CLNRUN  LHL     R9,PTIMRV,R5,R5 :GET NOPOLL TIME
        PUSH(R5)
        LHI     R5,RATE
        MHR     R9,R5           :CONVERT NOPOLL DURATION TO CLOCK TICKS
        POP(R5)
        A       R9,FASTC,,      :ADD FASTC TO GET TIME TO EXPIRE
        ST      R9,DWNTIM,R7,   :SAVE IT FOR LATER TEST
        J       CKPL80,,        :DO NO OTHER ACTION YET.
CESHD   RBT     R5,HSBIT,,      :FROM SOURCE -- MARK HOST DOWN
        J       CKPL50+18,,     :GO SEND HOST DOWN MESSAGE

:NOW PROVIDE FOR RESETING THE TIMER WHEN HOST IS ANSWERED

CONPATCH(CKPL60,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,20)
        SBT     R5,HSBIT,,      :FROM SOURCE -- MARK HOST UP
        JN      CKPL80,,        :DO NOTHING IF ALREADY UP
        LIS     R7,0            :PREPARE TO ZERO OUT HOST DOWN TIMER
        LR      R9,R5           :GET RELATIVE LINE NUMBER
        SLLS    R9,2            :MAKE FULL WORD INDEX
        ST      R7,DWNTIM,R9,   :TURN OFF TIMER FOR LINE
        J       CKPL60+8,,      :BACK TO SOURCE TO SEND HOST ANSWERED REPORT

CONPATCH(PA0PTR,,MAXHST*4+3)
DWNTIM  WS      MAXHST

ENDPATCH(PREVENT HOST DOWN REPORT UNTIL NOPOLL TIME EXPIRES)


:---------------------------------------------------------------------
: Patch name:  VHMEDF.104                     Product and Version:  PBH 10.04
:     Author:  JEFF MORDEN                            Organization:  STS
:   Customer:  TRANSAMERICA                            Date Written:  5/10/89
: Description of Problem:  #
:       PROBLEM OCCURES WHEN VIRTUAL HOST MODE DEVICES OR EMULATIONS ARE
:       CONFIGURED IMPROPERLY TO USE SOMETHING OTHER THAN AN ETX AS A
:       MESSAGE TERMINATOR.  IN THESE SITUATIONS IF ENOUGH DATA HAS BEEN 
:       RECEIVED TO START THE EDF PROCESS TO THE HOST THEN THE LINE
:       WILL BE TIED UP WITH SYNC PADS WHILE THE DEVICE WAITS TO RECEIVE
:       A PROPER MESSAGE TERMINATOR(ETX) FROM THE USER SO THAT THE 
:       BISYNC MESSAGE CAN BE COMPLETED.  THIS STREAMING SYNC CONDITION
:       COULD GO ON FOR A LONG TIME AND WHILE THIS IS OCCURING OTHER 
:       DEVICES WILL NOT BE SERVICED AND WILL TIME OUT DUE TO NOPOLL
:       INDICATIONS.  THIS PATCH DISABLES EDF FOR VIRTUAL HOST MODE DEVICES.
:
:---------------------------------------------------------------------



PATCH(051089,1100,J/MORDEB,PBQ2XMT-12,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,28)
        LIS     R4,AMODE
        CLB     R4,DMODE,RDCB,
        JE      PBQ2XMT,,
        RLCMD(XMT,R5,R5)
        J       PBQ2XMT,,
CONPATCH(CKBSZ1-12,,6)
        J       CKBSZ1
ENDPATCH(TURN OFF EDF FOR VIRTUAL HOST MODE SESSIONS)


:---------------------------------------------------------------------
: Patch name:  CHAINS.104                     Product and Version:  PBH 10.04
:     Author:  JEFF MORDEN                            Organization:  STS
:   Customer:  AIR FRANCE                            Date Written:  11/6/89
: Description of Problem:  #
:               WHEN THE DEVICES CIRCUIT IS LOST WHILE IN THE PROCESS OF
:               RECEIVING CHAINED COMMANDS FROM THE HOST THE PBH WILL NOT
:               CHECK DEVICE STATUS BUT WILL JUST KEEP ACKing MESSAGES.
:               PATCH CHECKS FOR CIRCUIT LOSS UPON RECEIVING A DATA MESSAGE
:               AND IF SO WILL SET STATUS PENDING AND TERMINATE THE SELECT
:               STATE BY SENDING EOT TO THE HOST.
:
:---------------------------------------------------------------------


        LO      BSCPTP
        LO      BSCRCV
        LO      BSCSRV
        LO      BSCSXM
        LO      BSCEXC
        LO      BSCCHR
PATCH(110389,1100,J/MORDEN,RDAT00,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,50)
        STH     R6,RSTIME,RL2,          :RESTORE SOURCE INSTRUCTION
        LB      RDEV,CDEV,RL,           :GET CURRENT DEVICE
        TBT     RDEV,DUNAV,,            :IS DEVICE STILL ACTIVE(WITH CIRCUIT)
        JE      STILUP                  :JUMP IF STILL UP
        LB      R1,DEVHST,RDEV,         :GET HOST TYPE
        LHL     R6,IRSTAT,R1,R1         :GET PROPER INTERVENTION REQUIRED STATUS
        STH     R6,FSTAT,RDEV,RDEV      :STORE IT FOR HOST TO RETRIEVE
        SBT     RDEV,FSPEND,,           :SET TO SHOW FOREGROUND STATUS PENDING
        JAL     R5,FLRCV,,              :FLUSH RECEIVED MESSAGE
        J       IRESET,,                :SEND EOT TO STOP HOST COMMANDS
STILUP  LB      R6,RCVCTY,RL,           :RESTORE ANOTHER SOURCE INSTRUCTION
        J       RDAT00+8,,              :IF STILL CIRCUIT CONTINUE
ENDPATCH(ABORT HOST SELECT IF CIRCUIT ZAPPED DURING CHAINED COMMANDS)
        FO      BSCPTP
        FO      BSCRCV
        FO      BSCSRV
        FO      BSCSXM
        FO      BSCEXC
        FO      BSCCHR
:---------------------------------------------------------------------
: Patch name:  RCVTOU.TST                   Product and Version:  PBH10.04
:     Author:  PEGGY JING                   Organization:  STS
:   Customer:  AIRFNET                       Date Written:  11/21/89
: Description of Problem:  
:	PBH STAYS IN TIMEOUT WHEN RECEIVING CHAINED COMMANDS.  THIS PATCH
: WILL CAUSE PBH TO ENQ HOST WHEN TIMEOUT OCCURES IN THE MIDDLE OF A 
: PENDING OPERATION SUCH AS THE CHAINED COMMAND SITUATION.
:---------------------------------------------------------------------
	LO	BSCPTP
	LO	BSCCHR
	LO	BSCEXC
	LO	BSCRCV
	LO	BSCSXM
PATCH(112189,1120,P/JING,RTMOT,,6)
	J	PA1PTR,,
CONPATCH(PA1PTR,,28)
	LHI	R3,RCVTO	:LOAD ERROR TYPE
	JAL	R5,SETERR,,	:COUNT ERROR & COMPARE TO LIMIT
	LIS	R1,XCENQ	:GET ENQ OP CODE
	LHI	R0,SRCV-PSEG	:SET STATE FOR RECEIVE DATA
	STH	R0,STATE,RL2,	
	LHI	R0,RSTIME,RL2	:GET RCV TIMEOUT VALUE
	LHI	R4,BSCRBC	:SET RCV BUFFERLET LIMIT
	J	XMTRCV,,	:GO XMT & DECODE READ RESPONSE
ENDPATCH(ENQ HOST WHEN TIMEOUT OCCURES IN THE MIDDLE OF PENDING OPERATION)
	FO	BSCSXM
	FO	BSCRCV
	FO	BSCEXC
	FO	BSCCHR
	FO	BSCPTP


:---------------------------------------------------------------------
: Patch name:  #                     Product and Version:  #
:     Author:  #                            Organization:  #
:   Customer:  #                            Date Written:  #
: Description of Problem:  #
:
:---------------------------------------------------------------------


PATCH(113089,1100,J/MORDEN,DSPQ52+12,,6)
        J       DSPQ55

:
:NOW MODIFY ROUTINE THAT PROCESSES THE RESPONSE ABORTED MESSAGE FROM THE
:PBT SO THAT IT WILL PUT AN EOT COMMAND ON THE DEVICES XMT Q AND NOT 
:DECRIMENT THE INPUT SEQUENCE NUMBER.  STATUS WILL NOT BE SET BUT WE
:WILL WAIT FOR IT TO COME FROM THE PBT.
:
CONPATCH(UNDMS4,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,6)
        J       NUM6PA,,


:
:NOW MODIFY IIXLST OF DSP Q BIT PACKET PROCESSING ROUTINES SO THAT WHEN A
:RESPONSE ABORTED MESSAGE IS RECEIVED FROM THE PBT THAT THE PBH WILL SEND
:AN EOT ABORT TOTHE HOST AND NOT THE ZAP THE CIRCUIT OR ADJUST THE INPUT
:SEQUENCE NUMBER.
:
CONPATCH(IIXLST+32,,4)
        HC      UNDMSG-10000
        HC      UNDMSG-10000            :PROCESS DSP ABORT FROM PBT WITH UNDMSG

:
:NEED TO ADJUST BACKGROUND SO THAT IT WILL RECOGNIZE ENQ FORWARD ABORT AS
:A VALID DATA MESSAGE AND INCREMENT RECEIVE SEQUENCE NUMBER DISEQ.
:
CONPATCH(DSPQ55,,6)
        J       PA1PTR,,
CONPATCH(PA1PTR,,1C)
        OHI     R0,ENQBIT               :FROM SOURCE
        LB      R4,DISEQ,RDCB,          :GET SEQUENCE NUMBER
        AIS     R4,1
        STB     R4,DISEQ,RDCB,
        LIS     R4,0
        J       DSPQ55+6,,


:
:ONE LAST CHANGE NEEDED FOR FOREGROUND.  MAKE SURE THAT FORWARD ABORT BY MEANS
:OF ENQ TERMINATOR TO DATA IS RECOGNIZED AS A MESSAGE TERMINATOR THUS ALLOWING
:PROCESSES TO PROPERLY COMPLETE AND RESET.
:
        LO      BSCPTP
CONPATCH(XRAK40+58,,4)
        THI     R0,ETXBIT!ETBBIT!ENQBIT :READY TO RELEASE MESSAGE TO SAVEQ?

        FO      BSCPTP

ENDPATCH(PROPERLY ACT UPON FORWARD ABORTS FROM PBT)

:---------------------------------------------------------------------
: Patch name:  NOPAT0.TST         Product and Version:  PBH10.R04 only
:     Author:  PEGGY JING                Organization:  STS
:   Customer:  UNIV.NY & ALL CUSTOMERS   Date Written:  02/09/90
: Description of Problem:  
:   In this version of code, there are five redundant instructions in
:   DSPQ66 which using PATCH0 area incorrectly.  The later version of
:   codes have this section commented out.  This patch also skip those
:   trouble maker.
:---------------------------------------------------------------------
PATCH(020990,1100,P/JING,DSPQ66+6,,6)
	J	DSPQ68,,	:SKIP REDUNDANT INSTRCUTIONS
ENDPATCH(CLEAN UP DSPQ66)


P0END   EQ      PA0PTR
P1END   EQ      PA1PTR
        REMARK  %   END PATCH FILE FOR PBH10.04

        REMARK  %%
        REMARK  %ALERT  ALERT   ALERT   ALERT   ALERT   ALERT   ALERT   ALERT
        REMARK  %    ALERT  ALERT   ALERT   ALERT   ALERT   ALERT   ALERT%%
        REMARK  CHANGES HAVE BEEN MADE TO ALLOW PATCH CODE INTO SEGMENT
        REMARK  %NINE.  BE SURE THAT YOUR COMMANDS FILE HAS THE PROPER
        REMARK  %SEGMENT NINE DESIGNATION AT THE END.  REFER TO THE 
        REMARK  %COMMAND FILE EXAMPLE PBH10.C04 FOUND IN THE BSCPAT
        REMARK  %DIRECTORY.  ALSO, INDICATE A P1SIZE OF 600 IN YOUR TYMFILE
        REMARK  %AND ADD 1K TO THE GEN SIZE REPORTED BY NAD.
        REMARK  %%
        REMARK  %ALERT  ALERT   ALERT   ALERT   ALERT   ALERT   ALERT   ALERT
        REMARK  %    ALERT   ALERT   ALERT   ALERT   ALERT   ALERT   ALERT

tX-¼