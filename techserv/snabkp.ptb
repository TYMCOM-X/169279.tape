
::	PATCH FILE FILENAME: SNA03.P03
::
::
::


::001)  Patch at RELBP and QDIFSN for Back Pressure Problem
:
	RA	0
RELBPL	EQ	0
SETBPL	EQ	0A

	LO	GBLDEF
	LO	CBKDEF
	LO	CMDLST
	LO	ISISC
	LO	OSCAN
	LO	SNAPKG
	LO	IFCNTL
	LO	BSUB
	LO	FRNTND
	LO 	MAIN

	PATCH(121787,1400,D/BONDAD,RELBP3+0A,,6)
	J	PA1PTR,,		:JUMP TO PATCH

	CONPATCH(RELBP1+12,,6)
	NOP	0,0,0,0			:ACP IS NOT SUPPORTED

	CONPATCH(LOGMB2+38,,6)
	J	PA1LOG,,		:LOGON TIME CLEANING

	CONPATCH(RSTTRM+38,,6)		:TIF ZAP CLEANING
	J	PA1TRM,,

	CONPATCH(DSCHPR+26,,6)		:HIF ZAP CLEANING
	J	PA1HPR,,

	CONPATCH(PA1PTR,,092)
	CHI	RPORT,NPORT		:CHECK RPORT
	JGE	RELBP1,,		:JUMP IF FINISHED
	LHL	RDCB,PORTAB,RPORT,RPORT	:GET DCB POINTER
	LHL	R4,DCBLKS+DCBLUC,RDCB,	:GET LUCTAB ENTRY
	JE	RELBP2,,		:IF NO LUC ENTRY THEN RETURN
	LIS	R0,0			:INIT PENDING LINK COUNTER TO ZERO
	LHL	R2,LUCTAB+LUSNDR,R4,	:GET NWK TO SNA DATA QUEUE RELEASE PTR
	LHL	R3,LUCTAB+LUSNDG,R4,	:GET NWK TO SNA DATA QUEUE GET PTR
RELBP4
	CR	R2,R3			:ANY PENDING CMD LINKS?
	JE	RELBP3+24,,		:NO, RELEASE BKPRSSRE
	AIS	R0,1			:YES, COUNT ONE MORE LINK PENDING
	CHI	R0,RELBPL		:ARE THERE TOO MANY?
	JG	RELBP2,,		:YES, DO NOT REL BKPR
	LHL	R3,CMDBLK+CLPTR,R3,	:POINT TO NEXT BUFFER IN QUEUE
	J	RELBP4			:GO CHECK IF DONE

PA1LOG	SBT	RPORT,TOISIS,,		:CLEAN BACKPRESSURE
	SBT	RPORT,FRISIS,,		:CLEAN BACKPRESSURE
	J	LOGMB2+3E,,		:JUMP BACK

PA1TRM	RBT	R1,ACP,,		:RESET ACTIVE PORT
	SBT	R1,TOISIS,,		:CLEAN BACKPRESSURE
	SBT	R1,FRISIS,,		:CLEAN BACKPRESSURE
	J	RSTTRM+3E,,		:JUMP BACK

PA1HPR	RBT	R2,ACP,,		:RESET ACTIVE PORT
	SBT	R2,TOISIS,,		:CLEAN BACKPRESSURE
	SBT	R2,FRISIS,,		:CLEAN BACKPRESSURE
	LA	R3,DETACH,,		:GET DETACH MESSAGE
	J	DSCHPR+2E,,		:JUMP BACK

	ENDPATCH(RELEASE BACKPRESSURE)


	PATCH(121787,1400,D/BONDAD,QDIFSN,,6)
	J	PA1PTR,,		:JUMP TO PATCH

	CONPATCH(PA1PTR,,76)
	ST	R6,BBISAV,,		:SAVE RET REG
	STM	R0,LURGSV,,		:SAVE ALL REGS
	LHL	RDCB,LUCTAB+LUCDCB,R11,	:GET DCB PTR
	JE	QDIFS2			:DONE IF NOT SET
	LHL	R1,DCBLKS+DCBIPR,RDCB,	:GET PORT NUMBER
	JE	QDIFS2			:DONE IF NOT SET
	TBT	R1,FRISIS,,		:CHECK BACKPRSSRE
	JE	QDIFS2			:DONE IF SET
	LIS	R0,0			:INIT LINK CONTER
	LHL	R4,LUCTAB+LUSNDG,R11,	:GET GET PTR
	LHL	R5,LUCTAB+LUSNDR,R11,	:GET REL PTR
QDIFS1	CR	R4,R5			:COMPARE
	JE	QDIFS2			:DONE
	LHL	R4,CMDBLK+CLPTR,R4,	:GET NEXT CMD LINK
	AIS	R0,1			:INCR COUNTER
	CHI	R0,SETBPL		:TOO MANY?
	JL	QDIFS1			:CONTINUE IF NOT
: BACK PRESSURE 
	RBT	R1,FRISIS,,		:RESET NO BP INDICATOR
	LHI	R2,NOSMSG		:GET APPLY BP MSG
:SEND MESSAGE
	LIS	R0,3			:GET MSG LENGTH
	JAL	R4,SLOR,,		:START OUTPUT OF MSG
	JAL	R4,ELOR,,		:END MSG OUTPUT
QDIFS2	LM	R0,LURGSV,,		:RESTORE ALL REGISTERS
	J	QDIFSN+6,,		:RETURN

	ENDPATCH(SET BACKPRESSURE)


	FO 	GBLDEF
	FO	CMDLST
	FO	DLCPRO
	FO	CBKDEF
	FO	ISISC
	FO	SNAPKG
	FO	BSUB
	FO	IFCNTL
	FO	OSCAN
	FO	FRNTND

