:
: PATCH FOR CONSAT VERSION 3.04 AND 3.05.  KIT LUEDER.  03/12/86.
:
: THIS PATCH REQUIRE USE OF TWO OTHER PATCHES (INSCODE:31)ACM203.P01 AND
: ACM203.P02, TO SETUP THE PATCH AREAS AND DEFINE THE PATCH MACROS.
:
: PATCH TO OVERWRITE PASSWORD TYPED AT USER TERMINAL, IF NETWORK ECHO
: IS DISABLED (I.E., A HALF-DUPLEX TERMINAL).  IT SENDS A BACKSPACE
: CHAR (CNTL-H), AN ASCII "O" AND THEN ANOTHER B/S, TO OVERWRITE THE TYPED 
: CHARACTER.  IF THE NETWORK IS DOING THE ECHO, JUST SUPPRESSING ECHO
: OF THE PASSWORD (THE NORMAL CASE) IS SUFFICIENT, AND THIS PATCH WILL
: NOT SEND THE BLOT-OUT CHARS IN THAT CASE.
:
: THE LETTER "O" WAS CHOSEN FOR THE BLOT-OUT CHAR, SINCE THAT IS THE
: LAST BLOT-OUT THAT WAS SENT DURING THE INITIAL PASSWORD PROMPT.
:
: THIS PATCH ASSUMES THAT THE TERMINAL IS AN ASYNC ASCII TERMINAL.
:
	LO	LOGGER

PATCH(031286,1930,TYM.K/LUEDER,VLOG+28,,4)
        JE       PA1PTR		:IF PASSWORD CHAR, INVOKE THIS PATCH
CONPATCH(PA1PTR,,40)
	TBT	RP,LGECHO	:WERE WE DOING ECHO BEFORE THE PASSWORD? 
	JN	LOG3		:IF SO, DON'T NEED THIS LOGIC
	TBT	RP,PARITY	:SHOULD WE USE EVEN PARITY?
	JFS	LOG11		:IF SO

: FOR ODD OR NO PARITY
	LIS	RBC,$008	:LOAD A CONTROL-H
	JAL	RCH,PUTCHR	:SEND IT TO THE TERMINAL
	LHI	RBC,$04F	:LOAD A LETTER O, TO BLOT OUT VALUE TYPED
	JAL	RCH,PUTCHR	:SEND IT TO THE TERMINAL
	LIS	RBC,$008	:LOAD A CONTROL-H
	JAL	RCH,PUTCHR	:SEND IT TO THE TERMINAL
	JFS	LOG12

: FOR EVEN PARITY
LOG11	LHI	RBC,$0088	:LOAD A CONTROL-H
	JAL	RCH,PUTCHR	:SEND IT TO THE TERMINAL
	LHI	RBC,$00CF	:LOAD A LETTER O, TO BLOT OUT VALUE TYPED
	JAL	RCH,PUTCHR	:SEND IT TO THE TERMINAL
	LHI	RBC,$0088	:LOAD A CONTROL-H
	JAL	RCH,PUTCHR	:SEND IT TO THE TERMINAL

LOG12				:DONE.  NOW RETURN
	J	LOG3		:CONTINUE PROCESSING THE PASSWORD CHAR
ENDPATCH(BLOT OUT P/W CHARS TYPED IF TERMINAL IS HALF-DUPLEX CRT)

	FO	LOGGER
