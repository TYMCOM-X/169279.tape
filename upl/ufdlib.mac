;+
;.TITLE UFDLIB - USER FILE DIRECTORY FORTRAN INTERFACE
;.LITERAL
;
;
;       NAME:   UFDLIB.MAC
;
;       AUTHOR: DETROIT OFFICE (SJC)
;
;       PURPOSE:THESE ROUTINES PROVIDE A FORTRAN
;               INTERFACE TO THE USER FILE DIRECTORY
;               ALLOWING THE PROGRAMMER TO OBTAIN INFORMATION
;               ABOUT FILES IN SAID UFD.
;
;
;       USE:    TO ACCESS THE ROUTINES, ADD TO YOUR LOAD 
;               STRING (UPL)UFDLIB
;
;       RELEASE: 1.0 07-MAY-1980
;
;       CHANGE LOG:
;
;.END LITERAL
;-
        TITLE UFDLIB USER FILE DIRECTORY LIBRARY

        ENTRY UFDOPN,UFDCLS,UFDSTP,UFDNAM,UFDSIZ,UFDSTA,UFDLIC,UFDDAT
        ENTRY UFDSPL,DATCNV

.CHCLS==1B17            ;CLOSE FUNCTION FOR CHANIO
.CHLK==4B17             ;LOOKUP FUNCTION FOR CHANIO
.CHOPN==13B17           ;OPEN FUNCTION
.CHUFD==33B17

OPNBLK: 0               ;OPEN BLOCK STATUS BITS
        SIXBIT/DSK/     ;DEVICE TYPE
        0,,0


LKBLK:  XWD 0,35        ;LOOKUP BLOCK ARG COUNT
        XWD 1,1         ;PPN
        XWD 1,4         ;FILENAME
        SIXBIT /UFD/    ;EXTENTION
        Z
        BLOCK 35        ;FEMAINDER OF EXTENDED BLOCK


UFDARG: -6,,UFDFNM      ;6 ARGUMENTS TO BE RETURNED TO BLOCK UFDFNM
UFDSNM: SIXBIT /*/      ;GIVE ME ALL FILES
UFDSXT: SIXBIT/*/       ;WITH ALL EXTENTIONS
        360000,,0
UFDSSN: Z
UFDSSX: Z
        Z
COUNT:  Z


UFDFNM: 0               ;SIXBIT NAME OF RETURNED FILE
UFDFXT: 0               ;SIXBIT EXTENTION OF RETURNED FILE
UFDFSZ: 0               ;FILE SIZE
UFDFLC: 0               ;LICENSE WORD
UFDFDT: 0               ;DATE AND TIME
UFDFST: 0               ;STATUS BITS


DATBLK: 0
TIMVAL: 0
CONVBT: 0,,0
SIXNAM: BLOCK 2         ;SIXBIT USERNAME
ASCNAM: BLOCK 5         ;ASCII USERNAME


PTR1= 5                 ;AC5 FOR BYTE POINTER 1
PTR2= 6                 ;AC6 FOR BYTE POINTER 2
PTR6:   POINT 7,ASCNAM, ;POINT TO ASCII USERNAME



;
;+
;.PG.S 3.LITERAL
;                       SUBROUTINE UFDOPN
;
;
;       PURPOSE:THIS ROUTINE WILL INITIALIZE AND OPEN
;               THE SPECIFIED UFD TO BE SCANNED.
;
;
;       CALLING SEQUENCE:
;       
;               CALL UPDOPN(UNAME,IERR)
;
;       WHERE:
;
;               UNAME = USERNAME TO BE CHECKED (IN AN ARRAY)
;               IERR  = ERROR CODE RETURNED BY SUBROUTINE
;
;
;       ERROR CODES
;               00 = SUCCESSFULL
;               01 = UNABLE TO OPEN FILE
;               02 = UNABLE TO LINK (BAD USERCODE)
;
;.END LITERAL
;-
UFDOPN: CAIA            ;SUBROUTINE ENTRY,F10 WILL SKIP NEXT INSTRUCTION
                        ;F40 WILL DEPOSIT INTO THIS ADDRESS AND EXECUTE
                        ;NEXT INSTRUCTION.
        PUSH 17,16      ;SAVE RETURN ADDRESS IN AC17 FOR RETURN
        MOVEI   9,0     ;PUT VALUE 0 IN AC9
        MOVEM   9,@1(16) ; PUT CONTENTS OF AC9 INTO 1 OF OF 16
        MOVE PTR1,[POINT 7,,] ;SEVEN BIT POINTER TO PASSED ARRAY
        MOVE PTR2,[POINT 6,SIXNAM,] ;SIX BIT POINTER TO NAME
                                    ;FIELD
        HRRI PTR1,@(16)
        MOVEI 0,^D12    ;MOVE THE VALUE OF 12 TO AC0(UNAME IS 12 BYTES)
NAMLOOP:ILDB 1,PTR1     ;LOAD A CHAR ADDRESS BYTE POINTER 2 (ASCII)
        CAIN 1,0        ;IF NAME IS DONE
        JRST ENDNAM     ;THEN DONE WITH NAME
        CAIN 1,40       ;IF NAME IS BLANK
        JRST ENDNAM     ;THEN DONE WITH NAME
        SUBI 1," "      ;SUBTRACT THE VALUE OF A SPACE (MAKE 6 BIT)
        IDPB 1,PTR2     ;DEPOSIT THE CONTENTS OF AC1 IN 6 BIT AREA
        SOJG 0,NAMLOOP  ;SUBTRACT ONE FROM AC0 AND IF THE CONTENTS OF AC0
                        ;ARE GREATER THEN 0, GO TO NAMLOOP
ENDNAM: MOVE 1,[.CHOPN+^D47]
        CHANIO 1,OPNBLK
        JRST OPNERR
        MOVEI 1,SIXNAM
        HRLI 1,0
        MOVEM 1,LKBLK+1
        MOVE 1,[.CHLK+^D47]
        CHANIO 1,LKBLK  ;LOOK UP TO GET PPN FOR SPECIFIED USERNAME
        JUMP            ;CONVERTED SPECIFIED USERNAME TO PPN
        MOVE 1,LKBLK+1  ;MOVE PPN INTO AC1
        MOVEM 1,LKBLK+2 ;STORE PPN INTO FILENAME LOCATION
        MOVE 1,[1,,1]   ;MOVE LOCATION INTO AC1
        MOVEM 1,LKBLK+1 ;STORE LOCATION OF UFD PPN INTO PPN LOCATION
        MOVE 1,[.CHLK+^D47] 
        CHANIO 1,LKBLK  ;OPEN UFD
        JRST LKERR      ;INVALID ID
        POPJ 17,
OPNERR: MOVEI 9,1       ;MOVE ERROR CODE INTO AC9
        MOVEM 9,@1(16)  ;MOVE ERROR CODE TO IERR       
        POPJ 17,
LKERR:  MOVEI 9,2       ;MOVE ERROR CODE INTO AC9
        MOVEM 9,@1(16)  ;MOVE ERROR CODE TO IERR
        POPJ 17,
;
;+
;.PG.S 3.LITERAL
;                       SUBROUTINE UFDCLS
;
;
;       PURPOSE:THIS ROUTINE WILL CLOSE THE UFD THAT WAS
;               OPENED BY SUBROUTINE UFDOPN
;
;
;       CALLING SEQUENCE:
;
;               CALL UFDCLS(IERR)
;
;       WHERE:
;
;               IERR = ERROR CODE RETURNED BY SUBROUTINE
;
;
;
;       ERROR CODES
;               00 = SUCCESSFULL
;               01 = UNABLE TO CLOSE (NEVER OPENED)
;
;
;.END LITERAL
;-
UFDCLS: CAIA            ;SUBROUTINE ENTRY,F10 WILL SKIP NEXT INSTRUCTION
                        ;F40 WILL DEPOSIT INTO THIS ADDRESS AND EXECUTE
                        ;NEXT INSTRUCTION.
        PUSH 17,16      ;SAVE RETURN ADDRESS IN AC17 FOR RETURN
        MOVEI 9,0       ;PUT VALUE 0 IN AC9
        MOVEM 9,@0(16)  ;PUT CONTENTS OF AC9 INTO 0 OFF OF 16
        MOVE 1,[.CHCLS+^D47] ;MOVE CLOSE OPTION TO AC1
        CHANIO 1,       ;ISSUE CHANNEL CLOSE
        POPJ 17,        ;RETURN
;
;+
;.PG.S 3.LITERAL
;                       SUBROUTINE UFDSTP
;
;
;       PURPOSE:THIS ROUTINE WILL RETURN THE NEXT
;               FILE IN THE UFD THAT MEETS THE CRITERIA
;               (IF ANY) SET BY UFDWLD AND UFDEXT
;               IF NO FILE IS FOUND THEN AN ERROR IS RETURNED
;
;
;       CALLING SEQUENCE:
;
;               CALL UFDSTP(IERR)
;
;
;       WHERE:
;
;
;               IERR = ERROR CODE RETURNED BY THE SUBROUTINE
;
;
;
;       ERROR CODES
;               00 = SUCCESSFUL
;               01 = NO FILE FOUND
;
;
;.END LITERAL
;.-
UFDSTP: CAIA            ;SUBROUTINE WENTRY,F10 WILL SKIP NEXT INSTRUCTION
                        ;F40 WILL DEPOSIT INTO THIS ADDRESS AND EXECUTE
                        ;NEXT INSTRUCTION.
        PUSH 17,16      ;SAVE RETURN ADDRESS IN AC17 FOR RETURN
        MOVEI   9,0     ;MOVE VALUE OF 0 TO AC 9
        MOVEM   9,@0(16) ;MOVE 0 TO IERR
UFDLP:  MOVE 1,[.CHUFD+^D47] ;OPTION 33 ON CHANNEL 47
        CHANIO  1,UFDARG
        JRST    UFDERR  ;AN ERROR HAS OCCURED
        MOVE    1,COUNT ;PUT VALUE OF COUNT IN AC1
        CAIG    1,0     ;IS AC0 GREATER THAN 0
        JRST    UFDLP   ;NO --> NOT AT END OF UFD(BUT BLANK ENTRY)
        POPJ 17,        ;RETURN
UFDERR: MOVEI   9,1     ;VALUE OF 1 INTO AC9
        MOVEM   9,@0(16) ;PUT AC INTO IERR
        POPJ    17,     ;RETURN
;
;+
;.PG.S 3.LITERAL
;
;                       SUBROUTINE UFDNAM
;
;
;
;       PURPOSE:THIS SUBROUTINE WILL RETURN THE FILE NAME
;               AND EXTENTION THAT UFDSTP HAS LOCATED
;
;
;       CALLING SEQUENCE:
;
;               CALL UFDNAM(NAME)
;
;       WHERE:
;
;
;               NAME = FILENAME.EXT (A 2 WORD ARRAY)
;
;
;
;.END LITERAL
;-
UFDNAM: CAIA            ;SUBROUTINE ENTRY,F10 WILL SKIP NEXT INSTRUCTION
                        ;F40 WILL DEPOSIT INTO THIS ADDRESS AND EXECUTE
                        ;NEXT INSTRUCTION.
        PUSH    17,16   ;SAVE RETURN ADDRESS IN AC17 FOR RETURN
        MOVEI   0,0     ;PUT VALUE OF 0 IN AC0
        MOVEI   1,0     ;PUT VALUE OF 0 IN AC1
        DMOVEM  0,@0(16) ;CLEAR OUT ARRAY
        MOVE    PTR1,[POINT 7,,] ;ASCII POINTER
        MOVE    PTR2,[POINT 6,UFDFNM,] ;SIXBIT POINTER TO NAME
        HRRI    PTR1,@0(16)     ;ASCII POINTER TO ARRAY PASSED
        MOVEI   0,^D6   ;LENGTH OF FILENAME (LOOP)
UFNLOP: ILDB    1,PTR2  ;INCR POINTER AND GET CHARACTER
        CAIN    1,0     ;IS AC1 EQUAL TO A 0
        JRST    ENDUFN  ;IF SO THEN END OF NAME
        ADDI    1," "   ;ADD 40 CONVERT TO SEVEN BIT (ASCII)
        IDPB    1,PTR1  ;PUT ASCII INTO ARRAY
        SOJG    0,UFNLOP ;KEEP GOING
ENDUFN: MOVEI   1,"."
        IDPB    1,PTR1
        MOVEI   0,^D3
        MOVE    PTR2,[POINT 6,UFDFXT,] ;SET POINTER FOR EXT
UFXLOP: ILDB    1,PTR2  ;INCR POINTER AND GET A CHARACTER
        CAIN    1,0     ;IS AC1 EQUAL TO A 0
        JRST    ENDUFX  ;IF SO ALL DONE
        ADDI    1," "   ;CONVERT TO ASCII
        IDPB    1,PTR1  ;DEPOSIT INTO ARRY
        SOJG    0,UFXLOP ;IF MORE DO IT
ENDUFX: POPJ    17,     ;RETURN
;
;+
;.PG.S 3.LITERAL
;
;
;
;                               SUBROUTINE UFDSIZ
;
;
;
;       PURPOSE:THIS SUBROUTINE WILL RETURN THE FILE SIZE
;               IN PAGES
;
;
;       CALLING SEQUENCE:
;
;               CALL UFDSIZ(ISIZE)
;       
;       WHERE:
;
;               ISIZE = THE FILE SIZE IN PAGES(1 WORD)
;
;
;
;.END LITERAL
;-
UFDSIZ: CAIA            ;SUBROUTINE ENTRY,F10 WILL SKIP NEXT INSTRUCTION
                        ;F40 WILL DEPOSIT INTO THIS ADDRESS AND EXECUTE
                        ;THE NEXT INSTRUCTION.
        PUSH    17,16   ;SAVE RETURN ADDRESS IN AC17 FOR RETURN
        MOVE    0,UFDFSZ ;MOVE SIZE IN BLOCKS TO AC0
        IDIVI   0,4     ;DIVIDE BLOCKS BY 4 TO GET PAGES
        MOVEM   0,@0(16) ;SAVE IN WORD
        POPJ    17,
;
;+
;.PG.S 3.LITERAL
;
;
;                       SUBROUTINE UFDLIC
;
;
;       PURPOSE:THIS SUBROUTINE WILL RETURN THE LICENSE
;               WORD
;
;
;       CALLING SEQUENCE:
;
;               CALL UFDLIC(ILIC)
;
;       WHERE:
;
;               ILIC = THE LICENSE BITS (1 WORD)
;
;
;
;.END LITERAL
;.-
UFDLIC: CAIA            ;SUBROUTINE ENTRY,F10 WILL SKIP NEXT INSTRUCTION
                        ;F40 WILL DEPOSIT ITO THIS ADDRESS AND EXECUTE
                        ;THE NEXT INSTRUCTION
        PUSH    17,16   ;SAVE RETURN ADDRESS IN AC17 FOR RETURN
        MOVE    0,UFDFLC ;MOVE LICENSE WORD TO AC0
        MOVEM   0,@0(16) ;PUT LICENSE WORD INTO SCALAR
        POPJ    17,     ;RETURN
;
;+
;.PG.S 3.LITERAL
;
;
;                       SUBROUTINE UFDSTA
;
;
;       PURPOSE:THIS SUBROUTINE WILL RETURN THE STATUS WORD
;
;
;       CALLING SEQUENCE:
;
;               CALL UFDSTA(ISTA)
;
;       WHERE:
;
;               ISTA = THE STATUS BITS(1 WORD)
;
;
;.END LITERAL
;-
UFDSTA: CAIA            ;SUBROUTINE ENTRY,F10 WILL SKIP NEXT INSTRUCTION
                        ;F40 WILL DEPOSIT INTO THIS ADDRESS AND EXECUTE
                        ;NEXT INSTRUCTION.
        PUSH    17,16   ;SAVE RETURN ADDRESS IN AC17 FOR RETURN
        MOVE    0,UFDFST ;LOAD STATUS BITS INTO AC0
        MOVEM   0,@0(16) ;STORE STATUS BITS INTO SCALAR
        POPJ    17,     ;RETURN
;
;+
;.PG.S 3.LITERAL
;
;
;                       SUBROUTINE UFDDAT
;
;
;       PURPOSE:THIS SUBROUTINE WILL RETURN THE DATE AND TIME
;               WHEN THE ZONE IS SUPPLIED FOR THE CURRENT FILE
;
;
;       CALLING SEQUENCE:
;
;
;               CALL UFDDAT(IZONE,IDATE,ITIME,IERR)
;
;
;       WHERE:
;
;
;               ZONE = TIME ZONE AND CHARACTISTICS
;               DATE = DATE IN DAYS SINCE 1964 (TYMSHARE FORMAT)
;               TIME = TIME IN SECONDS SINCE MIDNIGHT 
;               IERR = ERROR CODE RETURNED BY SUBROUTINE
;
;
;       ERROR CODES:
;               00 = SUCESSFUL
;               01 = UNABLE TO CONVERT DATE AND TIME
;
;
;
;.END LITERAL
;-
UFDDAT: CAIA            ;SUBROUTINE ENTRY,F10 WILL SKIP NEXT INSTRUCTION
                        ;F40 WILL DEPOSIT INTO THIS ADDRESS AND EXECUTE
                        ;THE NEXT INSTRUCTION.
        PUSH    17,16   ;SAVE RETURN ADDRESS IN AC17 FOR RETURN
        HLRZ    0,UFDFDT 
        MOVEM   0,DATBLK
        HRRZ    0,UFDFDT
        PUSHJ 17,JIFCNV
        MOVEM 9,@3(16)  ;STORE ERROR CODE VALUE INTO CALLING ROUTINE
        CAIE 9,0        ;IF ERROR EXECUTE NEXT INSTRUCTION
        POPJ 17,        ;RETURN TO CALLING ROUTINE
        MOVEM   0,TIMVAL
        MOVE    0,@0(16)
        ADDI    0,400000 ;STANDARD FORMAT
        HRLI    0,400020 ;GMT STANDARD FORMAT
        MOVEM   0,CONVBT
        MOVEI   0,DATBLK
        DATUUO  0,
        JFCL
        MOVE    0,DATBLK
        MOVEM   0,@1(16)
        MOVE    0,TIMVAL
        IDIV    0,1     ;DIVIDE JIFFIES BY JIFFIES/SECOND TO GET SECONDS
        MOVEM   0,@2(16)
        POPJ    17,
;
;+
;.PG.S 3.LITERAL
;
;
;                       SUBROUTINE UFDSPL
;
;
;       PURPOSE:THIS SUBROUTINE WILL ALLOW THE USER
;               TO SPECIFY A FILE OR WILD CARD TO BE SEARCHED FOR
;
;
;       CALLING SEQUENCE:
;
;
;               CALL UFDSPL(INAME,IEXT)
;
;
;       WHERE:
;
;
;               INAME = THE NAME OF THE FILE TO BE SEARCHED FOR
;               IEXT  = THE EXTENTION OF THE FILE TO BE SEARCHED FOR
;
;
;       NOTE:
;               WILDCARD SYMBOLS MAY BE USED IN THIS ROUTINE
;
;
;
;.END LITERAL
;-
UFDSPL: CAIA            ;SUBROUTINE ENTRY,F10 WILL SKIP NEXT INSTRUCTION
                        ;F40 WILL DEPOSIT INTO THIS ADDRESS AND EXECUTE
                        ;THE NEXT INSTRUCTION.
        PUSH    17,16   ;SAVE RETURN ADDRESS IN AC17 FOR RETURN
        MOVEI   0,0            ;MOVE VALUE OF 0 INTO AC0
        MOVEM   0,UFDSNM        ;CLEAR NAME AREA
        MOVEM   0,UFDSXT        ;CLEAR EXTENTION AREA
        MOVEM   0,UFDSSN        ;CLEAR STARTING NAME
        MOVEM   0,UFDSSX        ;CLEAR STARTING EXTENTION
        MOVE    PTR1,[POINT 7,,] ;ASCII POINTER TO FN
        MOVE    PTR2,[POINT 6,UFDSNM,] ;SIXBIT POINTER TO FN
        HRRI    PTR1,@0(16)  ;SET POINTER TO 1ST SCALAR (FN)
        PUSHJ   17,SOLVE        ;JUMP TO CONVERSION ROUTINE
        MOVE    PTR1,[POINT 7,,] ;ASCII POINTER TO FN
        MOVE    PTR2,[POINT 6,UFDSXT,] ;SIZBIT POINTER TO FN
        HRRI    PTR1,@1(16)     ;SET POINTER TO 2ND SCALAR (FX)
        PUSHJ   17,SOLVE        ;JUMP TO CONVERSION ROUTINE
        POPJ    17,             ;RETURN
SOLVE:  MOVEI   0,^D6           ;LOOP 6 TIMES
SIXLOP: ILDB    1,PTR1          ;GET AN ASCII CHAR
        CAIN    1,0             ;IF YOU ARE DONE
        JRST    ENDSOL          ;THEN END THE LOOP
        SUBI    1," "           ;CONVERT ASCII TO SIXBIT
        IDPB    1,PTR2          ;PUT SIXBIT INTO LOCATION
        SOJG    0,SIXLOP        ;IF AC0 = 0 THEN DONT LOOP
ENDSOL: POPJ    17,             ;RETURN
;
;+
;.PG.S 3 LITERAL
;
;                       SUBROUTINE DATCNV
;
;       PURPOSE:THIS SUBROUTINE CONVERTS THE TYMSHARE FILE DATE FROM UFDDAT
;               TO THE CORRECT MONTH DAY AND YEAR FOR THE TIME ZONE
;               SPECIFIED BY THE CALLING ROUTINE
;
;       CALLING SEQUENCE:
;
;               CALL DATCNV(IDATE,ITIME,IZONE,IMO,IDAY,IYR,IERR)
;
;       WHERE:
;
;               IDATE = THE DATE RETURNED FROM UFDDAT
;               ITIME = THE TIME IN SECONDS PAST MIDNIGHT RETURNED FROM
;                       UFDDAT
;               IZONE = THE TIME ZONE DATE AND TIME ARE TO BE
;               IMO   = THE MONTH AS AN INTEGER VALUE FROM 1 - 12
;               IDAY  = THE DAY AS AN INTEGER VALUE FROM 1 - 31
;               IYR   = THE YEAR AS AN INTEGER VALUE FROM 1964 - 2000
;               IERR  = ERROR CODE RETURNED BY SUBROUTINE
;
;       TIME ZONES:
;
;                6 = HST
;                8 = PST
;                9 = MST
;               10 = CST
;               11 = EST
;               12 = AST
;               16 = GMT
;               17 = PARIS
;               25 = TOKYO
;               26 = BRISBANE
;                       ADD THE VALUE 32 TO THE TIME ZONE IF SUBJECT TO 
;                       DAYLIGHT SAVINGS TIME
;
;       ERROR CODE:
;
;               00 = SUCESSFUL
;               01 = UNSUCESSFUL
;
;.END LITERAL
;-
DATCNV: CAIA            ;SUBROUTINE ENTRY,F10 WILL SKIP NEXT INSTRUCTION
                        ;F40 WILL DEPOSIT INTO THIS ADDRESS AND EXECUTE
                        ;THE NEXT INSTRUCTION.
        PUSH 17,16      ;SAVE RETURN ADDRESS IN AC17 FOR RETURN
        MOVE 0,@0(16)   ;GET DATE FROM CALLING PROGRAM
        MOVEM 0,DATBLK  ;STORE INTO DATE BLOCK
        MOVE 0,@1(16)   ;GET TIME FROM CALLING PROGRAM
        PUSHJ 17,JIFCNV ;CONVERT SECONDS FROM MIDNIGHT TO JIFFIES FROM MIDNIGHT
        MOVEM 9,@6(16)  ;STORE ERROR CODE VALUE INTO CALLING PROGRAM
        MOVEM 0,TIMVAL  ;STORE JIFFIES FROM MIDNIGHT INTO TIMVAL
        MOVE 0,@2(16)   ;TGET TIME ZONE FOR DATE AND TIME FROM CALLING PROGRAM
        HRRM 0,CONVBT   ;SET UP NEW BITS IN DEC FORMAT FOR DATUUO
        ADDI 0,400000   ;ADD IN THE VALUE FOR STANDARD FORMAT
        HRLM 0,CONVBT   ;SET UP OLD BITS FOR DATUUO
        MOVEI 0,DATBLK  ;STORE ADDRESS OF DATBLK IN AC0
        DATUUO  0,      ;CONVERT DATE AND TIME
        JFCL            ;ONLY RETURN
        MOVE 0,DATBLK   ;GET DATE AND STORE IN AC0
        MOVE 1,DATBLK   ;GET DATE AND STORE IN AC1
        IDIVI 1,^D31    ;INTEGER DIVIDE DATE BY 31 RESULT IN AC1
        MOVN 2,1        ;STORE INTEGER NO. OF 31 DAY MONTHS IN AC2
        IMULI 2,^D31    ;MULTIPLY AC2 BY 31
        ADDI 2,1        ;ADD ONE TO AC2
        ADDM 0,2        ;ADD THE TWO TOGETHER AND STORE INTO AC2 IDAY
        MOVEM 2,@4(16)  ;RETURN DAY VALUE
        MOVN 2,1        ;STORE INTEGER NO. OF 31 DAY MONTHS IN AC2
        IDIVI 2,^D12    ;DIVIDE AC2 BY 12 AND STORE RESULT INTO AC2
        IMULI 2,^D12    ;MULTIPLY AC2 BY 12
        ADDI 2,1        ;ADD 1 TO AC2
        ADDM 1,2        ;ADD INTEGER NO. OF 31 DAY MONTH INTO AC2 IMO
        MOVEM 2,@3(16)  ;STORE IMO INTO CALLING PROGRAM
        MOVE 2,1       ;STORE INTEGER NO. OF 31 DAY MONTHS IN AC2
        IDIVI 2,^D12     ;DIVIDE AC2 BY 12
        ADDI 2,^D1964   ;ADD 1964 TO AC2
        MOVEM 2,@5(16)  ;STORE YEAR INTO CALLING PROGRAM
        POPJ 17,        ;RETURN TO CALLING PROGRAM
JIFCNV: MOVE 1,[26,,11] ;PREPARE AC1 TO GET NO. OF JIFFIES PER SECOND
        GETTAB 1,       ;GET NO. OF JIFFIES PER SECOND AND STORE IN AC1
        JRST JIFERR     ;GO TO ERROR ROUTINE ON UNSUCCESSFUL GETTAB
        MOVEI 9,0       ;ESTABLISH SUCCL ERROR CODE
        MOVEM 9,@3(16)  ;STORE ERROR CODE VALUE INTO CALLING PROGRAM
        IMULM 1,0       ;MULTIPLY JIFFIES/SECOND BY SECONDS FROM UFD AND STORE
                        ;INTO AC0
        POPJ 17,        ;RETURN TO CALLING ROUTINE
JIFERR: MOVEI 9,1
        POPJ 17,        ;RETURN TO CALLING ROUTINE
        END
A@mæ